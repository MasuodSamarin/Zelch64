!--------------------------------------------------
!- Friday, May 19, 2017 9:55:38 PM
!- Import of : 
!- c:\src\zelch64\f1.prg
!- Commodore 64
!--------------------------------------------------
6 POKE721,70
7 IFPEEK(831)=1THENPOKE831,0:GOTO5590
8 IFPEEK(831)=2THENPOKE831,0:GOTO7120
9 IFSG=0THENGOSUB1600
10 GOTO1000
100 IN$="":SYS49152
110 IFPEEK(830)=1THEN500
120 RETURN
200 IFOU$=""THENRETURN
210 POKE254,ASC(OU$)
220 SYS49155
230 RETURN
300 I$="":SYS49158
310 IFPEEK(830)=1THEN500
320 RETURN
400 IFTX$=""THENRETURN
420 SYS49161,TX$
430 RETURN
500 GOSUB1900:RL=2520:TX$="Standby...":GOSUB400:A=FRE(0)
510 LOAD"{pound}@1",8,1
600 GOSUB1900:TX$="Standby...":GOSUB400:A=FRE(0)
610 LOAD"{pound}b1",8,1
1000 CS=0:TX$="{clear}Entering Sig System":GOSUB400:GOSUB1010:GOTO2000
1010 TX$="{down*3}Entering Sig"+STR$(CS+1):GOSUB400:CM=0:FORI=0TOGG:Z1(I)=0:Z2(I)=0
1020 NEXTI:OPEN8,SD(CS),8,"{pound}sigmsg"+STR$(CS)+",s,r":SYS49164
1030 CLOSE8
1040 OPEN1,8,15:OPEN2,8,2,"{pound}index.File"
1050 PRINT#1,"p"CHR$(98)CHR$(CS+2)CHR$(0)CHR$(1)
1060 INPUT#2,I1$
1070 NM=255-(ASC(I1$)):CLOSE1:CLOSE2
1099 CLOSE1:CLOSE2
1100 OPEN8,SD(CS),8,"{pound}Sig"+STR$(CS)+",s,r"
1120 FORI=1TONM:INPUT#8,A$:Z1(I)=VAL(A$)
1130 INPUT#8,A$:Z2(I)=VAL(A$):NEXTI:CLOSE8
1135 K=0:FORI=1TONM:K=K+Z1(I):NEXTI
1140 TX$=CHR$(13)+STR$(NM)+" Bulletins and "+STR$(K)+" Responses":GOSUB400
1199 VF=0:RETURN
1600 OPEN8,8,8,"{pound}Fpreps,s,r"
1610 INPUT#8,SG:DIMSG$(SG),SG(SG),SD(SG),SF(SG):FORI=0TOSG
1620 INPUT#8,SG$(I):INPUT#8,SG(I):INPUT#8,SD(I):INPUT#8,SF(I):NEXTI
1630 CLOSE8:PP=VAL(P$(0))+256*VAL(P$(1))
1640 OPEN1,8,15:OPEN2,8,2,"{pound}index.File"
1650 PRINT#1,"p"CHR$(98)CHR$(1)CHR$(0)CHR$(1):INPUT#2,HN$:HN=VAL(HN$):
1660 CLOSE1:CLOSE2
1665 I1=INT(HN/256):I2=I1*256:I2=HN-I2:P$(0)=STR$(I2):P$(1)=STR$(I1)
1670 GG=0:FORQ=0TOSG:IFSF(Q)>GGTHENGG=SF(Q)
1680 NEXT:DIMZ1(GG),Z2(GG):RETURN
1800 OPEN1,8,15:OPEN2,8,2,"{pound}index.File"
1810 PRINT#1,"p"CHR$(98)CHR$(1)CHR$(0)CHR$(1)
1820 I1$=STR$(HN)
1840 PRINT#2,I1$:CLOSE1:CLOSE2:RETURN
1900 IFVF=0THENRETURN
1901 TX$=CHR$(13)+"Saving Msg Index...":GOSUB400
1910 OPEN1,SD(CS),15,"s0:{pound}Sig"+STR$(CS):CLOSE1
1915 OPEN8,SD(CS),8,"{pound}Sig"+STR$(CS)+",s,w"
1935 FORI=1TONM:PRINT#8,STR$(Z1(I)):PRINT#8,STR$(Z2(I)):NEXTI
1940 CLOSE8:RETURN
2000 IFWI=1THENCM=CM+1:GOTO6020
2005 TX$=CHR$(14)+CHR$(13)+"{white}Sig"+STR$(CS+1)+":":GOSUB400
2010 GOSUB100:IFIN$=""THEN2010
2020 TX$=IN$+CHR$(13):GOSUB400
2030 IFIN$="?"THEN12000
2040 IFIN$="l"THEN3000
2050 IFIN$="c"THEN4000
2060 IFIN$="r"THEN5000
2070 IFIN$="n"THEN6000
2071 IFIN$="q"THEN600
2075 IFIN$="p"THEN7000
2076 IFIN$=CHR$(13)THENCM=CM+1:GOTO5015:GOTO2000
2080 GOTO2000
3000 TX$="{clear}Current Sigs Are:{down*2}":GOSUB400
3009 FORI=0TOSG
3010 IFSG(I)>ACTHEN3030
3020 TX$=STR$(I+1)+" "+SG$(I):GOSUB400
3030 NEXTI:GOTO2000
4000 TX$="New Sig #:":GOSUB400:GOSUB300:IFLEN(I$)>4THEN2000
4001 II=VAL(I$):IFII-1=CSORII<1ORII-1>SGTHEN2000
4010 IFII=0ORII-1>SGTHEN2000
4020 IFSG(II-1)>ACTHEN2000
4030 GOSUB1900:CS=II-1:GOSUB1010:GOTO2000
5000 TX$="File # or RETURN:":GOSUB400:GOSUB300:IFLEN(I$)>4ORI$=""THEN2000
5010 I=VAL(I$):CM=I
5015 IFCM<1THENCM=1:GOTO5030
5020 IFCM>NMTHENTX$="No Message #"+STR$(CM):CM=NM:WI=0:GOSUB400:GOTO2000
5030 I1=Z1(CM):I2=Z2(CM)
5040 OPEN8,SD(CS),8,"{pound}msg"+STR$(CS)+STR$(CM)
5050 SYS49164
5060 CLOSE8:IFI1=0THEN5500
5070 TX$=CHR$(14)+CHR$(13)+"Replys:(?):":GOSUB400
5080 GOSUB100:IFIN$=""THEN5080
5085 IFTX$=IN$THENGOSUB400
5090 IFIN$="s"THEN5900
5091 IFIN$="?"THENTX$=CHR$(13)+"R)ead, S)kip, or Q)uit":GOSUB400:GOTO5070
5100 IFIN$="q"THEN5500
5110 IFIN$="r"ORIN$=CHR$(13)THENI9=0:GOTO5200
5120 GOTO5070
5200 OPEN8,SD(CS),8,"{pound}msg"+STR$(CS)+STR$(CM):SYS49170
5201 IFI9=0THEN5220
5202 FORI=1TOI9:SYS49170:NEXT
5210 IFI9=I1THENCLOSE8:GOTO5500
5220 TX$="{clear}Response #"+STR$(I9+1):GOSUB400:SYS49164:CLOSE8
5230 TX$=CHR$(13)+CHR$(14)+"RETURN cont. A)bort:":GOSUB400
5240 GOSUB100:IFIN$="a"THEN5500
5250 IFIN$<>CHR$(13)THEN5240
5255 TX$=" ":GOSUB400
5260 I9=I9+1:GOTO5200
5500 TX$=CHR$(14)+CHR$(13)+"Public Reply?:":GOSUB400
5510 GOSUB100:IFIN$="n"THEN2000
5515 IFIN$=CHR$(13)THEN2000
5517 IFIN$="q"THENWI=0:GOTO2000
5520 IFIN$<>"y"THEN5510
5530 QQ$="REPLY BY>"+NA$+" ID#"+STR$(NA):GOSUB10000
5540 QD$="DATE>"+STR$(MN)+"/"+STR$(DA)+" TIME>"+TT$+CHR$(13)+CHR$(13)+CHR$(13)
5550 FI$="{pound}msg"+STR$(CS)+STR$(CM):GOSUB5555:GOTO5570
5555 FORI=1TOLEN(FI$)
5560 POKE681+I,ASC(MID$(FI$,I,1)):NEXTI:POKE681+I,0:V1=SD(CS):RETURN
5570 POKE831,1:TX$="Standby...":GOSUB400
5580 LOAD"{pound}m1",8,1
5590 IFPEEK(763)=1THEN2000
5600 VF=1:TX$="Fixing...":GOSUB400
5610 Z1(CM)=Z1(CM)+1:Z2(NM)=HN+1:HN=HN+1:GOSUB1800:GOTO2000
5900 TX$=CHR$(13)+"Number to skip:":GOSUB400:GOSUB300:IFLEN(I$)>4ORI$=""THEN5070
5920 I9=VAL(I$):IFI9>Z1(CM)ORI9<1THEN5500
5930 GOTO 5200
6000 CM=1:WI=0
6020 IFZ2(CM)>PPTHENWI=1:GOTO5020
6030 CM=CM+1:IFCM>NMTHENTX$=CHR$(13)+"No More New":GOSUB400:WI=0:GOTO2000
6040 GOTO 6020
7000 TX$="Post a Message?:":GOSUB400
7010 GOSUB100:IFIN$="n"THEN2000
7020 IFIN$<>"y"THEN7010
7030 QQ$="{clear}POSTED BY>"+NA$+" ID#"+STR$(NA)+CHR$(13)+"ON>"+STR$(MN)+"/"+STR$(DA)
7040 VF=1:TX$=CHR$(13)+"Subject>:":GOSUB400:GOSUB300
7050 IFI$=""THEN2000
7060 GOSUB10000:QD$="TIME>"+TT$+" SUBJECT>:"+I$+CHR$(13)+CHR$(13)
7070 IFSF(CS)=NMTHENTX$="Cycling Msgs...":GOSUB400:GOSUB7900:GOTO7090
7080 NM=NM+1
7090 FI$="{pound}msg"+STR$(CS)+STR$(NM):GOSUB5555
7100 TX$="Standby...":GOSUB400
7110 POKE831,2:LOAD"{pound}m1",8,1
7120 IFPEEK(763)=1THENNM=NM-1:GOTO2000
7121 TX$="Fixing...":GOSUB400
7130 GOSUB9000:Z1(NM)=0:Z2(NM)=HN+1:HN=HN+1:GOSUB1800:GOTO2000
7900 OPEN8,SD(CS),15,"s0:{pound}msg"+STR$(CS)+STR$(1)
7901 FORX=1TONM
7902 PRINT#8,"r0:{pound}msg"STR$(CS)STR$(X)"={pound}msg"STR$(CS)STR$(X+1)
7903 NEXTX:GOSUB11000:CLOSE8:RETURN
9000 OPEN1,8,15:OPEN2,8,2,"{pound}index.File"
9010 PRINT#1,"p"CHR$(98)CHR$(CS+2)CHR$(0)CHR$(1):PRINT#2,CHR$(255-NM)
9020 CLOSE1:CLOSE2:RETURN
10000 H=PEEK(56331):M=PEEK(56330):S=PEEK(56329):S=PEEK(56328)
10010 IFH>128THENCH=128
10020 H1=(HAND16)/16:H2=(HAND15):M1=(MAND240)/16:M2=(MAND15)
10030 H1$=RIGHT$(STR$(H1),1):H2$=RIGHT$(STR$(H2),1)
10040 M1$=RIGHT$(STR$(M1),1):M2$=RIGHT$(STR$(M2),1)
10050 IFH1=0THENTT$=H2$+"]"+M1$+M2$:RETURN
10060 TT$=H1$+H2$+"]"+M1$+M2$:RETURN
11000 REM CYCLE
11010 FORI=1TONM:Z1(I)=Z1(I+1):Z2(I)=Z2(I+1):NEXTI
11060 RETURN
12000 OU$="{clear}":GOSUB200
12010 OPEN8,8,8,"{pound}msgmenu,s,r"
12020 SYS49164
12030 CLOSE8:GOTO2000
