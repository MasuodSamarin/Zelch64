!--------------------------------------------------
!- Friday, May 12, 2017 11:09:19 PM
!- Import of : 
!- c:\src\zelch64\bak\chess.prg
!- Commodore 128 BASIC 7/7.1
!--------------------------------------------------
8000 SYS51983
8010 TX$="{f1}{down*2}ZELCH ON-LINE{sh space}CHESS! (V1.1a)":GOSUB5:I1=0:IFAC<9THEN8060
8030 TX$="{f1}EDITOR(Y/N)?{ct c}":GOSUB5:C$="yn":GOSUB66:IFG=1THEN9000
8060 TX$="{f1}ENTER YOUR GAME CODE:{ct c}":GOSUB5:GOSUB7:A$=I$:IFI$=""ORI$="   "THEN2000
8100 TX$=CR$+"{down*3}{f1}CHECKING{sh space}FOR YOUR GAME STATUS...":GOSUB5:OPEN2,8,2,"{pound}chess"
8110 INPUT#8,E:IFE>0THENTX$="{f1}GAME ERROR.":GOSUB5:CLOSE2:GOTO2000
8120 U=1:V=0:FORY=1TO10:I=0:U=Y:I$="":GOSUB4
8130 IFA$=I$THENI1=Y
8140 NEXTY:IFI1=0THENTX$="{f1}ILLEGAL GAME CODE!":CLOSE2:GOSUB5:GOTO2000
8150 U=I1:GOSUB3:FORI=96TO100:INPUT#2,TT$(I):NEXTI:CLOSE2
8160 IFNA<>VAL(TT$(97))ANDNA<>VAL(TT$(98))THENE$="chess cheat":GOSUB9:GOTO1500
8170 IFNA<>VAL(TT$(99))THENTX$="{f1}IT'S NOT{sh space}YOUR{sh space}TURN YET.":GOSUB5:GOTO2000
8180 TX$="{f1}OK, IT'S YOUR MOVE! LOADING GAME...":GOSUB5:IFGM=0THENDIMCH$(1,7,7)
8190 OPEN2,DV(1,NP),2,"{pound}chess"+STR$(I1)+",s,r":FORI=0TO1:FORX=0TO7:FORY=0TO7
8195 INPUT#2,CH$(I,X,Y):IFCH$(I,X,Y)=""THENCH$(I,X,Y)="  "
8197 NEXTY:NEXTX:NEXTI:GM=1:CLOSE2:GOSUB8400
8200 ER=0:TX$=CR$+"{f1}CHESS COMMAND(?):{ct c}":GOSUB5:C$="pcqrm?d":GOSUB66
8220 IFG=1THENI=0:GOSUB8300:GOTO8200
8230 IFG=2THENI=1:GOSUB8300:GOTO8200
8240 ONG-2GOTO8610,8400,8340,8270,8600
8270 TX$="{down}{f1}Q)UIT TO{sh space}COMMAND."+CR$+"{f1}R)EAD YOUR{sh space}GAME NOTE":GOSUB5
8280 TX$="{f1}P)REVIOUS{sh space}GAME BOARD"+CR$+"{f1}C)URRENT GAME{sh space}BOARD":GOSUB5
8290 TX$="{f1}M)OVE":GOSUB5:TX$="{f1}D)ECLARE{sh space}AN{sh space}ILLEGAL{sh space}MOVE":GOSUB5:GOTO8200
8300 TX$="  {ct c}":GOSUB5:FORX=1TO8:TX$=STR$(X)+" {ct c}":GOSUB5:NEXTX:Y$=""
8305 Q$="  !--!--!--!--!--!--!--!--!":TX$=CR$+Q$:GOSUB5:FORX=0TO7
8310 A$=STR$(X+1):FORY=0TO7:A$=A$+"!"+CH$(I,X,Y):NEXTY:TX$=A$+"!":GOSUB5
8320 TX$=Q$:GOSUB5:NEXTX:A=FRE(0):RETURN
8340 TX$="{f1}REMEMBER! ALL{sh space}MOVES ARE FINAL!":GOSUB5:I=1:GOSUB8300
8350 TX$="{f1}FROM{sh space}COLUMN #{ct c}":GOSUB5:GOSUB30:GOSUB8398:IFI=0THEN8200
8360 Y=I:TX$="{f1}FROM ROW    #{ct c}":GOSUB5:GOSUB30:GOSUB8398:IFI=0THEN8200
8370 X=I:IFTT$(96+VAL(CH$(1,X-1,Y-1)))<>TT$(99)THENI=0:GOSUB8398:GOTO8200
8380 TX$="{f1}TO{sh space}COLUMN #{ct c}":GOSUB5:GOSUB30:GOSUB8398:IFI=0THEN8200
8382 B=I:TX$="{f1}TO{sh space}ROW #{ct c}":GOSUB5:GOSUB30:GOSUB8398:IFI=0THEN8200
8383 A=I:IFTT$(96+VAL(CH$(1,A-1,B-1)))=TT$(99)THENI=0:GOSUB8398:GOTO8200
8384 TX$="{f1}FROM ("+STR$(Y)+","+STR$(X)+") - TO ("+STR$(B)+","+STR$(A)+")"
8386 GOSUB5:TX$="ARE{sh space}YOU{sh space}SURE (Y/N)?{ct c}":GOSUB5:C$="yn":GOSUB66:IFG=2THEN8200
8388 A=A-1:B=B-1:X=X-1:Y=Y-1
8390 FORI2=0TO7:FORI3=0TO7:CH$(0,I2,I3)=CH$(1,I2,I3):NEXTI3:NEXTI2
8392 CH$(1,A,B)=CH$(1,X,Y):CH$(1,X,Y)="    ":TX$="{f1}MOVE{sh space}MADE!":GOSUB5:ER=1:GOTO8450
8398 IFI=0ORI>8THENTX$="{f1}ILLEGAL COORDINATE OR BLOCK":GOSUB5:I=0
8399 RETURN
8400 GOSUB8410:TX$=TT$(VAL(TT$(100))):GOSUB5:GOTO8200
8410 IFTT$(0)="{f1}GOOD MOVE"THENRETURN
8415 TT$(0)="{f1}GOOD MOVE.":TT$(1)="{f1}BAD MOVE.":TT$(2)="{f1}I'VE{sh space}GOT{sh space}YOU NOW!"
8420 TT$(3)="{f1}OOPS.":TT$(4)="{f1}HA HA HA!!":TT$(5)="{f1}CHECK."
8430 TT$(6)="{f1}CHECK MATE!":TT$(7)="{f1}CRAP!"
8440 TT$(8)="{f1}YOUR MOVE.":TT$(9)="{f1}YOUR MOVE WAS ILLEGAL, MOVE AGAIN.":RETURN
8450 GOSUB8410:TX$="{down*2}{f1}COMMENT NOTES:":GOSUB5
8455 FORI=0TO8:TX$="{white}("+STR$(I+1)+" )"+TT$(I):GOSUB5:NEXTI
8460 TX$="{down}PICK A{sh space}COMMENT NOTE:{ct c}":GOSUB5:GOSUB30:IFI<1ORI>9THEN8460
8470 TT$(100)=STR$(I-1):IFI=10THENGOSUB8700:ER=1
8480 IFER=1ANDTT$(99)=TT$(97)THENTT$(99)=TT$(98):GOTO8500
8490 IFER=1THENTT$(99)=TT$(97)
8500 TX$="{f1}{down*2}RECORDING YOUR CODES...":GOSUB5:OPEN2,8,2,"{pound}chess"
8510 A$="":U=I1:V=0:I=0:GOSUB3:FORI=96TO100:A$=A$+TT$(I)+CR$:NEXTI
8520 PRINT#2,A$:CLOSE2:TX$="{f1}{down}RECORDING THE GAME BOARDS...":GOSUB5
8530 F$="{pound}chess"+STR$(I1):D=DV(1,NP):GOSUB22:OPEN2,D,2,F$+",s,w":FORI=0TO1
8540 FORX=0TO7:FORY=0TO7:PRINT#2,CH$(I,X,Y):NEXTY:NEXTX:NEXTI:CLOSE2
8590 TX$="{down}{f1}CLEARING GAME...":GOSUB5:FORI=0TO1:FORX=0TO7:FORY=0TO7:CH$(I,X,Y)=""
8595 NEXTY:NEXTX:NEXTI:FORI=0TO10:TT$(I)="":TT$(90+I)="":NEXTI:A=FRE(0):GOTO2000
8600 TX$="{f1}ARE{sh space}YOU{sh space}SURE (Y/N)?{ct c}":GOSUB5:C$="yn":I=10:GOSUB66:ONGGOTO8470,8200
8610 TX$="{f1}QUIT! ARE{sh space}YOU{sh space}SURE (Y/N)?{ct c}":GOSUB5:C$="yn":GOSUB66:ONGGOTO8590,8200
8700 FORX=0TO7:FORY=0TO7:CH$(1,X,Y)=CH$(0,X,Y):NEXTY:NEXTX:RETURN
9000 TX$="{f1}{down*2}C)REATE A NEW CHESS{sh space}FILE":GOSUB5:TX$="{f1}D)ELETE A PLAYER FILE"
9010 GOSUB5:TX$="{f1}E)DIT A{sh space}PLAYER FILE":GOSUB5:TX$="{f1}Q)UIT{sh space}TO{sh space}COMMAND":GOSUB5
9015 TX$="{f1}L)IST GAME{sh space}FILES":GOSUB5
9020 TX$="{down}{f1}CHOICE:{ct c}":GOSUB5:C$="qcdle":GOSUB66:ONGGOTO9070,9100,9200,9400,9300
9070 A=FRE(0):GOTO2000
9100 TX$="{f1}ARE{sh space}YOU{sh space}SURE(Y/N)?{ct c}":GOSUB5:C$="ny":GOSUB66:ONGGOTO9000
9110 TX$=IN$+CR$+"{f1}WORKING...":GOSUB5:PRINT#8,"s0:{pound}chess*":PRINT#8,"i0"
9120 OPEN2,8,2,"{pound}chess,l,"+CHR$(30):V=0:I=0:FORX=1TO2:FORU=1TO10
9130 GOSUB3:PRINT#2,"   ":NEXTU:NEXTX:CLOSE2:GOTO9000
9200 TX$="{f1}WHICH ONE(1-10)?{ct c}":GOSUB5:GOSUB30:IFI=0ORI>10THEN9000
9210 Y=I:TX$="{f1}WORKING...":GOSUB5:OPEN2,8,2,"{pound}chess":U=Y:V=0
9220 I=0:GOSUB3:PRINT#2,"   ":CLOSE2:PRINT#8,"s0:{pound}chess"+STR$(Y)
9230 GOTO9000
9300 TX$="{f1}WHICH ONE(1-10)?{ct c}":GOSUB5:GOSUB30:IFI=0ORI>10THEN9000
9310 Y=I:OPEN2,8,2,"{pound}chess":U=Y:V=0:I=0:GOSUB4:CLOSE2
9320 IFI$<>"   "THENTX$="{f1}GAME{sh space}IN{sh space}PROGRESS HERE.":GOSUB5:GOTO9000
9330 TX$="{f1}ENTER CHESS{sh space}CODE:{ct c}":GOSUB5:GOSUB7:IFI$=""THEN9000
9340 A$=I$:TX$="{f1}ENTER PLAYER 1's ID#{ct c}":GOSUB5:GOSUB30:IFI=0THEN9000
9350 A=I:TX$="{f1}ENTER PLAYER 2's ID#{ct c}":GOSUB5:GOSUB30:IFI=0ORA=ITHEN9000
9360 TX$="{f1}WORKING...":GOSUB5:OPEN2,8,2,"{pound}chess":U=Y:V=0:B=I:I=0
9370 GOSUB3:A$=A$+CR$+STR$(A)+CR$+STR$(B)+CR$+STR$(A)+CR$+STR$(8):PRINT#2,A$
9380 CLOSE2:GOTO9500
9400 TX$="{f1}SCANNING...":GOSUB5:OPEN2,8,2,"{pound}chess":V=0:I=0
9410 FORU=1TO10:GOSUB4:TX$=STR$(U)+")":IFI$="   "THENTX$=TX$+"EMPTY":GOTO9430
9420 TX$=TX$+I$:INPUT#2,A$,B$:TX$=TX$+"  #"+A$+"  #"+B$
9430 GOSUB5:NEXTU:CLOSE2:A=FRE(0):GOTO9000
9500 F$="{pound}chess"+STR$(Y):D=DV(1,NP):GOSUB22
9505 OPEN2,D,2,F$+",s,w":FORX=1TO2:PRINT#2,"1c"+CR$+"1k"
9510 PRINT#2,"1b"+CR$+"1K"+CR$+"1Q"+CR$+"1b"+CR$+"1k"+CR$+"1c":FORI=1TO8
9520 PRINT#2,"1p":NEXTI:FORI=1TO32:PRINT#2,"  ":NEXTI:FORI=1TO8:PRINT#2,"2p"
9525 NEXTI:PRINT#2,"2c"+CR$+"2k"+CR$+"2b"+CR$+"2K"+CR$+"2Q"+CR$+"2b"+CR$+"2k"
9530 PRINT#2,"2c":NEXTX:CLOSE2:TX$="{f1}DONE!":GOSUB5:A=FRE(0):GOTO9000
