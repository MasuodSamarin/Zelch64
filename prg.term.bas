!--------------------------------------------------
!- Saturday, May 20, 2017 4:57:55 PM
!- Import of : 
!- c:\src\zelch64\prg.term.prg
!- Commodore 64
!--------------------------------------------------
8000 FORI=0TO9:POKE43+I,PEEK(721+I):NEXT
8010 IFM0>0THEN8050
8020 PRINT:PRINT"3)00 or 1)200 baud:{reverse on} {reverse off}{left}";:
8030 GETA$:IFA$="3"THENPRINT"3":PRINT"300 baud":GOSUB51:GOTO8050
8040 IFA$<>"1"THEN8030
8045 PRINT"1":PRINT"1200 baud":GOSUB49
8050 POKE681,1
8060 IFPEEK(681)=1THENPRINT"{white}GRAPHICS term Active:":GOTO8070
8065 PRINT"{light green}ASCII term active:"
8070 POKE53269,1:PRINTCHR$(14):SYS38236:PRINTCHR$(14)
8080 POKE53269,0:PRINT"{clear}{white}{down*2}----{pink}Z{yellow}e{white}l{cyan}c{light blue}h {white}ZAP{sh space}TERM {light green}v2.0 {green}Command Menu{white}----"
8090 PRINT"{red}           Q)UIT BACK{sh space}TO{sh space}BBS{sh space}MODE"
8100 IFPEEK(659)=6THENPRINT"{pink}           B)AUD{sh space}CHANGE (300-500)"
8110 PRINT"{yellow}           U)PLOAD{sh space}A{sh space}FILE":PRINT"{white}           D)OWNLOAD{sh space}A{sh space}FILE"
8130 PRINT"{cyan}           T)ERMINAL{sh space}MODE":PRINT"{light blue}           R)ESET{sh space}TERM"
8136 PRINT"{blue}           $) DIRECTORY (8)":PRINT"{purple}           S)END{sh space}SEQ{sh space}FILE"
8138 PRINT"{red}           @) DISK CHANNEL (8)":PRINT"{down*2}{white}CHOOSE:{reverse on} {reverse off}{left}";
8150 POKE720,1:POKE714,1:I=90:GOSUB20:POKE830,0:C$="qbu$dtrs@":GOSUB66:POKE714,0
8155 POKE720,0:IFG=1THENGOSUB51:GOSUB37:A$="x1e0m0v0":GOSUB1:GOTO500
8160 G=G-1:ONGGOTO8200,8300,9010,8400,8060,8010,9500,9700
8200 IFPEEK(659)<>6THEN8100
8205 INPUT"NEW{sh space}BAUD";I:IFI<200ORI>500THENPRINT"ILLEGAL BAUD RATE":GOTO8080
8210 FH=1:GOSUB3040:GOTO8060
8300 I1=1:GOSUB8500:IFER=1THEN8060
8310 FH=1:OPEN2,8,2,"0:"+A$+","+B$+",r":INPUT#8,E
8320 IFE>0THENCLOSE2:GOTO8060
8330 PRINT"{clear}UPLOAD:"+A$:A$="g":GOTO2940
8400 GOSUB8500:IFER=1THEN8060
8410 UV(CU)=8:FH=1:FI$=A$+","+B$+",w":PRINT"{clear}DOWNLOAD:"+A$:A$="g":GOTO2832
8500 ER=0:INPUT"FILENAME";A$:IFLEN(A$)>16ORA$=""THENER=1:RETURN
8510 FORI=1TOLEN(A$):B$=MID$(A$,I,1):IFI1=1THEN8530
8515 IFB$=","ORB$=":"ORB$="*"THENER=1
8520 IFB$="$"ORB$="?"ORB$=";"THENER=1
8530 NEXT:IFER=1THENRETURN
8540 INPUT"P)RG/S)EQ";B$:IFB$<>"p"ANDB$<>"s"THENER=1:RETURN
8550 IFB$="p"THENTY=1
8560 IFB$="s"THENTY=2
8570 RETURN
9000 GOTO8060
9010 POKE720,1:PRINT"{clear}{white}";:
9020 POKE253,0:OPEN3,8,0,"$0:*":SYS38224:CLOSE3:PRINT:PRINT"{white}Hit A Key"
9030 GETA$:IFA$=""THEN9030
9040 POKE720,0:GOTO8080
9500 PRINT:INPUT"FILENAME";FI$:PRINTFI$;" CORRECT?(y/n)";
9510 GETA$:IFA$="n"THEN8080
9520 IFA$<>"y"THEN9510
9530 PRINT:PRINT"sending...":NN=0:VD=8:GOSUB25:GOTO8060
9700 PRINT:INPUT"COMMAND";A$:IFA$=""ORLEN(A$)>2THEN8080
9710 INPUT"OPERAND";B$
9720 PRINT#8,A$+":"+B$:INPUT#8,E,E$,E1,E2:PRINTE,E$,E1,E2:GOTO8080
