!--------------------------------------------------
!- Friday, May 19, 2017 8:45:57 PM
!- Import of : 
!- c:\src\zelch64\b1.prg
!- Commodore 64
!--------------------------------------------------
5 IFPEEK(767)=1THENPOKE767,0:RETURN
10 GOTO120
15 PRINTPEEK(58):GOTO15
17 GETA$:PRINTA$:GOTO17
20 OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
30 I=1
40 PRINT#1,"p"CHR$(98)CHR$(I)CHR$(0)CHR$(5)
50 INPUT#1,E1:IFE1<>50THENI=I+1:GOTO40
60 CLOSE2:CLOSE1:PRINTI:END
70 GOTO4780:REMSIG PROMPT
80 GOTO3140:REMMAIN MENU!
90 OPEN1,8,15:INPUT#1,E1$,E2$,E3$,E4$:PRINTE1$,E2$,E3$,E4$:CLOSE1
100 CLOSE2:CLOSE8:CLOSE7:END
110 PRINTPEEK(56577),:GOTO110
120 PRINTCHR$(14)
130 IFPEEK(700)<>16THENPOKE700,16:LOAD"{pound}cursor",8,1
140 IFPEEK(701)<>16THENPOKE701,16:LOAD"{pound}p.protocol",8,1
150 POKE53280,0:POKE53281,0:PRINT"{yellow}{clear}":GOTO1040
160 PRINT"{clear}{down*3}           press [f1] to exit"
170 PRINT"{down*2}            waiting for call"
180 POKE56577,00:POKE56579,38
190 GETA$:IFA$="{f1}"THEN 1050
200 IFPEEK(56577)AND8THEN180
210 PRINT"{down*2}             ring detected!
220 POKE56577,32
230 IFPEEK(56577)=233THENGOTO 1200
240 GETA$:IFA$="{f1}"THENGOTO1050
250 C=C+1:IFC<20THENFORI=0TO1000:NEXT:GOTO230
260 PRINT"{up}no carrier detected.":FORI=0TO2000:NEXT
270 POKE56577,0:POKE56579,32:FORI=0TO2000:NEXT:GOTO160
280 POKE56577,0:POKE56579,32
290 PRINT"{clear}{down*5}   enter number to dial :";:INPUT DI$
300 IFDI$=""THEN1050
310 POKE56577,32
320 PRINT"{clear}{down*5}pausing for dial tone:":FORI=0TO3000:NEXT
330 PRINTSPC(12)"{up}dialing:           {left*11}";DI$
340 PRINT"{up}            dialing:";:DI=LEN(DI$)
350 FORI1=1TODI:NU$=MID$(DI$,I1,1):IFNU$="-"THENGOTO390
360 NU=VAL(NU$)
370 PRINT"{reverse on}"NU$"{reverse off}";:IFNU$="0"THENNU=10
380 GOSUB480
390 GETA$:IFA$="{f1}"THENGOTO1050
400 NEXT I1
410 PRINT:PRINTSPC(8)"{down*5}checking for carrier....":C=8
420 POKE56579,6:POKE56577,2:FORI=0TO1000:NEXT
430 IFPEEK(56577)=235THEN1200
440 GETA$:IFA$="{f1}"THENGOTO1050
450 C=C+1:IFC<20THENFORI=0TO1000:NEXT:GOTO430
460 PRINT"{up}busy or no carrier.":FORI=0TO1000:NEXT
470 POKE56577,0:POKE56579,32:FORI=0TO1000:NEXT:GOTO310
480 FORI2=1TONU
490 POKE56577,00:FORI=1TO25:NEXTI
500 POKE56577,32:FORI=1TO15:NEXTI
510 NEXTI2
520 FORI=1TO300:NEXT:RETURN
530 CLOSE5:OPEN5,2,3,CHR$(6)+CHR$(0):DIMRV$(255),SE$(255):BA=300:DP=0:GX=1
540 DIMTT$(100)
550 BS=255:FORJ=0TO127:SE$(J)=CHR$(J):NEXT J:V=53248:POKEV+21,1:POKE2040,13
560 SE$(20)=CHR$(8):FORJ=65TO90:SE$(J)=CHR$(J+32):NEXT
570 FORJ=97TO122:SE$(J)=CHR$(J-32):NEXT
580 FORJ=91TO95:SE$(J)=CHR$(J):NEXT
590 FORJ=128TO255:SE$(J)=CHR$(J-128):NEXT
600 SE$(133)=CHR$(12):SE$(147)=CHR$(19):SE$(137)=CHR$(3)
610 SE$(134)=CHR$(17):SE$(138)=CHR$(19)
620 FORJ=0TO127:RV$(J)=CHR$(J):NEXT
630 FORJ=0TO31:RV$(J)=CHR$(32):NEXT
640 RV$(8)=CHR$(20):RV$(13)=CHR$(13)
650 FORJ=65TO90:RV$(J)=CHR$(J+32):NEXT
660 FORJ=97TO122:RV$(J)=CHR$(J-32):NEXT
670 FORJ=128TO255:RV$(J)=SE$(J-128):NEXT
680 FORJ=0TO3:CA(J)=3:NEXT:FORJ=4TO7:CA(J)=J:NEXT:CA(8)=500:CA(9)=999
690 IFPEEK(702)<>16THENGOTO2140
700 POKE702,0:MN=PEEK(703):DA=PEEK(704):YE=PEEK(705):IFPEEK(709)=1THENO=1
705 GOTO 2280
710 POKE56579,32:POKE56577,0:RETURN
720 POKE56579,6:POKE56577,2:RETURN
730 REM MODEM GET ROUTINE
740 IFTE=0ANDPEEK(56577)>239THENPRINT#7,"carrier dropped":GOTO4340
750 IFVAL(TI$)>TMTHENPRINT#7,"time up!":TX$="Time Up":GOSUB2040:GOTO4340
760 IFGX=1THEN800
770 GET#5,IN$
780 IFIN$=""THENRETURN
790 IN$=RV$(ASC(IN$)):RETURN
800 GET#5,IN$
810 RETURN
820 REM MODEM SEND ROUTINE
830 IFGX=1THEN900
840 PRINTOU$;:OU$=SE$(ASC(OU$))+CHR$(0)
850 PRINT#5,OU$;
860 RETURN
870 IFVAL(TI$)>TMTHENTM=1:TX$="Your time is UP!":GOSUB2040
880 RETURN
890 TX$="Call back sometime after midnight please":GOSUB2040
895 FORI=0TO6000:NEXT:GOTO4340
900 PRINTOU$;:PRINT#5,OU$;
910 RETURN
920 TX$="baud is"+STR$(BA):GOSUB2040
930 TX$="new baud or {reverse on}return{reverse off}>":GOSUB2040:GOSUB1890:BA$=I$
940 IFBA$=""ORVAL(BA$)<200ORVAL(BA$)>370THENRETURN
950 TX$="Set your BAUD and hit RETURN":GOSUB2040
960 BA=VAL(BA$)
970 B1=INT(1022730/BA/2-100):B2=INT(1022730/BA)
980 B3=(B1/256):B4=B3*256:B5=B1-B4:POKE661,B5:POKE662,B3
990 B3=(B2/256):B4=B3*256:B5=B2-B4:POKE665,B5:POKE666,B3
1000 IFTE=2THENRETURN
1010 GOSUB730:IFIN$<>CHR$(13)THENGETIN$
1020 IFIN$<>CHR$(13)THEN1010
1030 RETURN
1040 BA=300:TE=2:GOSUB970:PRINT"setting up":GOTO530
1050 PRINT"{clear}    menu  "
1060 PRINT"1)autodial"
1070 PRINT"2)answer"
1080 PRINT"3)hang-up"
1090 PRINT"4)send carrier"
1100 PRINT"5)terminal"
1110 PRINT"6)bbs"
1120 GETA$:I9=PEEK(56577)
1130 IFA$="2"THEN160
1140 IFA$="1"THEN280
1150 IFA$="3"THENGOSUB710
1160 IFA$="4"THENGOSUB720
1170 IFA$="5"ANDI9<240THENTE=1:GX=1:GOTO1210
1180 IFA$="6"THENGOTO2360
1190 GOTO1120
1200 IFTE=0THEN1410
1210 GOSUB720
1220 GETA$:GET#5,IN$
1230 IFA$="{f5}"THENGOTO1050
1240 IFA$="{f2}"ANDGX=1THENGX=0:PRINT"{light blue}ASCII ON!":GOTO1260
1250 IFA$="{f2}"ANDGX=0THENGX=1:PRINT"{blue}G{light blue}R{cyan}A{white}P{light green}H{green}I{white}C{red}S"
1260 IFA$="{f4}"THENGOSUB5800
1270 IFA$="{f8}"ANDDP=0THENDP=1:PRINT"{white}auto-echo {reverse on}{pink}on{reverse off}":GOTO1220
1280 IFA$="{f8}"ANDDP=1THENDP=2:PRINT"{white}auto-echo {reverse on}{pink}off{reverse off}":GOTO1220
1290 IFA$="{f1}"THENGOTO5730
1300 POKE212,0
1310 IFGX=1ANDIN$<>""THENPRINTIN$;
1320 IFGX=0ANDIN$<>""THENPRINTRV$(ASC(IN$));
1330 IFGX=0ANDA$<>""THENPRINT#5,SE$(ASC(A$));
1340 IFGX=1ANDA$<>""THENPRINT#5,A$;
1350 GOSUB 1360:GOTO1220
1360 POKEV+39,PEEK(646):Y=PEEK(214):Y=50+(Y*8):X=PEEK(211):IFX>39THENX=X-40
1370 IFX<31THENX=8+X*8:POKE53264,0:GOTO1390
1380 IFX>30THENX=X-31:X=X*8:POKE53264,1
1390 POKEV,X:POKEV+1,Y
1400 RETURN
1410 POKE512,0:TX$="{yellow}{reverse on}U/D "+STR$(UD)+": ":GOSUB2040
1420 GOSUB730:IFIN$=""THENGETIN$
1430 DV=DV(UD+1)
1440 IFIN$="?"THENFI$="{pound}udmenu":GOSUB4170:GOTO1410
1450 IFIN$="u"THENGOSUB1730
1460 IFIN$="b"THEN1830
1470 IFIN$="q"THEN3140
1480 IFIN$="l"THENGOSUB4010:GOTO1410
1490 IFIN$="c"THENGOSUB3960:GOTO1410
1500 IFIN$="d"THENGOSUB1530
1510 IFIN$="$"THENGOSUB4060:GOTO1410
1520 GOTO1420
1530 TX$="Download!":GOSUB2040
1540 IFER=1THENRETURN
1550 TX$="Filename:":GOSUB2040:GOSUB1890:FI$=I$:IFFI$=""ORLEN(FI$)>16THENRETURN
1560 IFLEFT$(FI$,1)="{pound}"THENRETURN
1570 FI$=STR$(UD)+FI$
1580 GOSUB1640:OPEN2,DV,2,"0:"+FI$+","+B$+",r":GOSUB1710
1590 IFE1>0THENER=1:CLOSE2:GOTO1540
1600 IFTE=0THENTX$="Goto RECEIVE mode!":GOSUB2040:PRINTNA$:PRINT#7,"download"
1610 SYS49173:POKE51227,TY:SYS49164:IFPEEK(512)=1THENGOSUB1860:RETURN
1620 GOSUB1720
1630 SYS49173:POKE51224,BS:SYS49158:GOSUB1860:GOSUB4120:RETURN
1640 TX$="P)rogram or S)equential":GOSUB2040
1650 GETB$:IFB$<>""THEN1670
1660 GOSUB730:B$=IN$
1670 IFB$="s"THENTY=2:RETURN
1680 IFB$="p"THENTY=1:RETURN
1690 GOTO1650
1700 RETURN
1710 OPEN15,8,15:INPUT#15,E1:RETURN
1720 PRINT:TX$="123456789*123456789*123456789*123456789*":GOSUB2040:RETURN
1730 TY$="ps":TX$="Upload!":GOSUB2040
1740 TX$="Filename:":GOSUB2040:GOSUB1890:FI$=I$:IFFI$=""ORLEN(FI$)>16THENRETURN
1750 I$=LEFT$(FI$,1):IFI$="{pound}"THENRETURN
1760 FI$=STR$(UD)+FI$
1770 SYS49173:SYS49161:IFPEEK(512)=1THENGOSUB1860:RETURN
1780 T$=","+MID$(TY$,PEEK(51227),1)+",w":GOSUB1710
1790 OPEN2,DV,2,"0:"+FI$+T$:FORI=1TO1300:NEXTI
1800 TX$="Goto SEND mode!":GOSUB2040:PRINTNA$:PRINT#7,"upload":SYS61310
1810 GOSUB1720:SYS49173:SYS49155
1820 FORI=1TO1000:NEXTI:POKE668,PEEK(667):GOSUB1860:GOSUB4120:RETURN
1830 TX$="block size="+STR$(BS):GOSUB2040
1840 TX$="new size:":GOSUB1890:BS$=I$
1850 BS=VAL(BS$):GOTO1200
1860 CLOSE15:CLOSE2
1870 S=54279:POKES+1,120:POKES+5,0:POKES+6,250:POKES+15,30:POKES+4,17
1880 FORI=1TO10:NEXT:POKES+4,16:RETURN
1890 REM HI
1900 IP=1:I$="":IFNA$=""THENQ1=VAL(TI$)+120:IFQ1>235959THENQ1=230000
1910 IFVAL(TI$)>Q1ANDNA$=""THEN4340
1920 A$="":IU$="":GOSUB730:A$=IN$:IFA$=""THENGETIU$:IFIU$=""THEN1920
1930 IFIU$="{f1}"THENGOSUB6210:GOTO1920
1940 IFIU$="{f3}"THENGOSUB6300:GOTO1920
1950 IFIU$="{f5}"THENGOSUB6340:GOTO1920
1960 IFIU$<>""THENA$=IU$
1970 IFA$=CHR$(13)THEN2030
1980 IFA$=CHR$(20)THEN2010
1990 IF(ASC(A$)AND127)<32THEN1920
1995 IFLEN(I$)>199THENRETURN
2000 OU$=A$:GOSUB820:GOSUB1360:I$=I$+A$:GOTO1910
2010 IFLEN(I$)=0THEN1920
2020 OU$=CHR$(20):GOSUB820:I$=LEFT$(I$,LEN(I$)-1):GOSUB1360:GOTO1910
2030 PRINT:PRINT#5:IP=0:GOSUB1360:RETURN
2040 TX$=CHR$(14)+TX$:IFTE=0THEN2070
2050 IFRIGHT$(TX$,1)=":"THENPRINTTX$;:RETURN
2060 PRINTTX$:RETURN
2070 PRINTTX$:IFGX=0THEN2100
2080 IFRIGHT$(TX$,1)=":"THENPRINT#5,TX$;:RETURN
2090 PRINT#5,TX$+CHR$(10):RETURN
2100 FORI=1TOLEN(TX$)
2110 PRINT#5,SE$(ASC(MID$(TX$,I,1)));:NEXT
2120 IFRIGHT$(TX$,1)<>":"THENPRINT#5,CHR$(10):RETURN
2130 RETURN
2140 INPUT"enter the month";MN
2150 INPUT"enter the day";DA
2160 INPUT"enter the year";YE
2170 PRINT"correct?"
2180 GETA$:IFA$="n"THEN2140
2190 IFA$<>"y"THEN2180
2200 INPUT"Enter the time (hhmmss)";TT$:IFTT$=""THEN2220
2210 TI$=TT$
2220 PRINT"correct?"
2230 GETA$:IFA$="n"THEN2200
2240 IFA$<>"y"THEN2230
2242 PRINT "printer {reverse on}on{reverse off}?":O=0
2245 GETA$:IFA$="y"THENO=1:POKE709,1:GOTO2250
2247 IFA$<>"n"THEN2245
2250 PRINT"enter your data disk in drive 8"
2260 PRINT"and press {reverse on}return{reverse off}"
2270 GETA$:IFA$<>CHR$(13)THEN2270
2280 OPEN1,8,15,"i":CLOSE1:GOSUB2290:GOTO2360
2290 OPEN8,8,8,"{pound}preps,s,r"
2300 FORI=0TO9:INPUT#8,UD$(I):INPUT#8,UD(I):INPUT#8,DV(I):NEXT:IFSTTHEN2350
2310 INPUT#8,BN$:INPUT#8,SN$:FORI=0TO9:INPUT#8,SG$(I):INPUT#8,SG(I):NEXT
2320 INPUT#8,MD$:MD=VAL(MD$):FORI=0TO9:INPUT#8,ML(I):INPUT#8,MD(I):NEXTI
2330 CLOSE8
2340 OPEN8,8,8,"{pound}last caller,s,r":INPUT#8,LC$:INPUT#8,LG$
2350 CLOSE8:RETURN
2360 PRINT"{clear}{down*12}";BN$:TE=0:C1=0:GX=0
2370 PRINT"bbs mode. {reverse on}f1{reverse off} for menu"
2380 PRINT"{reverse on}f3{reverse off} to log-on"
2390 PRINT"last caller=";LC$
2400 PRINT"logged off at:";LG$
2410 POKE56577,0:POKE56579,38
2420 PRINT"{home}{down}{right*2}time=";TI$:TE=0
2422 IFVAL(TI$)>230000ANDPEEK(712)<>15THENPOKE712,15:GOTO20000
2424 IFPEEK(712)=15ANDVAL(TI$)<130000THENPOKE712,0
2430 A$="":GETA$:IFA$="{f1}"THEN2500
2440 IFA$="{f3}"THENTE=1:GOTO2550
2450 IFPEEK(56577)AND8THEN2410
2460 CT=0:PRINT"ring????":POKE56577,32
2470 IFPEEK(56577)=233THEN2550
2480 CT=CT+1:IFCT<20THENFORI=0TO1000:NEXT:GOTO2470
2490 PRINT"no ring.":FORI=0TO2000:NEXT:GOTO2360
2500 PRINT"{clear}menu"
2510 PRINT"1)terminal
2520 GETA$:IFA$=""THEN2520
2530 IFA$="1"THEN1050
2540 GOTO2520
2550 CLOSE1:CLOSE2:CLOSE7:CLOSE8:IFO=0THENOPEN7,3:GOTO2560
2555 OPEN7,4,7
2560 GX=1:TX$="{clear}connection!!!!!":GOSUB2040
2590 TX$="{down*5}{reverse on}{yellow}press any lowercase letter:":GOSUB2040
2600 TM=VAL(TI$)+120
2610 GET#5,IN$:IFIN$=""THENGETIN$:IFIN$=""THEN2610
2620 IFASC(IN$)>127THENGX=0:TX$="ascii on":GOSUB2040:GOTO2640
2630 IFASC(IN$)<=127THENGX=1:TX$="{reverse on}{blue}GRA{light blue}PH{cyan}ICS":GOSUB2040:GOTO2640
2640 FI$="{pound}opening msg":GOSUB4170
2650 CLOSE2:CLOSE1:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":CT=0
2660 CT=CT+1:IFCT=6THENTX$="Goodbye mr. innocent!":GOSUB2040:GOTO4340
2670 TX$="Enter your ID# :":GOSUB2040:GOSUB1890
2680 NA=VAL(I$):IFI$="new"THEN5840
2690 IFNA=0THEN2750
2700 NC=INT(NA/256):I3=BC*256:NB=NA-I3:POKE710,NB:POKE711,NC:C1=NB:C2=NC
2710 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(90):INPUT#1,E1
2715 IFE1<>0THEN2750
2720 INPUT#2,PS$:TX$="Enter your password:":GOSUB2040:GOSUB1890
2730 IFI$=PS$THENTX$="{clear}Loading info":GOSUB2040:GOSUB6600:GOTO2770
2740 TX$="Invalid PASSWORD":GOSUB2040:GOTO2660
2750 TX$="Invalid ID":GOSUB2040:TX$="New Users type NEW at the ID prompt"
2760 GOSUB2040:GOTO2660
2770 PRINT#7,NA$+" on at "+TI$:TX$="Welcome Back "+NA$+"!":GOSUB2040:GOTO2790
2780 GOSUB6600
2790 AC=VAL(AC$):CA=VAL(CA$):IFCA>CA(AC)THENTX$="call tomorrow"
2850 IFCA>CA(AC)THENGOSUB2040:GOTO4340
2860 C4$=STR$(CA(AC)-CA):TX$="You may make "+C4$+" more calls today.":GOSUB2040
2870 CA$=STR$(CA):CLOSE2:CLOSE1:TM=VAL(TI$)+((AC+1)*1000)
2875 IFTM>235959ANDAC<>9THEN890
2880 TX$="Checking for mail!":GOSUB2040:I1$="{pound}1mail"+STR$(NA):I3=0
2890 OPEN8,MD,8,I1$+",s,r":GET#8,A$
2900 IFST<>0ANDI3=0THENTX$="NO mail.. (SORRY)":GOSUB2040:CLOSE8:GOTO3135
2910 IFST<>0THENTX$="NO MORE mail..":GOSUB2040:CLOSE8:GOTO3135
2920 I3=1:CLOSE8:OPEN8,MD,8,I1$+",s,r"
2930 GET#8,OU$:IFOU$<>"#"THENGOSUB820:GOTO2930
2940 GOSUB820:GET#8,O1$:OU$=O1$:GOSUB820:GET#8,O2$:OU$=O2$:GOSUB820:GET#8,O3$
2950 OU$=O3$:GOSUB820:ID=VAL(O1$):ID$=STR$(ID)
2960 GET#8,OU$:IFST=64THEN3010
2970 GOSUB820
2980 GOSUB730:IFIN$=" "THEN3010
2990 GETA$:IFA$=" "THEN3010
3000 GOTO2960
3010 TX$="1)Reply 2)Delete 3)Reread":GOSUB2040
3020 GOSUB730:IFIN$=""THENGETIN$
3030 IFIN$="3"THEN2920
3040 IFIN$="2"THENGOTO3070
3050 IFIN$<>"1"THEN3020
3060 I7=ID:GOSUB4530:GOTO3010
3070 CLOSE1:CLOSE8:OPEN1,8,15,"s0:"+I1$:FORI=2TO9:I1$="mail"+STR$(NA)
3080 I$=RIGHT$(STR$(I),1)
3090 I2$=I1$:I6$=RIGHT$(STR$(I-1),1):I7$="{pound}"+I6$+RIGHT$(I1$,LEN(I1$))
3100 I2$="{pound}"+I$+RIGHT$(I2$,LEN(I2$))
3110 PRINT#1,"r0:"+I7$+"="+I2$:NEXTI:CLOSE1:TX$="That letter deleted."
3120 GOSUB2040:I1$="{pound}1mail"+STR$(NA):CLOSE1:CLOSE2:CLOSE8:GOTO2890
3130 END
3135 CLOSE1:OPEN1,8,15:INPUT#1,E1,E1$,E2,E3:CLOSE1
3140 OU$=CHR$(14):GOSUB820:PRINTNA$:T$=STR$(TM-VAL(TI$))
3145 TX$="{white}Time left:"+T$+":{blue}C{light blue}O{cyan}M{white}M{yellow}A{pink}N{red}D{white}:":GOSUB2040
3150 FL(0)=0:GOSUB730:IFIN$=""THENFL(0)=1:GETIN$:IFIN$=""THEN3150
3155 TX$=IN$:GOSUB2040
3160 IFIN$="t"ANDUD(0)>0ANDAC>3THENUD=1:GOTO1410
3170 IFIN$="t"ANDUD(0)=0THENTX$="Sorry, the UD is down":GOSUB2040:GOTO3140
3180 IFIN$="b"THENGOSUB920:GOTO3140
3190 IFIN$="!"ANDAC>7THENGOSUB3930:GOTO3140
3200 IFIN$="?"THENFI$="{pound}mainmenu"+STR$(AC):GOSUB4170:GOTO3140
3210 IFIN$="f"THENGOSUB4430:GOTO3140
3220 IFIN$="e"ANDAC>2THENGOSUB4520:GOTO3140
3230 IFIN$="m"THENSG=0:GOTO4750
3240 IFIN$="o"THENGOSUB6160:GOTO3140
3250 IFIN$="l"ANDAC>7THENFI$="{pound}log!":GOSUB4170:GOTO3140
3260 IFIN$="d"ANDAC>7THENGOSUB6370:GOTO3140
3270 IFIN$="i"THENFI$="{pound}info":GOSUB4170:GOTO3140
3280 IFIN$="c"THENGOSUB5620:GOTO3140
3285 IFIN$="{f1}"ANDFL(0)=1THENGOSUB6210:GOTO3140
3286 IFIN$="{f3}"ANDFL(0)=1THENGOSUB6300:GOTO3140
3287 IFIN$="{f5}"ANDFL(0)=1THENGOSUB6340:GOTO3140
3290 IFIN$="u"ANDAC>2THENGOSUB7000:GOTO3140
3300 IFIN$="q"ANDAC>7THENGOSUB8000:GOTO3140
3310 IFIN$="$"ANDAC>6THENGOSUB6460:GOTO3140
3315 IFIN$="s"THENGOSUB13100:GOTO3140
3320 GOTO3140
3330 POKE767,1:TX$="Standby...":GOSUB2040
3340 LOAD"{pound}m1",8,1
3960 TX$="Change to UD number:":GOSUB2040
3970 GOSUB730:IFIN$=""THENGETIN$
3980 IN=VAL(IN$):IFIN=0THEN3970
3990 IFUD(IN-1)>ACTHENRETURN
4000 UD=IN:TX$="Now in UD number:"+STR$(IN+1):GOSUB2040:RETURN
4010 I=1
4020 IFUD(I-1)>ACTHENI=I+1:GOTO4040
4030 TX$=STR$(I)+") "+UD$(I-1):I=I+1:GOSUB2040
4040 IFI<11THEN4020
4050 RETURN
4060 CLOSE1:OPEN1,8,0,"$:"+STR$(UD)+"*":GET#1,A$:GET#1,A$
4070 GET#1,I$:GET#1,I$:IFST=64THENCLOSE1:RETURN
4080 GET#1,I1$:GET#1,I2$:I1=ASC(I1$+CHR$(0))+256*ASC(I2$+CHR$(0))
4090 TX$=STR$(I1)
4100 GET#1,I1$:IFI1$=""THENGOSUB2040:GOTO4070
4110 TX$=TX$+I1$:GOTO4100
4120 CLOSE1:OPEN1,8,0,"$0:z=u":FORI=1TO35:GET#1,I1$:NEXT
4130 GET#1,I2$:CLOSE1:OPEN1,8,15
4140 I1=ASC(I1$+CHR$(0))+256*ASC(I2$+CHR$(0))
4150 IFI1<50THENOPEN1,8,15,"s0:"+FI$
4160 CLOSE1:CLOSE2:CLOSE8:RETURN
4170 OU$="{clear}":GOSUB820:D5=0:IFFG=1THENFG=0:DV=DV(0):GOTO4200
4180 IFGF=1THENDV=MD(SG):GOTO4200
4190 DV=8
4200 CLOSE1:CLOSE8:OPEN1,8,15:OPEN8,DV,8,FI$+",s,r"
4210 GET#8,OU$:IFSTTHENGOTO4240
4220 GOSUB730:GETYI$:IFIN$=" "ORYI$=" "THEN4240
4230 GOSUB820:D5=1:GOTO4210
4240 PRINT#5:PRINT:IFGF=1THENGF=0:GOSUB4260
4250 CLOSE1:CLOSE8:RETURN
4260 IFD5=0THENRETURN
4270 TX$="Public Reply?:":GOSUB2040:GOSUB1890:IFI$="n"THENRETURN
4280 IFI$<>"y"THEN4270
4290 CLOSE8:CLOSE1:II$=FI$:FI$="make1":
4300 QQ$=CHR$(13)+CHR$(13)+"***************************************"+CHR$(13)
4310 QD$="{reverse on}{white}Reply By:"+NA$+" on:"+STR$(MN)+"/"+STR$(DA)+" at:"+TI$
4320 FG=1:GOSUB3330:IFEW<>1THENCLOSE8:FI$=II$:GOSUB5470:RETURN
4330 CLOSE8:OPEN1,8,15,"s0:make1":CLOSE1:RETURN
4340 POKE702,16:POKE703,MN:POKE704,DA:IFNA$=""THENPRINT#7,"before logon"
4350 POKE705,VAL(RIGHT$(STR$(YE),2))
4360 IFNA$=""THENCLOSE1:CLOSE2:RUN
4370 FI$="{pound}closing msg":GOSUB4170
4380 PRINT#7,"off at:"+TI$:TM$=TI$:LC$=STR$(MN)+"/"+STR$(DA):CA$=STR$(CA+1):
4390 C1=NB:C2=NC:GOSUB6100:TX$="{red}{reverse on}Disconnected!":GOSUB2040
4400 POKE56579,32:POKE56577,0:CLOSE1:CLOSE2:OPEN1,8,15,"s0:{pound}last caller":CLOSE1
4410 OPEN8,8,8,"{pound}last caller,s,w":PRINT#8,NA$:LC$=LC$+" "+TI$:PRINT#8,LC$
4420 CLOSE8:POKE702,16:RUN
4430 TX$="Send feedback to the sysop?":GOSUB2040
4440 GOSUB730:IFIN$=""THENGETIN$
4450 IFIN$="n"THENRETURN
4460 IFIN$<>"y"THEN4440
4470 CLOSE8:CLOSE1:CLOSE2
4480 FI$="make1":QQ$="from:"+NA$+" id#"+STR$(NA)
4490 QD$="on:"+STR$(MN)+"/"+STR$(DA)+"  at:"+TI$
4500 EW=0:GOSUB3330:IFEW=1THEN EW=0:CLOSE8:OPEN1,8,15,"s0:make1":CLOSE1:RETURN
4510 FI$="{pound}feedback":CLOSE8:CLOSE1:GOSUB5470:CLOSE8:CLOSE1:RETURN
4520 TX$="User ID or Name:":GOSUB2040:GOSUB1890:I7=VAL(I$):IFI7=0THEN4640
4530 I1=VAL(I$):I2=I1/256:I3=I2*256:I4=I1-I3:CLOSE1:GOSUB4740
4540 PRINT#1,"p"CHR$(98)CHR$(I4)CHR$(I2)CHR$(35):INPUT#1,E1,E1$,E2
4550 I=1:IFE1=50THENTX$="Not Found":GOSUB2040:RETURN
4560 ID$=STR$(I7):I$=STR$(I):I$="{pound}"+RIGHT$(I$,1)
4570 FI$=I$+"mail"+STR$(NA)
4580 CLOSE8:OPEN8,8,8,FI$+",s,r":INPUT#1,E1,E1$,E2
4590 GET#8,A$:IFE1=63ORE1=0THEN4620
4600 IFE1=62THEN4690
4610 TX$=E1$:GOSUB2040:RETURN
4620 CLOSE8:I=I+1:IFI=10THENTX$="He/She can't hold more mail":GOSUB2040
4625 IFI=10THENCLOSE1:CLOSE2:RETURN
4630 GOTO4560
4640 TX$="searching...":GOSUB2040:I1=1:I2=0:GOSUB4740
4650 PRINT#1,"p"CHR$(98)CHR$(I1)CHR$(I2)CHR$(5):IFST=50THEN4680
4660 INPUT#2,A$:IFA$=I$THENI4=I1:TX$="Found":I7=I1+256*I2:GOSUB2040:GOTO4540
4670 I1=I1+1:IFI1=256THENI1=0:I2=I2+1:GOTO4650
4680 TX$="Not found":GOSUB2040:CLOSE1:CLOSE2:CLOSE8:RETURN
4690 CLOSE8:NA=PEEK(710)+256*PEEK(711)
4700 QQ$="From:"+NA$+" id#"+STR$(NA)
4710 QD$="on:"+STR$(MN)+"/"+STR$(DA)+" at:"+TI$
4720 GOSUB3330:IFEW<>1THENCLOSE8:RETURN
4730 :CLOSE8:OPEN1,8,15,"s0:"+FI$:CLOSE1:RETURN
4740 CLOSE8:CLOSE1:CLOSE2:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":RETURN
4750 IFSG(SG)>ACANDSG=0THEN3140
4760 IFSG(SG)>ACANDSG<>0THENSG=0:GOTO4780
4770 TX$="{clear}":GOSUB2040:FG=1:FI$="{pound}sig"+STR$(SG):GOSUB4170:GOSUB5560
4780 PRINTNA$+" is online":CLOSE1:CLOSE2:CLOSE8:TX$="{reverse on}{light blue}SIG "+STR$(SG+1)
4790 GOSUB2040
4800 IN$="":GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN4800
4810 IFIN$="c"THENGOSUB5550:GOTO4780
4820 IFIN$="l"THENGOSUB5500:GOTO4780
4830 IFIN$="q"THENGOSUB5430:GOTO3140
4840 IFIN$="a"THENGOTO5360
4850 IFIN$="p"THENGOTO5180
4860 IFIN$="r"THENGOTO4890
4870 IFIN$="?"THENFI$="{pound}sigmenu":GOSUB4170:GOTO4780
4880 GOTO4800
4890 ER=0:TX$="F)orward R)everse S)ingle Q)uit":GOSUB2040
4900 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN4900
4910 IFIN$="f"THEN5060
4920 IFIN$="r"THEN5120
4930 IFIN$="q"THEN4780
4940 IFIN$<>"s"THEN4900
4950 TX$="Which one:":GOSUB2040:GOSUB1890:I=VAL(I$)-1:GOSUB4970:IFER=1THEN4780
4960 GF=1:GOSUB4170:GOTO4780
4970 CT=0:ER=0
4980 OPEN1,8,15:OPEN8,MD(SG),8,"{pound}msg"+STR$(SG)+STR$(CT):INPUT#8,A$:INPUT#1,E1
4990 IFE1>0THENCLOSE1:CLOSE8:ER=1:RETURN
5000 A=VAL(RIGHT$(A$,LEN(A$)-9))
5010 IFA=ITHENFI$="{pound}msg"+STR$(SG)+STR$(CT):CLOSE8:CLOSE1:RETURN
5020 I1=I-A:CT=CT+I1:FI$="{pound}msg"+STR$(SG)+STR$(CT):CLOSE1:CLOSE8:RETURN
5030 CLOSE8:OPEN8,8,8,FI$+",s,r":INPUT#8,A$:CLOSE8
5040 IFST AND 64THENCT=I:RETURN
5050 A=VAL(RIGHT$(A$,LEN(A$)-13)):CT=A+1:RETURN
5060 TX$="Start at:":GOSUB2040:GOSUB1890:S1=VAL(I$):S1=S1-1
5070 TX$="End at:":GOSUB2040:GOSUB1890:S2=VAL(I$):S2=S2-1
5075 IFS1<0ORS1>ML(SG)ORS2<0THEN4780
5080 I=S1:GOSUB4970:IFER=1THEN4780
5090 GF=1:GOSUB4170
5100 S1=S1+1:IFS1>S2THEN4780
5110 GOTO5080
5120 TX$="Start at:":GOSUB2040:GOSUB1890:S1=VAL(I$):S1=S1-1:IFS1<0THEN4780
5130 TX$="End at:":GOSUB2040:GOSUB1890:S2=VAL(I$):S2=S2-1:IFS2<0THEN4780
5140 I=S1:GOSUB4970:IFER=1THEN4780
5150 GF=1:GOSUB4170
5160 S1=S1+1:IFS1>S2THEN4780
5170 GOTO5140
5180 TX$="Subject:":GOSUB2040:GOSUB1890:SB$=I$:IFI$=""THEN4780
5190 I=ML(SG)
5200 OPEN1,8,15:OPEN8,MD(SG),8,"{pound}msg"+STR$(SG)+STR$(I)+",s,r":INPUT#8,A$
5210 INPUT#1,E1:CLOSE1:CLOSE8:IFE1=62THENI=I-1:IFI<0THENI=0:CT=1:GOTO5290
5220 IFE1<>62ANDI<ML(SG)THENI=I+1:GOSUB5520:CLOSE1:GOTO5290
5230 IFE1<>62THEN5250
5240 GOTO5200
5250 OPEN1,8,15,"s0:{pound}msg"+STR$(SG)+STR$(0)
5260 FORJ=1TOML(SG):R$="{pound}msg"+STR$(SG)+STR$(J-1)+"={pound}msg"+STR$(SG)+STR$(J)
5270 PRINT#1,"r0:"+R$:NEXT:PRINT#1,"s0:{pound}msg"+STR$(SG)+STR$(ML(SG)):CLOSE1
5280 I=ML(SG):FI$="{pound}msg"+STR$(SG)+STR$(ML(SG)):GOSUB5520:GOTO5300
5290 FI$="{pound}msg"+STR$(SG)+STR$(I)
5300 B$="{white}{reverse on}Posted by:{reverse off}{yellow}"+NA$+"{reverse on}{white} on {light green}{reverse on}"+STR$(MN)+" /"+STR$(DA)
5310 C$="{white}{reverse on}Subject:{reverse off}{light blue}"+SB$
5320 QQ$="{reverse on}{white}Message{reverse off}{pink} #"+STR$(CT):QD$=B$+CHR$(13)+C$+CHR$(13)
5330 GOSUB5030
5340 GOSUB3330:IFEW=1THENCLOSE8:OPEN1,8,15,"s0:"+FI$:CLOSE1:GOTO4780
5350 CLOSE8:TX$="Thanx for posting!":GOSUB2040:GOTO4780
5360 I=VAL(P$(SG))
5370 GOSUB4970:IFER=1THEN5400
5380 GF=1:GOSUB4170:IFIN$=" "THENP$(SG)=STR$(I):GOTO4780
5390 I=I+1:GOTO5370
5400 P$(SG)=STR$(I):SG=SG+1:IFSG=10THEN4780
5410 IFAC>=SG(SG)THENFI$="{pound}sig"+STR$(SG):FG=1:GOSUB4170:GOTO5360
5420 GOTO5400
5430 CLOSE1:CLOSE2:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO5450
5440 PRINT#1,"p"CHR$(98)CHR$(NB)CHR$(NC)CHR$(I):RETURN
5450 J=0:FORI=100TO145STEP5:GOSUB5440:PRINT#2,P$(J):J=J+1:NEXTI
5460 CLOSE2:CLOSE1:RETURN
5470 CLOSE1:CLOSE8:OPEN1,8,15,"r0:make2="+FI$
5480 PRINT#1,"c0:"+FI$+"=0:make2,make1":PRINT#1,"s0:make1"
5490 PRINT#1,"s0:make2":CLOSE1:RETURN
5500 FORI=0TO9:IFSG(I)<=ACTHENTX$=STR$(I+1)+" - "+SG$(I):GOSUB2040
5510 NEXT:RETURN
5520 OPEN8,MD(SG),8,"{pound}msg"+STR$(SG)+STR$(I-1)+",s,r":INPUT#8,IT$
5530 IFSTTHENCLOSE8:CT=1:RETURN
5540 CT=VAL(RIGHT$(IT$,LEN(IT$)-13)):CT=CT+1:CLOSE8:RETURN
5550 TX$="Change to Sig:":GOSUB2040:GOSUB1890:SG=VAL(I$)-1
5560 IFSG<0ORSG>9THENSG=0:GOTO4780
5565 CT=ML(SG)
5570 OPEN1,8,15:OPEN8,MD(SG),8,"{pound}msg"+STR$(SG)+STR$(CT)+",s,r"
5580 GET#8,A$:INPUT#1,E1:IFE1=0THENCLOSE8:CLOSE1:GOTO5610
5590 CT=CT-1:IFCT<0THENCLOSE8:CLOSE1:GOTO5610
5600 CLOSE1:CLOSE8:GOTO5570
5610 CLOSE1:OPEN1,8,15:INPUT#1,E1,E1$,E2,E3:CLOSE1
5615 TX$="There are "+STR$(CT+1)+" Bulletins here":GOSUB2040:RETURN
5620 TX$="Page the SysOp?":GOSUB2040:
5630 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN5630
5640 IFIN$="n"THENRETURN
5650 IFIN$<>"y"THEN5630
5660 TX$="Making rude noises...":GOSUB2040
5670 FORI=54272TO54295:POKEI,0:NEXT:POKE54296,15
5680 POKE54277,8:POKE54278,255:POKE54276,21:I2=255:
5690 FORI=1TO50:POKE54287,I2
5700 FORI1=255TO0STEP-20:POKE54273,I1:I2=I2-.4
5710 NEXTI1:GETA$:IFA$="{f1}"THENGOSUB6210
5720 NEXTI:POKE54278,15:RETURN
5730 DV=8:PRINT"D)ownload or U)pload"
5740 FI$="":GETA$:IFA$=""THEN5740
5750 IFA$="u"THENINPUT"filename:";FI$:IFFI$<>""THENGOSUB1580:GOTO1210
5760 IFA$="u"THENGOTO1210
5770 IFA$<>"d"THEN5740
5780 INPUT"filename";FI$:IFFI$<>""THENGOSUB1770:GOTO1210
5790 GOTO1210
5800 PRINT"baud is";BA
5810 INPUT"new baud is";BA$:IFBA$=""ORVAL(BA$)<200THENRETURN
5820 TE=2:GOSUB960
5830 RETURN
5840 TX$="finding a opening. standby":GOSUB2040
5850 CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":C1=1:C2=0
5860 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(5)
5870 INPUT#1,E1:IFE1=50THEN5890
5880 OU$=".":GOSUB820:C1=C1+1:IFC1=256THENC1=0:C2=C2+1:GOTO5860
5885 GOTO5860
5890 CLOSE1:CLOSE2:ID=C1+256*C2:IFID>2THENID=ID-1:C1=C1-1
5900 TX$="enter your handle":GOSUB2040:TX$="no more than 17 chars:":GOSUB2040
5910 GOSUB1890:NA$=I$:IFLEN(NA$)>17THEN5890
5920 TX$="enter your phone number in":GOSUB2040:TX$="xxx-xxxx format:":GOSUB2040
5930 GOSUB1890:PH$=I$:IFLEN(PH$)>8THEN5920
5940 TX$="enter your real name":GOSUB2040:TX$="no more than 17 chars":GOSUB2040
5950 GOSUB1890:RN$=I$:IFLEN(RN$)>17THEN5940
5960 TX$="enter a password you would like to use.":GOSUB2040:
5970 TX$="no more than 7 chars":GOSUB2040:GOSUB1890:PS$=I$:IFLEN(PS$)>7THEN5960
5980 AC$="0":LC$=STR$(MN)+"/"+STR$(DA):TM$=TI$:CA$="0"
5990 FORJ=0TO9:P$(J)="0":NEXTJ
6000 TX$="is all this correct?":GOSUB2040
6010 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN6010
6020 IFIN$="n"THEN5890
6030 IFIN$<>"y"THEN6010
6031 PRINT#7,"new user: "+NA$+"  id#"+STR$(ID)
6040 TX$=NA$:GOSUB2040:TX$="your id number is:"+STR$(ID):GOSUB2040:
6050 TX$="and your password is:"+PS$:GOSUB2040:TX$="standby.":GOSUB2040
6055 NA$="    "+NA$
6057 GOSUB6100
6060 FI$="{pound}newuser":GOSUB4170
6070 TX$="hit return to relog-on":GOSUB2040
6080 GOSUB730:IFIN$<>CHR$(13)THENGETIN$:IFIN$<>CHR$(13)THEN6080
6090 GOTO2590
6100 CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO6120
6110 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I):RETURN
6120 I=5:GOSUB6110:PRINT#2,NA$
6125 I=25:GOSUB6110:PRINT#2,PH$
6127 I=35:GOSUB6110:PRINT#2,AC$
6130 I=40:GOSUB6110:PRINT#2,RN$
6135 I=60:GOSUB6110:PRINT#2,LC$
6137 I=75:GOSUB6110:PRINT#2,CA$
6140 I=80:GOSUB6110:PRINT#2,TM$
6145 I=90:GOSUB6110:PRINT#2,PS$
6150 CT=0
6155 FORI=100TO145STEP5
6156 GOSUB6110:PRINT#2,P$(CT)
6157 CT=CT+1:NEXT
6159 CLOSE1:CLOSE2:RETURN
6160 TX$="Exit.. are you SURE??":GOSUB2040
6170 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN6170
6180 IFIN$="n"THENRETURN
6190 IFIN$<>"y"THEN6170
6200 GOSUB4430:GOTO4340
6210 T8$=TI$:TX$="chat mode":GOSUB2040
6220 W$="":IN$="":GOSUB730:IFIN$=""THENGETW$:IFW$=""THEN6220
6231 IFGX=0ANDIN$<>""THENPRINTIN$;:IN$=SE$(ASC(IN$)):PRINT#5,IN$;
6232 IFGX=1ANDIN$<>""THENPRINTIN$;:PRINT#5,IN$;
6235 IFGX=0ANDW$<>""THENPRINTW$;:W$=SE$(ASC(IN$)):PRINT#5,W$;
6240 IFGX=1ANDW$<>""THENPRINT#5,W$;:PRINTW$;
6260 IFW$="{f1}"THENTI$=T8$:RETURN
6290 GOSUB1360:GOTO6220
6300 PRINT"sure you want him thrown off??"
6310 GETA$:IFA$="n"THENRETURN
6320 IFA$<>"y"THEN6310
6330 TX$="you've been thrown off!":GOSUB2040:GOTO4340
6340 PRINT"current access is";AC
6350 INPUT"new access ";AC
6360 RETURN
6370 DV=8
6380 CLOSE1:CLOSE2:OPEN1,DV,15:
6390 TX$=STR$(DV)+":":GOSUB2040:GOSUB1890
6395 IFI$=""THENRETURN
6400 IFVAL(I$)>7ANDVAL(I$)<11THENDV=VAL(I$) :GOTO6380
6410 PRINT#1,I$:INPUT#1,E1,E1$,E2,E3:TX$=STR$(E1)+","+E2$:GOSUB2040:GOTO6390
6420 IFI$=CHR$(13)THENRETURN
6460 CLOSE1:CLOSE2:OPEN1,8,0,"$"
6470 GET#1,A$:GET#1,A$
6480 GET#1,I$:GET#1,I$:IFST=64THENCLOSE1:RETURN
6490 GET#1,I1$:GET#1,I2$:I1=ASC(I1$+CHR$(0))+256*ASC(I2$+CHR$(0))
6500 TX$=STR$(I1)+":":GOSUB2040
6510 GET#1,OU$:IFOU$=""THENPRINT:PRINT#5:GOTO6480
6520 GOSUB730:IFIN$=""THENGETIN$
6530 IFIN$=" "THENCLOSE1:RETURN
6540 GOSUB820:GOTO6510
6600 EQ=0:CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO6620
6610 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I)
6615 INPUT#1,E1:IFE1<>0THENEQ=1:CLOSE1:CLOSE2
6617 RETURN
6620 I=5:GOSUB6610:IFEQ=1THENRETURN
6625 INPUT#2,NA$:I=25:GOSUB6610:INPUT#2,PH$
6627 I=35:GOSUB6610:INPUT#2,AC$
6630 I=40:GOSUB6610:INPUT#2,RN$
6635 I=60:GOSUB6610:INPUT#2,LC$
6637 I=75:GOSUB6610:INPUT#2,CA$
6640 I=80:GOSUB6610:INPUT#2,TM$
6645 I=90:GOSUB6610:INPUT#2,PS$
6650 CT=0
6655 FORI=100TO145STEP5
6656 GOSUB6610:INPUT#2,P$(CT)
6657 CT=CT+1:NEXT
6659 CLOSE1:CLOSE2:RETURN
7000 TX$="Start at:":GOSUB2040:GOSUB1890:S1=VAL(I$):IFS1<=0THENRETURN
7010 TX$="End at:":GOSUB2040:GOSUB1890:S2=VAL(I$):IFS1<=0THENRETURN
7020 CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
7030 IFS1>S2THENCLOSE1:CLOSE2:RETURN
7040 GOSUB730:IFIN$=" "THENRETURN
7050 GETIN$:IFIN$=" "THENRETURN
7060 I1=INT(S1/256):I3=I1*256:I2=S1-I3
7070 PRINT#1,"p"CHR$(98)CHR$(I2)CHR$(I1)CHR$(5):INPUT#1,E1,E1$,E2,E3
7080 IFE1=50THENCLOSE1:CLOSE2:RETURN
7090 INPUT#2,N$:TX$=STR$(S1)+":"+N$:GOSUB2040
7100 S1=S1+1:GOTO7030
8000 TX$="1) edit user log":GOSUB2040
8010 TX$="2) read feedback":GOSUB2040
8020 TX$="3) edit files":GOSUB2040
8030 TX$="4) clear feedback":GOSUB2040
8099 TX$="q) quit":GOSUB2040
8100 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN8100
8110 IFIN$="q"THENRETURN
8120 IFIN$="1"THENGOSUB9000:GOTO8000
8130 IFIN$="2"THENFI$="{pound}feedback":GOSUB4170:GOTO8000
8140 IFIN$="3"THENGOSUB10000:GOTO8000
8150 IFIN$="4"THENGOSUB12000:GOTO8000
8199 GOTO8100
8200 M$(0)=NA$:M$(1)=PH$:M$(2)=AC$:M$(3)=RN$:M$(4)=LC$:M$(5)=CA$:M$(6)=TM$
8210 M$(7)=PS$:FORJ=0TO9:M1$(J)=P$(J):NEXTJ:RETURN
8300 NA$=M$(0):PH$=M$(1):AC$=M$(2):RN$=M$(3):LC$=M$(4):CA$=M$(5):TM$=M$(6)
8310 PS$=M$(7):FORJ=0TO9:P$(J)=M1$(J):NEXTJ:RETURN
9000 TX$="User # to edit:":GOSUB2040:GOSUB1890:
9010 I=VAL(I$):C2=INT(I/256):C3=C2*256:C1=I-C3:GOSUB8200:GOSUB6600
9020 TX$="{clear}name:"+NA$:GOSUB2040:TX$="phone #:"+PH$:GOSUB2040
9030 TX$="access:"+AC$:GOSUB2040:TX$="real name:"+RN$:GOSUB2040
9040 TX$="last call:"+LC$:GOSUB2040:TX$="calls today:"+CA$:GOSUB2040
9050 TX$="last time:"+TM$:GOSUB2040:TX$="password:"+PS$:GOSUB2040
9060 TX$=CHR$(13)+"edit this record?:":GOSUB2040
9070 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN9070
9080 IFIN$="n"THENGOSUB8300:RETURN
9090 IFIN$<>"y"THEN9070
9100 TX$="name:":GOSUB2040:GOSUB1890:IFLEN(I$)>17THEN9100
9105 IFI$=""THEN9110
9107 NA$=I$
9110 TX$="phone:":GOSUB2040:GOSUB1890:IFLEN(I$)>8THEN9110
9115 IFI$=""THEN9120
9117 PH$=I$
9120 TX$="access:":GOSUB2040:GOSUB1890:IFLEN(I$)>8THEN9120
9125 IFI$=""THEN9130
9127 AC$=I$:AC=VAL(AC$)
9130 TX$="real name:":GOSUB2040:GOSUB1890:IFLEN(I$)>17THEN9130
9135 IFI$=""THEN9140
9137 RN$=I$
9140 TX$="calls today:":GOSUB2040:GOSUB1890:IFLEN(I$)>3THEN9140
9145 IFI$=""THEN9150
9147 CA$=I$:CA=VAL(CA$)
9150 TX$="password:":GOSUB2040:GOSUB1890:IFLEN(I$)>7THEN9150
9155 IFI$=""THEN9160
9157 PS$=I$
9160 TX$="s)ave e)dit again q)uit":GOSUB2040
9170 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN9170
9180 IFIN$="q"THENGOSUB8300:RETURN
9182 IFIN$="e"THEN9100
9190 IFIN$="s"THENGOSUB6100:GOSUB8300:RETURN
9200 GOTO9170
10000 TX$="filename:":GOSUB2040:GOSUB1890:II$=I$
10010 TX$="device #":GOSUB2040:GOSUB1890:IFI$=""THENDV=8:GOTO10020
10015 DV=VAL(I$)
10020 OPEN8,DV,8,II$+",s,r":LN=0
10030 GET#8,I$:IFSTTHEN10100
10035 IFI$=CHR$(142)THENUP=1
10040 IFASC(I$)=13THENLN=LN+1:IFLN>95THEN10100
10050 IFASC(I$)=13THENGOTO10030
10060 TT$(LN)=TT$(LN)+I$:GOTO10030
10100 CLOSE8:CLOSE1:FI$="made1":GOSUB3490
10110 IFEW=1THENOPEN1,8,15,"s0:made1":CLOSE1:RETURN
10120 TX$="replace "+II$+" with your modified msg?":GOSUB2040
10130 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN10130
10140 IFIN$="n"THENEW=1:GOTO10110
10150 IFIN$<>"y"THEN10130
10160 OPEN1,8,15,"s0:"+II$:PRINT#1,"r0:"+II$"=made1":CLOSE1
10170 RETURN
12000 TX$="clear feedback. are you sure?":GOSUB2040:
12010 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN12010
12020 IFIN$="n"THENRETURN
12030 IFIN$<>"y"THEN12010
12040 OPEN1,8,15,"s0:{pound}feedback"
12050 CLOSE1:OPEN8,8,8,"{pound}feedback,s,w"
12060 PRINT#8,"start":CLOSE8:RETURN
12515 IFI$=""THENRETURN
13100 TX$="--------------------":GOSUB2040
13105 TX$="Status: ":GOSUB2040
13110 TX$="Handle:"+NA$:GOSUB2040:TX$="Name:"+RN$:GOSUB2040
13120 TX$="Access lvl:"+STR$(AC):GOSUB2040:TX$="Time:"+TI$:GOSUB2040
13130 TX$="--------------------":GOSUB2040:RETURN
20000 PRINT"{clear}{down*3}{reverse on}{green}monster mode"
20010 PRINT"user update":C1=1:C2=0
20015 OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
20020 PRINT#1,CHR$(98)CHR$(C1)CHR$(C2)CHR$(60):INPUT#1,E1
20030 IFE1=50THENCLOSE1:CLOSE2:CLOSE8:GOTO20100
20040 CLOSE1:CLOSE2:PRINTC1+256*C2,:GOSUB6600:IFEQ=1THENE1=50:GOTO20030
20065 GOSUB20081
20070 C1=C1+1:IFC1=256THENC1=0:C2=C2+1:
20080 GOTO20015
20081 M1=VAL(LEFT$(LC$,2)):M2=VAL(RIGHT$(LC$,2))
20082 M2=M2+30:IFM2>30THENM1=M1+1:M2=M2-30:IFM1=13THENM1=1
20083 IFMN>M1THENNA$="deleted":PRINT"delete":AC$="0"
20084 IFMN=M1ANDDA>M2THENNA$="delete":PRINT"delete":AC$="0"
20085 CA$="0":GOSUB6100:RETURN
20100 PRINT"validating disk...":CLOSE1:OPEN1,8,15,"v":CLOSE1
20120 GOTO 2360
