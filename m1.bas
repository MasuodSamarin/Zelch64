!--------------------------------------------------
!- Friday, May 19, 2017 8:46:03 PM
!- Import of : 
!- c:\src\zelch64\m1.prg
!- Commodore 64
!--------------------------------------------------
10 GOTO3330
730 REM MODEM GET ROUTINE
740 IFTE=0ANDPEEK(56577)>239THENPRINT#7,"carrier dropped":GOTO4340
750 IFVAL(TI$)>TMTHENPRINT#7,"time up!":TX$="Time Up":GOSUB2040:GOTO4340
760 IFGX=1THEN800
770 GET#5,IN$
780 IFIN$=""THENRETURN
790 IN$=RV$(ASC(IN$)):RETURN
800 GET#5,IN$
810 RETURN
820 REM MODEM SEND ROUTINE
830 IFGX=1THEN900
840 PRINTOU$;:OU$=SE$(ASC(OU$))+CHR$(0)
850 PRINT#5,OU$;
860 RETURN
870 IFVAL(TI$)>TMTHENTM=1:TX$="Your time is UP!":GOSUB2040
880 RETURN
1890 REM HI
1900 IP=1:I$="":IFNA$=""THENQ1=VAL(TI$)+120:IFQ1>235959THENQ1=230000
1910 IFVAL(TI$)>Q1ANDNA$=""THEN4340
1920 A$="":IU$="":GOSUB730:A$=IN$:IFA$=""THENGETIU$:IFIU$=""THEN1920
1930 IFIU$="{f1}"THENGOSUB6210:GOTO1920
1940 IFIU$="{f3}"THENGOSUB6300:GOTO1920
1950 IFIU$="{f5}"THENGOSUB6340:GOTO1920
1960 IFIU$<>""THENA$=IU$
1970 IFA$=CHR$(13)THEN2030
1980 IFA$=CHR$(20)THEN2010
1990 IF(ASC(A$)AND127)<32THEN1920
1995 IFLEN(I$)>199THENRETURN
2000 OU$=A$:GOSUB820:GOSUB1360:I$=I$+A$:GOTO1910
2010 IFLEN(I$)=0THEN1920
2020 OU$=CHR$(20):GOSUB820:I$=LEFT$(I$,LEN(I$)-1):GOSUB1360:GOTO1910
2030 PRINT:PRINT#5:IP=0:GOSUB1360:RETURN
2040 TX$=CHR$(14)+TX$:IFTE=0THEN2070
2050 IFRIGHT$(TX$,1)=":"THENPRINTTX$;:RETURN
2060 PRINTTX$:RETURN
2070 PRINTTX$:IFGX=0THEN2100
2080 IFRIGHT$(TX$,1)=":"THENPRINT#5,TX$;:RETURN
2090 PRINT#5,TX$+CHR$(10):RETURN
2100 FORI=1TOLEN(TX$)
2110 PRINT#5,SE$(ASC(MID$(TX$,I,1)));:NEXT
2120 IFRIGHT$(TX$,1)<>":"THENPRINT#5,CHR$(10):RETURN
2130 RETURN
3330 CLOSE1:CLOSE8:EW=0:OPEN1,8,15:INPUT#1,E1,E1$,E2,E3:CLOSE1:IFGX=0THEN3380
3340 IFFG=0THENTX$="U)ppercase GFX?":GOSUB2040
3350 IFFG=0THENGOSUB730:IFIN$=""THENGETIN$
3360 IFFG=0THENIFIN$="y"THENUP=1:OU$=CHR$(142):GOSUB820:GOTO3380
3370 IFFG=0THENIFIN$<>"n"THEN3350
3380 TX$="{clear}{reverse on}{pink}Message{white} {light blue}Maker!":GOSUB2040
3390 TX$="{white}Type '-h' on a new line for options.":GOSUB2040
3400 TX$="You have 95 lines, Go!":GOSUB2040
3410 LN=0:FORI=0TO95:TT$(I)="":NEXT
3420 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN3420
3430 IFLEFT$(IQ$,1)="-"ANDIN$="h"THENIQ$="":GOTO3490
3450 IFIN$=CHR$(13)THENGOTO13000
3470 IFIN$=CHR$(20)ANDLG>0THENGOSUB3800:GOSUB1360:GOTO3420
3475 LG=LEN(IQ$):IFLG>=80THENLN=LN+1:IFLN>=50THEN3490
3480 IQ$=IQ$+IN$:OU$=IN$:GOSUB820:GOSUB1360:GOTO3420
3490 OU$=CHR$(13):GOSUB820:OU$=CHR$(10):GOSUB820
3495 TX$="S)ave E)dit V)iew C)ontinue A)bort":GOSUB2040
3500 TX$="X)Clear Text  D)elete":GOSUB2040
3510 GOSUB730:IFIN$=""THENGETIN$
3520 IFIN$="s"THEN3840
3530 IFIN$="a"THENEW=1:GOTO20000
3540 IFIN$="e"THEN3700
3550 IFIN$="v"THEN3590
3560 IFIN$="c"THENI9=1:GOTO3590
3570 IFIN$="x"THEN3330
3575 IFIN$="d"THENGOSUB13500:GOTO3490
3580 GOTO3510
3590 I1=0:CT=0:TX$="Line numbers?":GOSUB2040
3600 GOSUB730:IFIN$=""THENGETIN$
3610 IFIN$="y"THENI1=1:GOTO3630
3620 IFIN$<>"n"THEN3600
3630 TX$=TT$(CT):IFI1=1THENTX$=STR$(CT)+"-"+TX$
3640 GOSUB2040:GOSUB730:IFIN$=" "THEN3490
3650 GETIN$:IFIN$=" "THEN3490
3660 CT=CT+1:IFCT>LNTHEN3680
3670 GOTO3630
3680 IFI9=1THEN3420
3690 GOTO 3490
3700 F=1:TX$="Line to Edit:":GOSUB2040:GOSUB1890:Z=VAL(I$):IFZ>95THEN3490
3710 TX$=TT$(Z):GOSUB2040:TX$="Text to Change":GOSUB2040:GOSUB1890
3715 FL(5)=0:IFI$=""ANDTT$(Z)=""THENFL(5)=1:GOTO3791
3720 IFI$=""THEN3490
3730 IFMID$(TT$(Z),F,LEN(I$))=I$THEN3760
3740 F=F+1:IFF<=LEN(TT$(Z))THEN3730
3750 GOTO3790
3760 I3=LEN(I$):I4=LEN(TT$(Z))
3770 I1=F-1:I2=I4-F-I3+1:I1$=LEFT$(TT$(Z),I1):I2$=RIGHT$(TT$(Z),I2)
3780 TX$="Change to:":GOSUB2040:GOSUB1890:TT$(Z)=I1$+I$+I2$:GOTO3710
3790 TX$="text not found!":GOSUB2040:GOTO3490
3791 TX$="Change to:":GOSUB2040:GOSUB1890:TT$(Z)=I$:FL(5)=0:GOTO3710
3800 I$=RIGHT$(IQ$,1):IQ$=LEFT$(IQ$,LG-1):I=ASC(I$)
3810 IFI$="{down}"THENPRINT"{up}";:PRINT#5,"{up}";:RETURN
3820 IFI$="{up}"THENPRINT"{down}";:PRINT#5,"{down}";:RETURN
3825 IFI>31ANDI<130THEN3830
3826 IFI>148ANDI<156THEN3830
3827 IFI>159ANDI<234THEN3830
3828 IFI=17ORI=29ORI=157ORI=145THEN3830
3829 RETURN
3830 OU$=CHR$(20):GOSUB820:RETURN
3840 TX$="Are you sure?":GOSUB2040:
3850 GOSUB730:IFIN$=""THENGETIN$
3860 IFIN$="n"THEN3490
3870 IFIN$<>"y"THEN3850
3871 CLOSE1:OPEN1,MD(SG),15,"s0:"+FI$:CLOSE1
3875 OPEN8,MD(SG),8,FI$+",s,w":PRINT#8,QQ$:PRINT#8,QD$
3880 IFFG=1THENFG=0:PRINT#8,"{white}************************************"
3890 IFUP=1THENUP=0:PRINT#8,CHR$(142)+"{clear}"
3900 FORI=0TOLN:IFTT$(I)<>""THENPRINT#8,TT$(I)
3910 IFTT$(I)=""THENTT$(I)=" ":PRINT#8,TT$(I)
3920 NEXT:TX$="Saved":GOSUB2040:CLOSE8:CLOSE1:GOTO20000
3930 TX$="Filename":GOSUB2040:GOSUB1890:QD$=CHR$(13):GOSUB3330
3940 IFEW=1THENCLOSE8:OPEN1,8,15,"s0:"+FI$:GOTO20000
3950 CLOSE8:RETURN
3960 TX$="Change to UD number:":GOSUB2040
10000 TX$="filename:":GOSUB2040:GOSUB1890:II$=I$
10010 TX$="device #":GOSUB2040:GOSUB1890:IFI$=""THENDV=8:GOTO10020
10015 DV=VAL(I$)
10020 OPEN8,DV,8,II$+",s,r":LN=0
10030 GET#8,I$:IFSTTHEN10100
10035 IFI$=CHR$(142)THENUP=1
10040 IFASC(I$)=13THENLN=LN+1:IFLN>95THEN10100
10050 IFASC(I$)=13THENGOTO10030
10060 TT$(LN)=TT$(LN)+I$:GOTO10030
10100 CLOSE8:CLOSE1:FI$="made1":GOSUB3490
10110 IFEW=1THENOPEN1,8,15,"s0:made1":CLOSE1:RETURN
10120 TX$="replace "+II$+" with your modified msg?":GOSUB2040
10130 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN10130
10140 IFIN$="n"THENEW=1:GOTO10110
10150 IFIN$<>"y"THEN10130
10160 OPEN1,8,15,"s0:"+II$:PRINT#1,"r0:"+II$"=made1":CLOSE1
10170 RETURN
12000 TX$="clear feedback. are you sure?":GOSUB2040:
12010 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN12010
12020 IFIN$="n"THENRETURN
12030 IFIN$<>"y"THEN12010
12040 OPEN1,8,15,"s0:{pound}feedback"
12050 CLOSE1:OPEN8,8,8,"{pound}feedback,s,w"
12060 PRINT#8,"start":CLOSE8:RETURN
12515 IFI$=""THENRETURN
13000 TT$(LN)=IQ$:LN=LN+1:IFLN>=95THEN3490
13010 OU$=CHR$(13):GOSUB820:OU$=CHR$(10):GOSUB820
13020 GOSUB1360:GOTO 3420
13500 TX$="Delete From:":GOSUB2040:GOSUB1890:IFI$=""THENRETURN
13510 S1=VAL(I$):TX$="To ("+STR$(LN)+" = end):":GOSUB2040:GOSUB1890
13520 S2=VAL(I$):IFS1>S2THENRETURN
13530 FORI=S1TOS2:TX$=TT$(I):GOSUB2040:NEXT
13540 TX$="Delete this^ are you sure?":GOSUB2040
13550 GOSUB730:IFIN$=""THENGETIN$:IFIN$=""THEN13550
13560 IFIN$="n"THENRETURN
13570 IFIN$<>"y"THEN13550
13580 FORI=S1TOS2:TT$(I)="":NEXT
13590 FORI=0TOS2-S1:TT$(S1+I)=TT$(S2+I+1):NEXT:LN=LN-(S2-S1):IFLN<0THENLN=0
13600 RETURN
20000 POKE767,1:
20010 TX$="Standby..":GOSUB2040
20020 LOAD"{pound}b1",8,1
