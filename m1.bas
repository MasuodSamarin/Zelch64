!--------------------------------------------------
!- Friday, May 19, 2017 9:56:08 PM
!- Import of : 
!- c:\src\zelch64\m1.prg
!- Commodore 64
!--------------------------------------------------
5 IFPEEK(764)=3THENTX$="msg mkr active":GOSUB180:A=FRE(0):GOTO640
10 GOTO500
20 SYS49152
30 IFIN$<>""THENTX$=IN$+CHR$(13):SYS49161,TX$
50 IFPEEK(830)THEN400
60 RETURN
70 IFOU$=""THENRETURN
90 POKE254,ASC(OU$):SYS49155
100 RETURN
110 SYS49158
150 IFPEEK(830)THEN400
160 RETURN
170 REM
180 SYS49161,TX$
210 RETURN
400 RL=2520:TX$="Standby...":GOSUB170
410 LOAD"{pound}@1",8,1
500 CLOSE1:CLOSE8:IFPEEK(681)=0THEN600
510 TX$="UPPERCASE Graphics?:":GOSUB180:UP=0
520 GOSUB20:IFIN$=""THEN520
530 IFIN$="n"THEN600
540 IFIN$<>"y"THEN510
550 UP=1:OU$=CHR$(142):GOSUB70
600 FORI=0TO50:TT$(I)=CHR$(144):NEXT
610 A=FRE(0):LN=0
620 POKE763,0:TX$="{clear}{reverse on}{pink}message{white} {light blue}maker{white}!{reverse off}":GOSUB180
630 TX$="type *h for help":GOSUB180:TX$="50 lines.go: ":GOSUB180
640 GOSUB110:IFI$=""THENI$=CHR$(144)
649 TT$(LN)=I$
650 K$="":Q$=LEFT$(TT$(LN),2):IFLEFT$(Q$,1)="*"THENK$=RIGHT$(Q$,1)
680 IFK$="h"THEN1000
690 IFK$="s"THEN2000
700 IFK$="e"THENE$=RIGHT$(TT$(LN),LEN(TT$(LN))-2):IFLEN(E$)<5THEN3000
710 IFK$="d"THEN4000
730 IFK$="l"THEN6000
740 IFK$="c"THEN9000
750 IFK$="v"THEN7000
752 IFK$="i"THEN5000
755 IFK$="a"THEN8000
760 IFLN>49THENTX$="Line Limit!":GOSUB180:GOTO640
770 LN=LN+1:GOTO640
1000 OPEN8,8,8,"{pound}msgmkr help,s,r":SYS49164:CLOSE8:GOTO640
2000 TX$="save..(y/n)?:":GOSUB180
2010 GOSUB20:IFIN$=""THEN2010
2020 IFIN$="n"THEN640
2021 IFIN$<>"y"THEN2000
2030 OI=0:IFV1<8ORV1>11THENV1=8
2035 TX$="saving...":GOSUB180:GOSUB8500:OPEN8,V1,8,FI$+",s,r":CLOSE8
2040 OPEN1,V1,15:INPUT#1,E:CLOSE1:IFE<20THEN2047
2045 OPEN8,V1,8,FI$+",s,w":GOTO2050
2047 OPEN8,V1,8,FI$+",a":PRINT#8,CHR$(1)
2050 IFQQ$<>""ANDQD$<>""THENPRINT#8,QQ$:PRINT#8,QD$:PRINT#8,CHR$(2)
2055 IFUP=1THENPRINT#8,CHR$(142)+CHR$(147)
2060 I=0
2061 PRINT#8,TT$(I)
2062 IFI>=LN-1THEN2080
2063 I=I+1:GOTO2061
2080 CLOSE8:A=FRE(0):TX$="saved!":GOSUB180:GOTO8030
3000 Q=VAL(E$):IFQ>LNORQ<0THENTX$="bad #":GOSUB180:GOTO640
3010 TX$=TT$(Q):GOSUB180
3020 TX$="c)hange part or r)ewrite:":GOSUB180
3025 GOSUB20:IFIN$=""THEN3025
3030 IFIN$="c"THEN3060
3035 IFIN$=CHR$(13)THEN640
3040 IFIN$<>"r"THEN3020
3050 TX$="rewrite: ":GOSUB180:GOSUB110:TT$(Q)=I$:IFI$=""THENTT$(Q)=CHR$(144)
3055 GOTO640
3060 F=1:TX$="change:":GOSUB180:GOSUB110:I3=LEN(I$):I4=LEN(TT$(Q))
3070 IFI$=""ANDTT$(Q)=CHR$(144)THEN3050
3080 IFI$=""THEN640
3090 IFMID$(TT$(Q),F,I3)=I$THEN3130
3100 F=F+1:IFF<=I4THEN3090
3110 TX$="not found!":GOSUB180:GOTO640
3130 I1=F-1:I2=I4-F-I3+1:I1$=LEFT$(TT$(Q),I1):I2$=RIGHT$(TT$(Q),I2)
3140 TX$="to:":GOSUB180:GOSUB110:TT$(Q)=I1$+I$+I2$
3150 IFTT$(Q)=""THENTT$(Q)=CHR$(144)
3160 GOTO640
4000 TX$="from (0 -"+STR$(LN-1)+"):":GOSUB180:GOSUB110
4005 IFLEN(I$)>4ORI$=""THEN640
4006 S1=VAL(I$):IFS1>LN-1ORS1<0THEN640
4010 TX$="to:":GOSUB180:GOSUB110:IFI$=""ORLEN(I$)>4THEN640
4020 S2=VAL(I$):IFS1>S2ORS2>LN-1THEN640
4070 S3=S2-S1+1:IFI$=""THEN640
4080 FORI=S1TOLN:TT$(I)=TT$(I+S3):NEXT
4085 LN=LN-S3:IFLN<0THENLN=0
4090 A=FRE(0):GOTO640
5000 TX$="at (0 -"+STR$(LN-1)+"):":GOSUB180:GOSUB110:IFI$=""ORLEN(I$)>4THEN640
5010 S1=VAL(I$):IFS1>LNORLN+1>49ORS1<0THEN640
5070 FORI=LNTOS1+1STEP-1:TT$(I)=TT$(I-1):NEXTI:TT$(S1)=CHR$(144):LN=LN+1
5090 A=FRE(0):GOTO640
6000 I=0:OU$="{clear}":GOSUB70
6010 TX$=STR$(I)+")"+TT$(I):GOSUB180
6020 I=I+1:IFI>=LNTHEN640
6030 GOSUB20:IFIN$<>" "THEN6010
6040 GOTO640
7000 I=0:OU$="{clear}":GOSUB70
7010 TX$=TT$(I)+CHR$(144):GOSUB180
7020 I=I+1:IFI>=LNTHEN640
7030 GOSUB20:IFIN$<>" "THEN7010
7040 GOTO640
8000 TX$="abort...(y/n):":GOSUB180
8010 GOSUB20:IFIN$=""THEN8010
8019 IFIN$="n"THEN640
8020 IFIN$<>"y"THEN8000
8023 FORI=0TO50:TT$(I)="":NEXTI
8025 A=FRE(0):POKE763,1
8030 TX$=CHR$(14)+"Standby...":GOSUB180
8050 FF$="{pound}"+RIGHT$(CHR$(PEEK(721)),1)+"1
8060 LOADFF$,8,1
8500 FI$="":CT=0
8510 A=PEEK(CT+682):IFA=0THENRETURN
8520 FI$=FI$+CHR$(A):CT=CT+1:GOTO8510
9000 TX$="clear...(y/n):":GOSUB180
9010 GOSUB20:IFIN$=""THEN9010
9019 IFIN$="n"THEN640
9020 IFIN$<>"y"THEN9000
9030 GOTO600
