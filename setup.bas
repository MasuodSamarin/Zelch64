!--------------------------------------------------
!- Friday, May 19, 2017 8:45:50 PM
!- Import of : 
!- c:\src\zelch64\setup.prg
!- Commodore 64
!--------------------------------------------------
5000 POKE53280,0:POKE53281,0:PRINTCHR$(14)
5005 PRINT"{clear}{red}{reverse on}          EDITOR COMMANDS            "
5010 PRINT"{reverse on}{red*2}          {cm t*15}            "
5020 CLOSE2:CLOSE1:PRINT"{reverse on}{pink}1) Preps file"
5030 PRINT"{white}{reverse on}2) User log"
5040 PRINT"{light green}{reverse on}3) Menus"
5050 PRINT"{green}{reverse on}4) Mail"
5051 PRINT"{light blue}{reverse on}5) Necessary Files"
5052 PRINT"{blue}{reverse on}6) Other Main Files"
5060 GETA$:IFA$="1"THEN6000
5070 IFA$="2"THEN7000
5080 IFA$="3"THEN8000
5090 IFA$="4"THEN9000
5091 IFA$="5"THEN10000
5092 IFA$="6"THEN5200
5100 GOTO5060
5200 PRINT"{clear}{reverse on} create opening/closeing messages"
5210 PRINT"{down*4}O)pening or C)losing message?"
5220 GETA$:IFA$="o"THENFI$="{pound}opening msg":GOTO60000
5230 IFA$="c"THENFI$="{pound}closing msg":GOTO60000
5240 GOTO5220
6000 PRINT"{clear}{light blue}Put your Data Disk in drive 8 and"
6010 PRINT"{light blue}Press {reverse on}RETURN{reverse off}"
6020 GETA$:IFA$<>CHR$(13)THEN6020
6030 OPEN1,8,15,"i":CLOSE1
6040 OPEN8,8,8,"{pound}preps,s,r"
6050 FORI=0TO9:INPUT#8,UD$(I):INPUT#8,UD(I):INPUT#8,DV(I):NEXT:IFSTTHEN6090
6080 INPUT#8,BN$:INPUT#8,SN$:FORI=0TO9:INPUT#8,SG$(I):INPUT#8,SG(I)
6085 NEXTI:INPUT#8,MD:FORI=0TO9:INPUT#8,ML(I):INPUT#8,MD(I):NEXT
6087 GOTO6100
6090 CLOSE8:OPEN1,8,15:INPUT#1,I1,I2$:PRINTI1,I2$:CLOSE1:GOSUB6800
6100 CLOSE8:PRINT"{clear}"
6110 PRINT"{clear}1) Name of BBS:";BN$:PRINT"2) Name of SysOp:";SN$
6120 PRINT"3) Sig device #";DV(0):PRINT"4) Mail device #";MD
6130 PRINT"5) Sig Section   6) UD Section"
6135 PRINT"7) Save and Exit 8) Exit"
6140 GETA$:IFA$="1"THENINPUT"new name";BN$:IFBN$=""THEN6140
6145 IFA$="8"THENRUN
6150 IFA$="2"THENINPUT"Your Name";SN$:IFSN$=""THEN6150
6160 IFA$="3"THENINPUT"Device number";DV(0):IFDV(0)<8ORDV(0)>11THEN6160
6170 IFA$="4"THENINPUT"Device number";MD:IFMD<8ORMD>11THEN6170
6180 IFA$="5"THEN6200
6185 IFA$="6"THEN6500
6190 IFA$="7"THEN6900
6197 IFA$=""THEN6140
6199 GOTO6110
6200 PRINTTAB(1)"{clear}  #";TAB(5)"Sig Name";TAB(20)"Lvl";TAB(25)"Msg Lmt";
6210 PRINTTAB(35)"Dev #":FORI=0TO9
6220 PRINTTAB(1)I;TAB(5)SG$(I);TAB(20)SG(I);TAB(25)ML(I);TAB(35)MD(I):NEXT
6225 PRINT"'^' to exit"
6230 GETA$:IFA$=""THEN6230
6235 IFA$="^"THEN6100
6240 A=VAL(A$):IFA=0ANDA$<>"0"THEN6230
6250 PRINT"Name of sig "A:INPUTSG$(A):PRINT"Security Lvl for sig "A:INPUTSG(A)
6253 PRINT"Message Limit for Sig"A:INPUTML(A):PRINT"Dev. # for sig"A:INPUTMD(A)
6255 IFSG$(A)=""THEN6250
6260 GOTO6200
6500 PRINT"{clear}1) Active UD sections:";DV(1)
6510 PRINTTAB(1);" #";TAB(5)"UD name"TAB(20)"UD security";TAB(32)"device"
6520 FORI=2TO9:PRINTTAB(1)I;TAB(5)UD$(I-2);TAB(20)UD(I-2);TAB(32)DV(I)
6525 NEXTI:PRINT"'^' to exit"
6530 GETA$:A=VAL(A$):IFA$=""THEN6530
6535 IFA=1THEN:INPUT"How Many? (0-7)";DV(1):IFDV(1)<8THENGOTO6500
6536 IFA=1THEN6535
6537 IFA$="^"THEN6100
6540 IFA=0THEN6530
6550 INPUT"Name ";UD$(A-2):INPUT"Security Lvl";UD(A-2):INPUT"Device";DV(A)
6560 IFDV(A)<8ORDV(A)>11THEN6550
6570 IFUD$(A-2)=""THEN6550
6580 GOTO6500
6800 FORI=0TO9:UD$(I)="empty":BN$="Name":SN$="name":SG$(I)="Blank":NEXT
6810 FORI=0TO1000:NEXT:RETURN
6900 PRINT"Are you sure (Y/N)?"
6905 GETA$:IFA$="n"THEN6100
6907 IFA$<>"y"THEN6905
6910 OPEN1,8,15,"s0:{pound}preps":CLOSE1
6920 OPEN8,8,8,"{pound}preps,s,w"
6930 FORI=0TO9:PRINT#8,UD$(I):PRINT#8,UD(I):PRINT#8,DV(I):NEXT
6940 PRINT#8,BN$:PRINT#8,SN$:FORI=0TO9:PRINT#8,SG$(I):PRINT#8,SG(I):NEXT
6950 PRINT#8,MD:FORI=0TO9:PRINT#8,ML(I):PRINT#8,MD(I):NEXTI:CLOSE8:RUN
7000 PRINT"{clear}1) Create new user log"
7005 PRINT"2) Edit User Log"
7010 PRINT"3) Exit"
7020 GETA$:IFA$="1"THEN7500
7030 IFA$="3"THENRUN
7035 IFA$<>"2"THEN7020
7040 PRINT"d)isplay record headers
7050 PRINT"e)dit a record"
7055 PRINT"x)it "
7060 GETA$:IFA$="e"THEN7200
7065 IFA$="x"THENCLOSE1:CLOSE2:GOTO7000
7070 IFA$<>"d"THEN7060
7080 INPUT"start at record";S1
7090 INPUT"end at record";S2
7100 PRINT"space aborts..."
7105 OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
7110 I1=INT(S1/256):I3=I1*256:I2=S1-I3:
7120 PRINT#1,"p"CHR$(98)CHR$(I2)CHR$(I1)CHR$(5):IFST>60THEN7190
7130 INPUT#2,NA$:PRINT"#";S1;" ";NA$:GETA$:IFA$=" "THEN7190
7140 S1=S1+1:IFS1>S2THEN7190
7150 GOTO7110
7190 CLOSE2:CLOSE1:PRINT"hit any key"
7195 POKE198,0:WAIT197,64,64:GOTO7000
7200 INPUT"enter the record to edit";S1:I1=INT(S1/256):I3=I1*256:I2=S1-I3
7210 OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO7230
7220 PRINT#1,"p"CHR$(98)CHR$(I2)CHR$(I1)CHR$(I):IFI<100THENINPUT#2,A$
7225 IFST<64THENRETURN
7226 PRINT"no record":FORI=0TO2000:NEXT:CLOSE1:CLOSE2:RUN
7230 I=5:GOSUB7220:NA$=A$:I=25:GOSUB7220:PH$=A$:I=35:GOSUB7220:AC$=A$:
7240 I=40:GOSUB7220:RN$=A$:I=60:GOSUB7220:LA$=A$:I=75:GOSUB7220:CA$=A$
7250 I=80:GOSUB7220:TM$=A$:I=90:GOSUB7220:PA$=A$:I=100:GOSUB7220
7260 O=0:FORI=100TO145STEP5:GOSUB7220:INPUT#2,P$(O):O=O+1:NEXT
7270 PRINT"name:";NA$:PRINT"Phone:";PH$:PRINT"Access lvl:";AC$:
7280 PRINT"Real Name:";RN$:PRINT"Last Call Date:";LA$:PRINT"Calls today:";CA$
7290 PRINT"off at:";TM$:PRINT"password:";PA$:FORI=0TO9
7300 PRINT"msg of sig"I" = ";P$(I):NEXT:PRINT"Edit this record?"
7310 GETA$:IFA$="n"THENCLOSE2:CLOSE1:GOTO7000
7320 IFA$<>"y"THEN7310
7330 INPUT"name:";NA$:INPUT"Phone:";PH$:INPUT"Access lvl:";AC$:
7340 INPUT"Real Name:";RN$:INPUT"Last Call Date:";LA$:INPUT"Calls today:";CA$
7350 INPUT"off at:";TM$:INPUT"password:";PS$:FORI=0TO9
7360 PRINT"msg of sig "I" = ";:INPUTP$(I):NEXT:PRINT"Save this record?"
7370 GETA$:IFA$="n"THEN7270
7380 IFA$<>"y"THEN7370
7390 GOTO7760
7500 PRINT"enter your name (20 chars)":INPUTNA$
7510 PRINT"enter the phone #(10 chars)":INPUTPH$
7520 AC$="9":INPUT"your real name(20 chars)";RN$
7530 LA$="10/10/86":CA$="0":TM$="000000":INPUT"password (7 chars)";PA$
7535 FORI=0TO9:P$(I)="0":NEXT
7540 PRINT"take this questionare again (Y/N)"
7550 GETA$:IFA$="y"THEN7500
7560 IFA$<>"n"THEN7550
7749 OPEN1,8,15,"s0:{pound}userlog":I2=1:I1=0
7750 OPEN2,8,2,"{pound}userlog,l,"+CHR$(150)
7760 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(5)
7770 PRINT#2,NA$
7780 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(25)
7790 PRINT#2,PH$
7800 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(35)
7810 PRINT#2,AC$
7820 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(40)
7830 PRINT#2,RN$
7840 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(60)
7850 PRINT#2,LA$
7860 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(75)
7870 PRINT#2,CA$
7880 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(80)
7885 PRINT#2,TM$
7890 PRINT#1,"p"CHR$(2+96)CHR$(I2)CHR$(I1)CHR$(90)
7895 PRINT#2,PA$:GOTO7910
7900 PRINT#1,"p"CHR$(98)CHR$(I2)CHR$(I1)CHR$(I):PRINT#2,P$(O):RETURN
7910 O=0:FORI=100TO145STEP5:GOSUB7900:O=O+1:NEXT
7920 CLOSE2:CLOSE1
7950 PRINT"done. hit any key"
7960 POKE198,0:WAIT197,64,64:GOTO7000
8000 PRINT"1) main menu"
8010 PRINT"2) sig menu"
8020 PRINT"3) ud menu"
8024 PRINT"4) exit"
8030 GETA$:IFA$=""THEN8030
8040 IFA$="1"THENINPUT"what level";I:FI$="{pound}mainmenu"+STR$(I):GOSUB60000:GOTO5000
8050 IFA$="2"THENFI$="{pound}sigmenu":GOSUB60000:GOTO5000
8060 IFA$="3"THENFI$="{pound}udmenu":GOSUB60000:GOTO5000
8065 IFA$="4"THEN5000
8070 GOTO8030
60000 POKE650,128:POKE56325,40:PRINT"{clear}":POKE53280,0:POKE53281,0
60070 GOSUB61850:PRINT"{home}";:DIMCO$(15):DIMCH(255):DIMLN$(24):CO$(0)=CHR$(144)
60080 CO$(1)=CHR$(5)
60150 CO$(2)=CHR$(28):CO$(3)=CHR$(159):CO$(4)=CHR$(156):CO$(5)=CHR$(30)
60160 CO$(6)="{blue}":CO$(7)="{yellow}
60210 CO$(8)="{orange}":CO$(9)="{brown}":CO$(10)="{pink}":CO$(11)="{dark gray}":CO$(12)="{gray}":CO$(13)="{light green}
60270 CO$(14)="{light blue}":CO$(15)="{light gray}":T=0:FORT=TTO26:CH(T)=(T+64):NEXT:T=27:FORT=TTO31
60350 CH(T)=(T+64):NEXT:CH(32)=32:CH(96)=160:T=33:FORT=TTO63:CH(T)=T:NEXT:T=64
60360 FORT=TTO95
60450 CH(T)=(T+128):NEXT:T=97:FORT=TTO127:CH(T)=(T+64):NEXT:PC=0:RV=0:PRINT"{home}";
60500 T=0:FORT=TTO23
60560 T$=MID$(STR$(T),2,2):PRINTTAB(37):PRINTT$:NEXT:PRINT"{home}";:T=0:FORT=TTO23
60570 T1=0:FORT1=T1TO36
60650 CC=PEEK(55296+T1)+(T*40)AND15:IF(CC<>PC)THEN:PC=CC:LN$(T)=(LN$(T)+CO$(CC))
60670 CR=PEEK(((1024+T1)+(T*40))):IF(CR<>32)THEN:FL=1
60690 IF(CR>127)THEN:NR=1
60700 IF(CR<128)THEN:NR=0
60710 IF(NR>RV)THEN:RV=NR:LN$(T)=(LN$(T)+"{reverse on}")
60720 IF(NR<RV)THEN:RV=NR:LN$(T)=(LN$(T)+"{reverse off}")
60730 IF(CR>127)THEN:CR=(CR-128)
60740 LN$(T)=LN$(T)+CHR$(CH(CR)):GOSUB62060:PRINT"{right}";:NEXT:IFFL=0THENLN$(T)="
60790 FL=0:RV=0:PRINT:NEXT
60830 PRINT"{clear}1. directory":PRINT"{down}2. disk commands":PRINT"{down}3. save"
60840 INPUT"choice";A
60870 ONAGOSUB61050,61570:IF(A<3)THEN60830
60890 A$=FI$:PRINT"start save at  0";:INPUT"{left*3}";S
60900 PRINT"end save at  23";
60930 INPUT"{left*4}";E:OPEN2,8,2,(("@0:"+A$)+",s,w"):T=S:FORT=TTOE:CMD2,;
60940 PRINT#2,LN$(T):NEXT:CLOSE2:CLR
61020 GOTO60070:RETURN:GOTO5000
61050 PRINT"{clear}insert disk to read any hit any key
61060 GETA$:IF(A$="")THEN61060
61080 OPEN15,8,15:OPEN2,8,2,"#":TR=18:SC=1
61120 L=2:GOSUB61350
61140 CMD15,;:PRINT"b-p";:PRINT2;:PRINT#15,L:GET#2,T$:GET#2,A$:GET#2,A$:T=ASC((T$+CHR$(0))):IF(T=0)THEN61310
61150 T=ASC((T$+CHR$(0))):IF(T=0)THEN61310
61230 B$="":T=0:FORT=TTO15:GET#2,A$:IFA$<>CHR$(160)ANDA$<>CHR$(96)THEN:B$=B$=A$
61280 NEXT:IF(B$="")THEN61310
61300 PRINTB$
61310 L=(L+32):IF(L<250)THEN61140
61330 IFT1=18THEN:TR=T1:SC=S1:GOTO61120
61340 GOTO61510
61350 CMD15,;:PRINT"u1:";:PRINT2;:PRINT0;:PRINTTR;:PRINT#15,SC:GET#2,A$:CMD15,;:PRINT"b-p";:PRINT2;:PRINT#15,0:GET#2,A$
61400 PRINT"b-p";:PRINT2;:PRINT#15,0:GET#2,A$
61470 GET#2,B$:T1=ASC((A$+CHR$(0))):S1=ASC((B$+CHR$(0))):RETURN
61510 PRINT"hit any key to continue
61520 GETA$:IF(A$="")THEN61520
61540 CLOSE2:CLOSE15:RETURN
61570 D$="":PRINT"{clear}enter disk command":OPEN15,8,15:PRINT"> ";
61610 GETA$:IF(A$="")THEN61610
61630 TQ$=CHR$(20):TQ=LEN(D$)
61635 IFA$=TQ$ANDTQ>0THEND$=LEFT$(D$,LEN(D$)-1):PRINTCHR$(20);:GOTO61610
61640 IF(A$=CHR$(13))THEN61680
61650 PRINTA$;:D$=(D$+A$):GOTO61610
61680 IF(D$="")THEN:FL=1:GOSUB61800:PRINT:PRINTA;A$;B;C:GOSUB61760:RETURN
61690 CMD15,;:PRINT#15,D$:PRINT:GOSUB61800:IFATHEN:GOSUB61760
61740 CLOSE15:RETURN
61760 PRINT"hit any key to continue
61770 GETA$:IF(A$="")THEN61770
61790 RETURN
61800 INPUT#15,A,A$,B,C:ER$=A$:IF(FL=0)THEN:IF(A<>0)THEN:PRINTA;A$;B;C
61830 FL=0:RETURN
61850 INPUT"edit one already saved?";A$:IF(A$="y")THEN:GOSUB62170:GOTO61910
61880 PRINT"{clear}{white}";:POKE53280,0:POKE53281,0
61910 GOSUB62060
61920 GETA$:IF(A$="")THEN61920
61940 GOSUB62140:IF(A$="{f1}")THENRETURN
61960 IF(A$="{f3}")THEN:CLR:GOTO60070
61970 PRINTA$;:POKE212,0:POKE216,0:IF((POS(X)>36)AND(A$="{left}"))THEN:PRINTCHR$(13);
61980 IFPOS(X)>36ANDA$="{left}"THENPRINT"{up}";:PRINTTAB(36):GOTO62020
62010 IF(POS(X)>36)THEN:PRINTCHR$(13);:RV=0
62020 PO=(((PEEK(210)*256)+PEEK(209))+PEEK(211))
62030 IF(PO=>1984)THEN:PO=((PO-960)-1024):PRINT"{home}";:PRINTTAB(PO)
62040 IF((A$="{up}")AND(PO<1064))THEN:PRINT"{down*23}";
62050 GOTO61910
62060 PO=(((PEEK(210)*256)+PEEK(209))+PEEK(211)):B=PEEK(PO)
62070 C=PEEK(((PO-1024)+55296))
62090 IF(B<128)THEN:POKEPO,(B+128)
62100 IF(B>128)THEN:POKEPO,(B-128)
62110 IF(B=160)THEN:POKEPO,32
62120 POKE((PO-1024)+55296),PEEK(646):RETURN
62130 POKEPO,B:POKEPO-1024+55296,C:RETURN
62140 POKEPO,B:POKE((PO-1024)+55296),C:RETURN
62170 A$=FI$:OPEN2,8,2,A$:PRINT"{clear}";
62200 GET#2,A$:POKE212,0:POKE216,0:PRINTA$;:IF(ST=0)THEN62200
62250 PRINT"{home}{white}";:CLOSE2:RETURN:END
