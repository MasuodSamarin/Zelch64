!--------------------------------------------------
!- Saturday, May 20, 2017 12:06:10 AM
!- Import of : 
!- c:\src\zelch64\q1.prg
!- Commodore 64
!--------------------------------------------------
5 QQ$="":QD$="":IFDQ=0THENDQ=1:DIMK$(10),K1$(10)
6 IFPEEK(699)=1THENPOKE699,0:GOTO2050
10 GOTO1000
100 IN$="":SYS49152
110 IFPEEK(830)=1THEN500
120 RETURN
200 IFOU$=""THENRETURN
210 POKE254,ASC(OU$)
220 SYS49155
230 RETURN
300 I$=""
310 SYS49158
320 IFPEEK(830)=1THEN500
330 RETURN
400 IFTX$=""THENRETURN
420 SYS49161,TX$
430 RETURN
500 RL=2520:TX$="Standby...":GOSUB400
510 LOAD"{pound}@1",8,1
600 TX$="Standby...":GOSUB400
610 LOAD"{pound}b1",8,1
1000 TX$=CHR$(13)+"{white}SysOp:":GOSUB400:GOSUB100
1005 IFIN$=""THENGOSUB100:GOTO1005
1007 TX$=IN$+CHR$(13):GOSUB400
1010 IFIN$="?"THENOPEN8,8,8,"{pound}sysopmenu,s,r":SYS49164:CLOSE8:GOTO1000
1040 IFIN$="d"THEN4000
1050 IFIN$="l"THEN5000
1060 IFIN$="e"THEN6000
1065 IFIN$="q"THEN600
1070 GOTO1000
2000 TX$="Filename:":GOSUB400:GOSUB300:II$=I$:IFI$=""THEN4010
2005 FI$="{pound}make1":GOSUB9000
2010 TX$="Device:":GOSUB400:GOSUB300:V1=VAL(I$):IFV1<8ORV1>11THEN4010
2020 TX$="Loading...":GOSUB400
2030 POKE721,ASC("q"):POKE699,1
2040 LOAD"{pound}m1",8,1
2050 TX$="replace "+II$+" with your text?":GOSUB400:GOSUB100
2060 IFIN$="y"THEN2100
2070 IFIN$<>"n"THENGOSUB100:GOTO2060
2080 OPEN1,V1,15,"s0:{pound}make1":CLOSE1:GOTO4010
2100 OPEN1,V1,15,"s0:"+II$:PRINT#1,"r0:"II$"={pound}make1":CLOSE1:GOTO4010
3000 OU$="{clear}":GOSUB200:OPEN8,DV,0,I$:SYS49176:CLOSE8:GOTO4010
4000 DV=8
4010 TX$=CHR$(13)+STR$(DV)+":":GOSUB400:GOSUB300
4020 IFI$=""THENGOTO1000
4021 IFI$="@"THENOPEN1,8,15:INPUT#1,E1,E2$,E3,E4:CLOSE1:GOTO4050
4025 IFI$="read"THEN4100
4026 IFI$="write"THEN2000
4027 IFLEFT$(I$,1)="$"THEN3000
4030 IFVAL(I$)>7ANDVAL(I$)<11THENDV=VAL(I$):GOTO4010
4040 OPEN1,DV,15,I$:INPUT#1,E1,E2$,E3,E4:CLOSE1
4050 TX$=STR$(E1)+","+E2$+","+STR$(E3)+","+STR$(E4):GOSUB400:GOTO4010
4100 TX$="filename:":GOSUB400:GOSUB300:FI$=I$:IFI$=""THEN4010
4110 OPEN8,DV,8,FI$+",s,r"
4120 SYS49164
4125 IFPEEK(253)=0THEN4160
4130 TX$="cont (y/n)":GOSUB400
4140 GOSUB100:IFIN$="y"THEN4120
4150 IFIN$<>"n"THEN4140
4160 CLOSE8:GOTO4010
5000 TX$="start at:":GOSUB400:GOSUB300:I=VAL(I$):IFI=0THEN1000
5005 OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
5010 I1=INT(I/256):I3=I1*256:I2=I-I3
5020 PRINT#1,"p"CHR$(98)CHR$(I2)CHR$(I1)CHR$(5):GET#2,A$:INPUT#1,E1
5030 IFE1<>0ORA$=""THENCLOSE1:CLOSE2:GOTO1000
5040 PRINT#1,"p"CHR$(98)CHR$(I2)CHR$(I1)CHR$(5):SYS49185
5050 TX$="#"+STR$(I)+" "+I$:GOSUB400:GOSUB100:IFIN$=" "THENA$="":GOTO5030
5060 I=I+1:GOTO5010
6000 TX$="User #:":GOSUB400:GOSUB300:I=VAL(I$):IFI=0THEN1000
6010 BB=INT(I/256):I3=BB*256:B=I-I3:OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
6020 PRINT#1,"p"CHR$(98)CHR$(B)CHR$(BB)CHR$(5):GET#2,A$:IFST=0ANDA$<>""THEN6040
6030 TX$="no record":GOSUB400:CLOSE1:CLOSE2:GOTO1000
6040 GOSUB7000
6060 TX$="Handle:"+K$(0)+":":GOSUB400:GOSUB300:IFI$=""THEN6080
6070 IFLEN(I$)>17THEN6060
6075 K$(0)=I$
6080 TX$="Fone #"+K$(1)+":":GOSUB400:GOSUB300:IFI$=""THEN6100
6090 IFLEN(I$)>8THEN6080
6095 K$(1)=I$
6100 TX$="Access:"+K$(2)+":":GOSUB400:GOSUB300:IFI$=""THEN6120
6110 IFLEN(I$)>1THEN6100
6115 K$(2)=I$
6120 TX$="Real Name:"+K$(3)+":":GOSUB400:GOSUB300:IFI$=""THEN6140
6130 IFLEN(I$)>17THEN6120
6135 K$(3)=I$
6140 TX$="Password:"+K$(7)+":":GOSUB400:GOSUB300:IFI$=""THEN6160
6150 IFLEN(I$)>7THEN6140
6155 K$(7)=I$
6160 TX$="S)ave E)dit again Q)uit":GOSUB400
6170 GOSUB100:IFIN$="q"THEN1000
6180 IFIN$="e"THEN6060
6190 IFIN$<>"s"THEN6170
6195 GOSUB8000
6230 GOTO 1000
7000 REM GET USER INFO
7010 CLOSE1:CLOSE2:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO7030
7020 PRINT#1,"p"CHR$(98)CHR$(B)CHR$(BB)CHR$(I):SYS49185
7025 RETURN
7030 I=5:GOSUB7020:K$(0)=I$
7040 I=25:GOSUB7020:K$(1)=I$
7050 I=35:GOSUB7020:K$(2)=I$
7060 I=40:GOSUB7020:K$(3)=I$
7070 I=60:GOSUB7020:K$(4)=I$
7090 I=80:GOSUB7020:K$(6)=I$
7100 I=90:GOSUB7020:K$(7)=I$
7110 CT=0:FORI=100TO145STEP5:GOSUB7020:K1$(CT)=I$:CT=CT+1:NEXT
7120 CLOSE1:CLOSE2:RETURN
8000 REM SEND USER INFO
8010 CLOSE1:CLOSE2:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO8030
8020 PRINT#1,"p"CHR$(98)CHR$(B)CHR$(BB)CHR$(I):RETURN
8030 I=5:GOSUB8020:PRINT#2,K$(0)
8040 I=25:GOSUB8020:PRINT#2,K$(1)
8050 I=35:GOSUB8020:PRINT#2,K$(2)
8060 I=40:GOSUB8020:PRINT#2,K$(3)
8070 I=60:GOSUB8020:PRINT#2,K$(4)
8090 I=80:GOSUB8020:PRINT#2,K$(6)
8100 I=90:GOSUB8020:PRINT#2,K$(7)
8110 CT=0:FORI=100TO145STEP5:GOSUB8020:PRINT#2,K1$(CT):CT=CT+1:NEXT
8120 CLOSE1:CLOSE2:RETURN
9000 FORI=1TOLEN(FI$):POKE681+I,ASC(MID$(FI$,I,1)):NEXT:POKE681+I,0:RETURN
