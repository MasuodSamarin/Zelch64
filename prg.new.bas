!--------------------------------------------------
!- Friday, May 12, 2017 11:13:54 PM
!- Import of : 
!- c:\src\zelch64\bak\prg.new.prg
!- Commodore 128 BASIC 7/7.1
!--------------------------------------------------
8000 SYS51983
8010 CLOSE2
8020 TX$="{f1}ENTER YOUR{sh space}HANDLE (UNDER 17 CHARACTERS)"+CR$+":{ct c}":GOSUB5
8030 POKE731,1:GOSUB7:NN$=I$:IFLEN(NN$)>17ORI$=""THEN8020
8040 TX$="{f1}ENTER YOUR PHONE NUMBER (XXX/XXX-XXXX)"+CR$+":{ct c}":GOSUB5
8050 GOSUB7:PH$=I$:IFLEN(I$)>16ORI$=""THEN8040
8060 TX$="{f1}ENTER YOUR NAME (UNDER 17 CHARACTERS)"+CR$+":{ct c}":GOSUB5
8070 POKE731,1:GOSUB7:RN$=I$:IFLEN(I$)>17ORI$=""THEN8060
8080 TX$="{f1}ENTER A PASSWORD (UNDER 7 CHARACTERS)"+CR$+":{ct c}":GOSUB5
8090 GOSUB7:PS$=I$:IFLEN(I$)>7ORI$=""THEN8080
8091 TX$="{f1}DOES YOUR{sh space}TERMINAL{sh space}DISPLAY 80"+CR$+"COLUMNS (Y/N)?{ct c}":GOSUB5
8092 C$="yn":GOSUB66:GG=G:A$="NO":IFG=1THENA$="YES"
8100 TX$="{clear}{down}{f1}HANDLE:"+NN$+CR$+"{f1}PHONE:"+PH$+CR$+"{f1}NAME:"+RN$:GOSUB5
8105 TX$="{f1}PASSWORD:"+PS$+CR$+"{f1}80 COLUMNS:"+A$:GOSUB5
8110 TX$="{f1}{down*3}IS{sh space}ALL{sh space}THIS{sh space}CORRECT (Y/N)?{ct c}":GOSUB5:C$="yn":GOSUB66:IFG=2THEN8010
8140 TX$="{down}{f1}SCANNING USERLOG, STANDBY{ct c}":GOSUB5:U1=1:U2=0:OPEN2,8,2,"0:{pound}log1"
8150 I=0:U=U1:V=U2:GOSUB3:GET#2,A$:IFA$<>"{pound}"THEN8200
8160 SYS38233:IFI$="EMPTY"THEN8200
8170 IFI$=NN$THENTX$=CR$+"{f1}ILLEGAL{sh space}HANDLE!":GOSUB5:GOTO8010
8180 TX$="{f1}.{ct c}":GOSUB5:U1=U1+1:IFU1=256THENU2=U2+1:U1=0
8190 GOTO8150
8200 CLOSE2:ID=U1+(256*U2):TX$=" ":GOSUB5:E$="new #"+STR$(ID)+"/"+NN$:GOSUB9
8210 AC$=" 0":CA$=" 0":FORI=0TO9:P(I)=0:NEXTI:P(3)=20:P(6)=22:IFGG=1THENP(6)=7
8220 TX$="{clear}{down*7}{f1}YOUR{sh space}ID IS #"+STR$(ID)+CR$+"{f1}YOUR PASSWORD IS "+PS$:GOSUB5
8230 NA=ID:GOSUB75:LC$=STR$(A):TX$="{f1}{down*2}SAVING INFO...":GOSUB5
8240 NA$=NN$:Y0=1:GOSUB58:TX$="{f1}{down*2}COPY THIS INFO AND{sh space}HIT RETURN{ct c}":GOSUB5
8250 GOSUB35:NN=1:FI$="{pound}newuser":GOSUB25
9010 OPEN2,8,2,"0:{pound}newqs,s,r":GET#2,A$:I=0
9020 SYS38230:I1$(I)=I$:GET#2,A$:IFST>0THEN9040
9030 IFI<9THENI=I+1:GOTO9020
9040 CLOSE2:FORX=0TOI:TX$="{f1}"+I1$(X)+"{ct c}":GOSUB5:GOSUB7:I2$(X)=I$
9050 NEXTX:TX$="{f1}IS{sh space}THIS{sh space}CORRECT?{ct c}":GOSUB5:C$="yn":GOSUB66:IFG=2THEN9040
9080 OPEN2,8,2,"0:{pound}newinfo,s,a":INPUT#8,E:IFE<20THEN9100
9090 CLOSE2:OPEN2,8,2,"0:{pound}newinfo,s,w":PRINT#2,"start"
9100 PRINT#2,CHR$(1):PRINT#2,"#"+STR$(ID)+" "+NA$:FORX=0TOI
9110 PRINT#2,I1$(X)+I2$(X):NEXTX:CLOSE2:GOTO1145
