!--------------------------------------------------
!- Saturday, May 20, 2017 12:03:44 AM
!- Import of : 
!- c:\src\zelch64\@1.prg
!- Commodore 64
!--------------------------------------------------
10 POKE766,0
30 IFRL=1540THEN1210
40 IFRL=2520THEN2060
70 IFPEEK(766)=255THENPOKE766,0:POKE702,16
80 PRINTCHR$(14)
140 CLOSE5:OPEN5,2,3,CHR$(6)+CHR$(0)
150 DIMTT$(60),TT(60):POKE53280,0
170 FORJ=0TO9:CA(J)=J+1:NEXT:CA(9)=999
180 IFPEEK(702)<>16THENGOTO580
190 POKE702,0:MN=PEEK(703):DA=PEEK(704):IFPEEK(709)=1THENO=1
200 GOTO850
210 POKE56579,32:POKE56577,0:RETURN
220 POKE56579,6:POKE56577,2:RETURN
230 SYS 49152
260 IFPEEK(830)=1THEN2060
270 RETURN
280 IFOU$=""THENRETURN
300 POKE254,ASC(OU$):SYS49155
310 RETURN
450 POKE703,MN:POKE704,DA:POKE709,O
460 PRINT"Put your SYSTEMS disk in drive 8"
465 PRINT"and press RETURN"
466 GETA$:IFA$<>CHR$(13)THEN466
469 LOAD"e.menu",8,1
470 SYS 49158
510 IFPEEK(830)=1THEN2060
520 RETURN
530 IFTX$=""THENRETURN
560 SYS 49161,TX$
570 RETURN
580 PRINT"{yellow}";:MN=PEEK(703):DA=PEEK(704):O=PEEK(709)
810 PRINT"Put your data disk in drive 8"
820 PRINT"and press {reverse on}return{reverse off}":GOSUB210
830 GETA$:IFA$<>CHR$(13)THEN830
831 POKE56334,PEEK(56334)AND254:POKE1,PEEK(1)AND251:POKE788,0:POKE789,203
832 POKE1,PEEK(1)OR4:POKE56334,PEEK(56334)OR1
850 OPEN1,8,15:OPEN8,8,8,"{pound}Mpreps,s,r":INPUT#8,BN$:INPUT#1,E1
855 IFE1<>0THENCLOSE8:CLOSE1:PRINT"{up*2}";:POKE646,INT(RND(TI)*15)+1:GOTO810
860 INPUT#8,MD:INPUT#8,MO:DIMM1$(MO),M2(MO),M3$(MO)
861 FORI=0TOMO:INPUT#8,M1$(I):INPUT#8,M2(I)
870 INPUT#8,M3$(I):NEXT
890 CLOSE8:CLOSE1
900 OPEN8,8,8,"{pound}last caller,s,r":INPUT#8,L1$:SYS49182:L2$=I$:INPUT#8,LZ
910 CLOSE8:CLOSE1
920 PRINT"{clear}{down*12}{yellow}";BN$:C1=0:FORI=760TO767:POKEI,0:NEXT:GOSUB210
930 POKE713,0:POKE53280,0:POKE53281,0:PRINT"{yellow}BBS mode: {reverse on}F1{reverse off} for menu":POKE701,0
940 PRINT"{reverse on}F3{reverse off} to log-on":POKE896,0
960 PRINT"Last caller=";L1$:POKE830,0
970 PRINT"{yellow}Logged off at:";L2$
975 PRINT"calls today:";STR$(PEEK(733))
980 GOSUB210
990 GOSUB3700:PRINT"{home}{down}{right*2}{white}TIME=";TT$+"{black}" :POKE830,0:POKE713,0:POKE714,0
1000 A$=LEFT$(TT$,2):B$=RIGHT$(TT$,2):A=VAL(A$)
1010 IF(A=11ANDB$="pm")OR(A=12ANDB$="am")ANDPEEK(712)<>15THENPOKE712,15:GOTO3280
1020 IFPEEK(712)=15ANDVAL(A$)<12ANDB$="am"THENPOKE712,0
1030 GETA$:IFA$="{f1}"THEN1110
1040 IFA$="{f3}"THENPOKE714,1:POKE830,0:GOSUB220:GOTO1180
1060 IF(PEEK(56577)AND8)=8THEN990
1070 CT=0:PRINT"{white}RING!!!!":GOSUB220
1080 A=PEEK(56577):IF(AAND16)=0THENPOKE830,0:GOTO1180
1090 CT=CT+1:IFCT<11THENFORI=0TO1000:NEXT:GOTO1080
1100 PRINT"NO carrier":FORI=0TO2000:NEXT:GOTO920
1110 PRINT"{yellow}{clear}menu"
1120 PRINT"1)Editors
1130 PRINT"2)BBS
1131 PRINT"3)Monster mode"
1132 PRINT"4)Terminal"
1140 GETA$:IFA$=""THEN1140
1150 IFA$="1"THEN450
1160 IFA$="2"THEN920
1161 IFA$="3"THEN3280
1162 IFA$="4"THENPRINT"{clear}term active":GOSUB220:SYS49188:GOTO920
1170 GOTO1140
1180 CLOSE1:CLOSE2:CLOSE7:CLOSE8:POKE713,1:FORI=0TO500:NEXT
1200 POKE681,0:TX$=CHR$(13)+CHR$(13)+"{white}connection!!!":GOSUB530
1201 TX$=CHR$(13)+CHR$(13)+CHR$(13)+"hit return: ":GOSUB530
1202 TI$="000000"
1203 GOSUB230:IFVAL(TI$)>130THEN2060
1204 IFIN$<>CHR$(13)ANDIN$<>CHR$(10)THEN1203
1210 NA$=""
1211 TX$=CHR$(13)+CHR$(13)+"are you using commodore graphics?:":GOSUB530
1220 TI$="000000"
1240 GOSUB230:IFVAL(TI$)>130THEN2060
1250 IFIN$=CHR$(110)THENIN$=CHR$(206)
1255 IFIN$=CHR$(121)THENIN$=CHR$(217)
1259 IFIN$="n"ORIN$="N"THENPOKE681,0:GOTO1280
1260 IFIN$<>"Y"ANDIN$<>"y"THEN1240
1270 POKE681,1
1280 FI$="{pound}opening msg":GOSUB1990
1285 AC=-1:GOSUB3590:POKE713,0
1290 CLOSE2:CLOSE1:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":CT=0
1300 CT=CT+1:IFCT=6THENTX$="GOODBYE!":GOSUB530:GOTO2060
1310 TX$=CHR$(13)+"{yellow}Enter your ID number or NEW:":GOSUB530:GOSUB470
1315 IFLEN(I$)>4THEN1400
1320 NA=VAL(I$):IFI$="new"THENCLOSE1:CLOSE2:GOTO2670
1330 IFNA<1THEN1400
1340 NC=INT(NA/256):I3=NC*256:NB=NA-I3:POKE710,NB:POKE711,NC:C1=NB:C2=NC
1350 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(90):GET#2,A$:INPUT#1,E1
1360 IFA$=""ORE1>0THEN1400
1365 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(90):SYS49185:PS$=I$
1367 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(0)
1370 TX$=CHR$(13)+"{pink}Enter your password        :":GOSUB530:GOSUB470
1380 IFI$=PS$THEN1420
1390 TX$="{red}Invalid PASSWORD":GOSUB530:GOTO1300
1400 TX$="{red}Invalid ID":GOSUB530:TX$="{white}New Users type NEW at the ID prompt"
1410 GOSUB530:GOTO1300
1420 TX$="{clear}{home}Loading info":GOSUB530:GOSUB3020:GOSUB3700:AC=VAL(AC$):GOSUB3580
1421 SYS49191
1430 POKE1,53:PRINT"{clear}{pink}{reverse on}Hndl:{reverse off}{white}";NA$;TAB(23);"{reverse on}{light blue}Acs:{reverse off}{white}";AC$
1431 PRINT"{reverse on}{yellow}Name:{reverse off}{white}";RN$;TAB(23);"{reverse on}{light green}Time on:{reverse off}{white}";TT$
1434 PRINT"{reverse on}{cyan}ID#  {white}{reverse off}";STR$(NA);TAB(23);"{reverse on}{cyan}Off at:{white}{reverse off}"+I$
1440 TX$="{light blue}Welcome Back "+NA$+"!":GOSUB530:POKE733,PEEK(733)+1:GOTO1450
1445 IFPEEK(709)=1THENOPEN4,4,7:PRINT#4,NA$;":";TT$:PRINT#4:CLOSE4
1450 CA=VAL(CA$):IFCA>CA(AC)THENTX$="{red}Daily Call Number Exceeded!"
1460 IFCA>CA(AC)THENGOSUB530:GOTO2060
1470 C4$=STR$(CA(AC)-CA):TX$="{cyan}You may make "+C4$+" more calls today.":GOSUB530
1480 CLOSE2:CLOSE1
1481 TX$="You are caller #"+STR$(LZ):GOSUB530
1485 TX$="{blue}Last Call:"+LC$+CHR$(13)+"{light blue} Off at:"+TM$:GOSUB530
1487 TX$="{green}Last Caller:"+L1$+CHR$(13)+"{light green} Off at:"+L2$:GOSUB530
1490 TX$="{yellow}Checking for mail!":GOSUB530:I3=0
1500 OPEN8,MD,8,"{pound}mail"+STR$(NA)+",s,r":GET#8,A$:CLOSE8
1510 IFST<>0THENTX$="{white}NO mail.. (SORRY)":GOSUB530:GOTO1630
1530 TX$="{white}You Have Mail!":GOSUB530
1630 IFAC<4THEN1730
1635 OPEN8,8,8,"{pound}newsfile,s,r":GET#8,A$:CLOSE8
1640 IFST<>0THENTX$="{red}No News":GOSUB530:GOTO1730
1650 TX$="{yellow}System {purple}News{white}:":GOSUB530:OPEN8,8,8,"{pound}newsfile,s,r":SYS49164:CLOSE8
1730 TX$="Standby...":GOSUB530
1740 LOAD"{pound}b1",8
1990 OU$="{clear}":GOSUB280
2020 CLOSE1:CLOSE8:OPEN1,8,15:OPEN8,8,8,FI$+",s,r"
2030 SYS49164
2040 IFPEEK(253)=1THEND5=1
2050 CLOSE1:CLOSE8:RETURN
2060 POKE1,55:IFPEEK(830)=1THENTX$="{red}Time UP!!":GOSUB530
2065 POKE702,16:POKE703,MN:POKE704,DA
2080 IFNA$=""THENCLOSE1:CLOSE2:RUN
2090 OPEN8,8,8,"{pound}closing msg,s,r":SYS49164:CLOSE8:GOSUB3700
2105 IFPEEK(709)=1THENOPEN4,4,7:PRINT#4,"off at:";TT$:PRINT#4:CLOSE4
2110 TM$=TT$:LC$=STR$(MN)+"/"+STR$(DA):CA$=STR$(CA+1):C2=INT(NA/256):C3=C2*256
2120 C1=NA-C3:GOSUB2690:TX$="{red}{reverse on}Bye...":GOSUB530:FORI=0TO500:NEXTI
2130 GOSUB 210:CLOSE1:CLOSE2:OPEN1,8,15,"s0:{pound}last caller":CLOSE1
2140 GOSUB3700:OPEN1,8,15,"i":CLOSE1
2150 OPEN8,8,8,"{pound}last caller,s,w":PRINT#8,NA$+"  ":LC$=LC$+" "+TT$:PRINT#8,LC$
2160 PRINT#8,LZ+1:CLOSE8:OPEN1,8,15,"i":CLOSE1:POKE702,16:RUN
2670 RL=1540:POKE766,1:TX$="Standby...":GOSUB530
2680 LOAD"{pound}n1",8,1
2690 CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
2699 GOTO2710
2700 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I):RETURN
2710 I=5:GOSUB2700:PRINT#2,NA$
2720 I=25:GOSUB2700:PRINT#2,PH$
2730 I=35:GOSUB2700:PRINT#2,AC$
2740 I=40:GOSUB2700:PRINT#2,RN$
2750 I=60:GOSUB2700:PRINT#2,LC$
2770 I=80:GOSUB2700:PRINT#2,TM$
2780 I=90:GOSUB2700:PRINT#2,PS$
2790 CT=0
2800 FORI=100TO145STEP5
2810 GOSUB2700:PRINT#2,P$(CT)
2820 CT=CT+1:NEXT
2830 CLOSE1:CLOSE2
2840 OPEN1,8,15:OPEN2,8,2,"{pound}calls"
2850 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(1)
2860 PRINT#2,CA$
2870 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(0)
2880 CLOSE1:CLOSE2:RETURN
3020 EQ=0:CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
3025 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(0)
3029 GOTO3060
3030 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I):SYS49185
3050 RETURN
3060 I=5:GOSUB3030:INPUT#1,E1:IFE1>0THENCLOSE1:CLOSE2:RETURN
3070 NA$=I$:I=25:GOSUB3030:PH$=I$
3080 I=35:GOSUB3030:AC$=I$
3090 I=40:GOSUB3030:RN$=I$
3100 I=60:GOSUB3030:LC$=I$
3120 I=80:GOSUB3030:TM$=I$
3130 I=90:GOSUB3030:PS$=I$
3140 CT=0
3150 FORI=100TO145STEP5
3160 GOSUB3030:P$(CT)=I$
3170 CT=CT+1:NEXT
3180 CLOSE1:CLOSE2:OPEN1,8,15:OPEN2,8,2,"{pound}calls"
3185 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(0)
3190 I=1:GOSUB3030:CA$=I$:CLOSE1:CLOSE2:RETURN
3280 POKE712,15:POKE733,0:PRINT"{clear}{down*3}{reverse on}{green}monster mode"
3290 PRINT"user update":C1=1:C2=0:PRINT"Counting users..."
3300 OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
3310 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(25):GET#2,A$:INPUT#1,E1
3315 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(1)
3320 IFA$=""ORE1>0THENCLOSE1:CLOSE2:GOTO3370
3330 PRINT"{home}{down*6}";STR$(C1+256*C2)
3350 C1=C1+1:IFC1=256THENC1=0:C2=C2+1:
3360 GOTO3310
3370 PRINTCHR$(13)+"Saving Calls..."
3375 OPEN1,8,15,"s0:{pound}calls":OPEN2,8,2,"{pound}calls,l,"+CHR$(6):C=C1+256*C2
3380 FORI=1TOC:C2=INT(I/256):C3=C2*256:C1=I-C3
3390 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(1):PRINT#2,"0":NEXTI
3400 CLOSE1:CLOSE2
3420 PRINT"Validating Disk...":CLOSE1:OPEN1,8,15,"v":CLOSE1
3421 DA=DA+1:IFDA>29THENMN=MN+1:IFMN=13THENMN=1
3422 POKE703,MN:POKE704,DA:POKE709,O
3430 GOTO920
3580 REM
3590 H=PEEK(56331):M=PEEK(56330):S=PEEK(56329):T=PEEK(56328):CH=0
3600 IFH>128THENCH=128
3610 H1=(HAND16)/16:H2=(HAND15):M1=(MAND240)/16:M2=(MAND15)
3615 IFAC=-1THENM2=M2+5:IFM2>=10THENM2=M2-10:M1=M1+1
3620 TW=AC+1:IFTW>5THENTW=5
3630 M1=M1+TW:IFM1<6THEN3680:
3640 M1=M1-5
3650 H2=H2+1:IFH2>=10ANDH1=0THENH2=H2-10:H1=1:GOTO3680
3660 IFH2<=1ORH1<>1THEN3680
3665 H1=1:H2=2:IFCH=128THENCH=0:GOTO3680
3670 IFCH=0THENCH=128
3680 H=(16*H1)+H2+CH:M=(16*M1)+M2
3690 POKE896,H:POKE897,M:POKE898,S:POKE899,T:RETURN
3700 SYS49167:TT$=I$:RETURN
