!--------------------------------------------------
!- Saturday, May 20, 2017 3:19:24 AM
!- Import of : 
!- c:\src\zelch64\bbs.prg
!- Commodore 64
!--------------------------------------------------
10 PRINTCHR$(14)
11 GOTO27
12 IFP0>250ORE$=""THENRETURN
13 P0$(P0)=E$:P0=P0+1:RETURN
27 CLOSE5:OPEN5,2,3,CHR$(8)+CHR$(0)+CHR$(0)+CHR$(0):PRINT#5,"ate0s0=1m1x0"
30 DIMTT$(51),TT(51),Z1(51),Z2(50),P0$(250):POKE53280,0:BA=300
50 IFPEEK(702)<>16THENGOTO230
60 POKE702,0:MN=PEEK(703):DA=PEEK(704)
70 GOTO290
80 PRINT#5:FORI=0TO900:NEXT:PRINT#5,"+++":FORI=0TO900:NEXT:RETURN
81 RETURN
100 IN$="":SYS49152:IFPEEK(830)=1THEN1230
101 IFPEEK(197)<>5THENRETURN
102 PRINTCHR$(13)+"CURRENT{sh space}ACCESS:"+STR$(AC)
103 INPUT"NEW{sh space}ACCESS";AC
104 PRINT"BURST{sh space}TIME?";
105 GETA$:IFA$="n"ORA$=CHR$(13)THENRETURN
106 IFA$<>"y"THEN105
107 T1=0:T2=2:GOSUB1700
110 RETURN
111 I=0:SYS49158:IFI$=""ORLEN(I$)>4THENRETURN
112 IFVAL(I$)>0THENI=VAL(I$)
113 RETURN
120 IFOU$=""THENRETURN
130 POKE254,ASC(OU$):SYS49155:RETURN
190 SYS49158:POKE710,0:IFPEEK(830)=1THEN1230
200 RETURN
210 IFTX$=""THENRETURN
220 SYS 49161,TX$:RETURN
230 PRINT"{yellow}";:MN=PEEK(703):DA=PEEK(704):O=PEEK(709)
240 PRINT"Put your data disk in drive 8"
250 PRINT"and press {reverse on}return{reverse off}"
260 GETA$:IFA$<>CHR$(13)THEN260
270 POKE56334,PEEK(56334)AND254:POKE1,PEEK(1)AND251:POKE788,0:POKE789,203
280 POKE1,PEEK(1)OR4:POKE56334,PEEK(56334)OR1
290 OPEN1,8,15:OPEN8,8,8,"{pound}variables,s,r":INPUT#8,BN$:INPUT#1,E1
300 IFE1<>0THENCLOSE8:CLOSE1:PRINT"{up*2}";:POKE646,INT(RND(TI)*15)+1:GOTO240
310 INPUT#8,MD,CD$,CC$,CE$,CF$,CG$,SG:DIMSG$(SG),SG(SG),SD(SG),SF(SG),SH(SG)
314 FORI=0TOSG:INPUT#8,SG$(I),SG(I),SD(I),SF(I):NEXTI
315 INPUT#8,UD:DIMUD$(UD),UA(UD),UV(UD)
316 FORI=0TOUD:INPUT#8,UD$(I),UA(I),UV(I):NEXTI:FORI=0TO9:INPUT#8,CA(I),TM(I)
320 NEXTI:INPUT#8,UL:CLOSE8:CLOSE1
380 OPEN8,8,8,"{pound}last caller,s,r":INPUT#8,L1$:SYS49182:L2$=I$:INPUT#8,LZ
389 INPUT#8,HN:CLOSE8:FH=1:I=1200:POKE659,8:GOSUB6230
390 PRINT"{clear}{down*11}{yellow}";BN$:C1=0:GOSUB80:POKE682,0:POKE714,0:POKE705,0
400 POKE53280,0:POKE53281,0:PRINT"{yellow}BBS mode: {reverse on}F1{reverse off} for menu":POKE829,0
410 PRINT"{reverse on}F3{reverse off} to log-on":POKE896,0
430 PRINT"Last caller=";L1$
440 PRINT"{yellow}Logged off at:";L2$
450 PRINT"Calls today:";STR$(PEEK(733))
455 LA$="":RI=0
460 SYS49167:PRINT"{home}{down}{right*2}{white}TIME=";I$+"{black}"
470 A$=LEFT$(I$,2):B$=RIGHT$(I$,2):A=VAL(A$)
480 IFA=11ANDB$="pm"ANDPEEK(712)<>15THENPOKE712,15:GOTO1530
485 IFA=12ANDB$="am"ANDPEEK(712)<>15THENPOKE712,15:GOTO1530
490 IFPEEK(712)=15ANDVAL(A$)<12ANDB$="am"THENPOKE712,0
500 GETA$:IFA$="{f1}"THEN580
520 IFA$="{f3}"THENPOKE714,1:POKE830,0:GOTO690
530 GET#5,A$:IFA$=""THEN460
531 LA$=LA$+A$:POKE212,0:IFLEN(LA$)<3THEN537
532 E$="no carrier":F$="ring":G$="connect 1200":H$="connect"+CHR$(13)
533 IFMID$(LA$,2)=E$ORLA$=E$THEN455
534 IFMID$(LA$,2)=F$ORLA$=F$THENRI=RI+1:IFRI>1THEN455
535 IFMID$(LA$,2)=G$ORLA$=G$THENPOKE830,0:POKE714,0:GOTO690
536 IFMID$(LA$,2)=H$ORLA$=H$THENGOSUB6800:POKE830,0:POKE714,0:GOTO690
537 IFA$=CHR$(13)THEN455
538 GOTO530
580 PRINT"{yellow}{clear}menu"
590 PRINT"1)Return to BBS
600 PRINT"2)Monster mode
610 PRINT"3)Zelch (Zap){sh space}Term"
620 PRINT"4)Read Feedback"
621 PRINT"5)View log"
630 GETA$:IFA$=""THEN630
640 IFA$="1"THEN390
650 IFA$="2"THEN1530
660 IFA$="3"THEN6810
665 IFA$="4"THENFI$="{pound}feedback":GOSUB5690
667 IFA$="5"THENFI$="{pound}Log!":GOSUB5690
680 GOTO630
690 CLOSE1:CLOSE2:CLOSE7:CLOSE8:FORI=0TO500:NEXT
700 POKE681,0:TX$=CHR$(13)+CHR$(13)+"{white*2}connection!!!":GOSUB210
701 SYS49167:E$=I$+" Call":GOSUB12
710 TX$=CHR$(13)+CHR$(13)+"hit return: ":GOSUB210
720 TI$="000000"
730 GOSUB100:IFVAL(TI$)>59THEN1230
740 IFIN$<>CHR$(13)ANDIN$<>CHR$(10)THEN730
750 NA$="":TX$=CHR$(13)+CHR$(13)+"are you using commodore graphics?{ct c}":GOSUB210
760 TI$="000000"
770 GOSUB100:IFVAL(TI$)>59THEN1230
780 IFIN$=CHR$(110)THENIN$=CHR$(206)
790 IFIN$=CHR$(121)THENIN$=CHR$(217)
800 IFIN$="n"ORIN$="N"THENOU$=IN$:GOSUB120:GOTO830
810 IFIN$<>"Y"ANDIN$<>"y"THEN770
820 POKE681,1:OU$=IN$:GOSUB120
830 NN=1:FI$="{pound}opening msg":GOSUB1200:T1=1:T2=0:GOSUB1710
840 CLOSE2:CLOSE1:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":CT=0
850 CT=CT+1:IFCT=6THENTX$="GOODBYE!":GOSUB210:GOTO1230
860 TX$=CHR$(13)+"{yellow}Enter your ID number or NEW:{ct c}":GOSUB210:GOSUB111
870 IFI=0ANDI$<>"new"THEN850
880 NA=VAL(I$):IFI$="new"THENCLOSE1:CLOSE2:GOTO1850
900 C2=INT(NA/256):I3=C2*256:C1=NA-I3
910 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(90):GET#2,A$:INPUT#1,E1
920 IFA$=""ORE1>0THEN970
930 SYS49185:PS$=A$+I$
940 TX$=CHR$(13)+"{pink}Enter your password        :{ct c}":GOSUB210:POKE710,1:GOSUB190
950 TX$=" ":GOSUB210:IFI$=PS$THENCLOSE1:CLOSE2:GOTO990
960 TX$="{red}Invalid PASSWORD":GOSUB210:GOTO850
970 TX$="{red}Invalid ID":GOSUB210:TX$="{white}New Users type NEW at the ID prompt"
980 GOSUB210:GOTO850
990 TX$="{clear}{home}Loading info":GOSUB210:GOSUB1440:AC=VAL(AC$):E$=NA$:GOSUB12
991 T2=INT(TM(AC)/10):T1=TM(AC)-(T2*10):GOSUB1700
1000 POKE1,53:PRINT"{clear}{pink}{reverse on}Hndl:{reverse off}{white}";NA$;TAB(23);"{reverse on}{light blue}Acs:{reverse off}{white}";AC$
1010 SYS49167:PRINT"{reverse on}{yellow}Name:{reverse off}{white}";RN$;TAB(23);"{reverse on}{light green}Time on:{reverse off}{white}";I$
1020 SYS49191:PRINT"{reverse on}{cyan}ID#  {white}{reverse off}";STR$(NA);TAB(23);"{reverse on}{cyan}Off at:{white}{reverse off}"+I$
1030 PP=VAL(P$(0))+256*VAL(P$(1))
1040 TX$="{light blue}Welcome Back "+NA$+"!":GOSUB210:POKE733,PEEK(733)+1:GOTO1060
1050 CA=VAL(CA$):IFPEEK(709)=1THENOPEN4,4,7:PRINT#4,NA$;":";TT$:PRINT#4:CLOSE4
1060 IFCA>CA(AC)THENTX$="{red}Daily Call Limit Exceeded!":GOSUB210:GOTO1230
1070 TX$="{cyan}You may make "+STR$((CA(AC))-VAL(CA$))+" more calls today."
1080 GOSUB210:TX$="You are caller #"+STR$(LZ):GOSUB210:Q$=CHR$(13)
1090 TX$="{blue}Last Call:"+LC$+Q$+"{light blue} Off at:"+TM$:GOSUB210
1100 TX$="{green}Last Caller:"+L1$+Q$+"{light green} Off at:"+L2$:GOSUB210
1110 TX$="{yellow}Checking for mail!":GOSUB210:I3=0
1120 OPEN8,MD,8,"{pound}mail"+STR$(NA)+",s,r":GET#8,A$:CLOSE8
1130 IFST<>0THENTX$="{white}NO mail.. (SORRY)":GOSUB210:GOTO1150
1140 TX$="{white}You Have Mail!{ct g}":GOSUB210
1150 IFAC<4THEN1190
1160 OPEN8,8,8,"{pound}newsfile,s,r":GET#8,A$:CLOSE8
1170 IFST<>0THENTX$="{red}No News":GOSUB210:GOTO1190
1180 TX$="{yellow}System {purple}News{white}:{ct c}":GOSUB210:OPEN8,8,8,"{pound}newsfile,s,r":SYS49164:CLOSE8
1190 IFVAL(P$(2))<=BATHEN2120
1191 TX$="{yellow}Change to "+P$(2)+" Baud?{ct c}":GOSUB210:
1192 GOSUB100:IFIN$="n"THENTX$="n":GOSUB210:GOTO2120
1193 IFIN$<>"y"THEN1192
1194 TX$="y"+CHR$(13)+"{pink}Change and hit {reverse on}RETURN{reverse off}{ct c}":GOSUB210:I=VAL(P$(2))
1195 FH=1:GOSUB6230:GOSUB6370:GOTO2120
1200 IFNN=1THENOU$="{clear}":GOSUB120
1210 IFVD<8ORVD>11THENVD=8
1220 OPEN8,VD,8,FI$+",s,r":SYS49164:D5=PEEK(253):CLOSE8:NN=0:VD=0:RETURN
1230 CLOSE1:CLOSE2:CLOSE8:CLOSE9:CLOSE7
1240 POKE1,55:IFPEEK(830)=1THENTX$="{red}Time UP!!":GOSUB210
1250 POKE703,MN:POKE704,DA:OPEN1,8,15:CLOSE1
1260 IFNA$=""THENPOKE702,16:RUN
1270 FI$="{pound}closing msg":GOSUB1200
1280 IFPEEK(709)=1THENOPEN4,4,7:SYS49167:PRINT#4,"off at:";I$:PRINT#4:CLOSE4
1290 CA$=STR$(VAL(CA$)+1)
1300 SYS49167:TM$=I$:LC$=STR$(MN)+"/"+STR$(DA):C2=INT(NA/256):C3=C2*256
1310 C1=NA-C3:GOSUB1350:TX$="{red}{reverse on}Bye...":GOSUB210:FORI=0TO500:NEXTI
1311 PRINT"{white}{reverse on}Saving Log...":OPEN8,8,8,"{pound}Log!,s,r":CLOSE8:OPEN1,8,15
1312 INPUT#1,E:CLOSE1:IFE=0THEN1317
1313 OPEN8,8,8,"{pound}Log!,s,w":PRINT#8,"start":CLOSE8
1317 IFP0=0THEN1320
1318 OPEN8,8,8,"{pound}Log!,a":FORI=0TOP0-1:PRINT#8,P0$(I):NEXTI
1319 SYS49167:PRINT#8,"off at:"+I$:PRINT#8,CHR$(1):CLOSE8
1320 CLOSE1:OPEN1,8,15,"s0:{pound}last caller":CLOSE1:SYS49167
1330 OPEN8,8,8,"{pound}last caller,s,w":PRINT#8,NA$+"  ":LC$=LC$+" "+I$:PRINT#8,LC$
1340 PRINT#8,LZ+1:PRINT#8,HN:CLOSE8:OPEN1,8,15,"i":CLOSE1:POKE702,16:RUN
1350 CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO1370
1360 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I):RETURN
1370 I=5:GOSUB1360:PRINT#2,NA$:I=25:GOSUB1360:PRINT#2,PH$:I=35:GOSUB1360
1380 PRINT#2,AC$:I=40:GOSUB1360:PRINT#2,RN$:I=60:GOSUB1360:PRINT#2,LC$
1390 I=80:GOSUB1360:PRINT#2,TM$:I=90:GOSUB1360:PRINT#2,PS$:CT=0
1395 IFY0=1THENY0=0:GOTO1410
1400 I2=INT(HN/256):P$(1)=STR$(I2):I2=I2*256:I1=HN-I2:P$(0)=STR$(I1)
1410 FORI=100TO145STEP5:GOSUB1360:PRINT#2,P$(CT):CT=CT+1:NEXT:CLOSE1:CLOSE2
1420 OPEN1,8,15:OPEN2,8,2,"{pound}calls"
1430 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(1):PRINT#2,CA$:CLOSE1:CLOSE2:RETURN
1440 EQ=0:CLOSE1:CLOSE2:CLOSE8:OPEN1,8,15:OPEN2,8,2,"{pound}userlog":GOTO1470
1460 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I):SYS49185:RETURN
1470 I=5:GOSUB1460:INPUT#1,E1:IFE1>0THENCLOSE1:CLOSE2:RETURN
1480 NA$=I$:I=25:GOSUB1460:PH$=I$:I=35:GOSUB1460:AC$=I$:I=40:GOSUB1460:RN$=I$
1490 I=60:GOSUB1460:LC$=I$:I=80:GOSUB1460:TM$=I$:I=90:GOSUB1460:PS$=I$:CT=0
1500 FORI=100TO145STEP5:GOSUB1460:P$(CT)=I$:CT=CT+1:NEXT:CLOSE1:CLOSE2
1510 OPEN1,8,15:OPEN2,8,2,"{pound}calls":PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(1)
1520 SYS49185:CA$=I$:CLOSE1:CLOSE2:RETURN
1530 POKE712,15:POKE733,0:PRINT"{clear}{down*3}{reverse on}{green}monster mode"
1540 PRINT"user update":C1=1:C2=0:PRINT"Counting users..."
1550 OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
1560 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(25):GET#2,A$:INPUT#1,E1
1580 IFA$=""ORE1>0THENCLOSE1:CLOSE2:GOTO1620
1590 PRINT"{home}{down*6}";STR$(C1+256*C2)
1600 C1=C1+1:IFC1=256THENC1=0:C2=C2+1:
1610 GOTO1560
1620 PRINTCHR$(13)+"Saving Calls..."
1630 OPEN1,8,15,"s0:{pound}calls":OPEN2,8,2,"{pound}calls,l,"+CHR$(6):C=C1+256*C2
1640 FORI=1TOC:C2=INT(I/256):C3=C2*256:C1=I-C3
1650 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(1):PRINT#2,"0":NEXTI:CLOSE1:CLOSE2
1660 PRINT"Validating Disk...":CLOSE1:OPEN1,8,15,"v":CLOSE1
1670 DA=DA+1:IFDA>29THENMN=MN+1:IFMN=13THENMN=1
1680 POKE703,MN:POKE704,DA:POKE709,O
1690 GOTO390
1700 REM
1710 H=PEEK(56587):M=PEEK(56586):S=PEEK(56585):T=PEEK(56584):CH=0
1720 IFH>128THENCH=128
1730 H3=(HAND16)/16:H2=(HAND15):H1=H3*10+H2:M1=(MAND240)/16:M2=(MAND15)
1760 M1=M1+T1:IFM1>=6THENM1=M1-6:T2=T2+1
1775 IFT2=0THEN1820
1780 H1=H1+1:T2=T2-1:IFH1=13THENH1=1
1790 IFH1<>12THEN1820
1800 IFCH=128THENCH=0:GOTO1820
1810 IFCH=0THENCH=128
1820 IFT2>0THEN1780
1821 H3=INT(H1/10):H4=H3*10:H2=H1-H4:
1830 H=(16*H3)+H2+CH:M=(16*M1)+M2
1840 POKE896,H:POKE897,M:RETURN
1850 T1=1:T2=0:GOSUB1700:TX$="{cyan}Finding an opening. Standby{ct c}":GOSUB210:C1=1:C2=0
1860 OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
1870 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(5):GET#2,A$:INPUT#1,E1
1880 IFE1>0ORA$=""THEN1930
1890 SYS49185:I$=A$+I$
1900 IFI$="EMPTY"THEN1930
1910 OU$=".":GOSUB120:C1=C1+1:IFC1=256THENC1=0:C2=C2+1:GOTO1870
1920 GOTO1870
1930 CLOSE1:CLOSE2:ID=C1+(256*C2):OU$=CHR$(13):GOSUB120:Q$=CHR$(13)
1940 TX$="{pink}Enter your HANDLE (17 chars or less)"+Q$+":{ct c}":GOSUB210
1950 GOSUB190:NN$=I$:IFLEN(I$)>17ORI$=""THEN1940
1960 TX$="{yellow}Enter your PHONE{sh space}NUMBER (XXX-XXXX)"+Q$+":{ct c}":GOSUB210
1970 GOSUB190:PH$=I$:IFLEN(I$)>9ORI$=""THEN1960
1980 TX$="{white}Enter your NAME (17 chars or less)"+Q$+":{ct c}":GOSUB210
1990 GOSUB190:RN$=I$:IFLEN(I$)>17ORI$=""THEN1980
2000 TX$="{cyan}Enter a PASSWORD (7 chars or less)"+Q$+":{ct c}":GOSUB210
2010 GOSUB190:PS$=I$:IFLEN(I$)>9ORI$=""THEN2000
2020 TX$="{clear}{pink}Handle:"+NN$:GOSUB210:TX$="{yellow}Phone:"+PH$:GOSUB210:TX$="{white}Name:"+RN$
2030 GOSUB210:TX$="{cyan}Password:"+PS$:GOSUB210:TX$="{light blue}Is this correct?{ct c}":GOSUB210
2040 GOSUB100:IFIN$="n"THEN1940
2050 IFIN$<>"y"THEN2040
2051 E$="new:#"+STR$(ID)+NN$:GOSUB12
2060 AC$="0":LC$=STR$(MN)+"/"+STR$(DA):SYS49167:TM$=I$:CA$="0":FORI=0TO9
2070 P$(I)="0":NEXTI:TX$=NA$:GOSUB210:TX$=CHR$(13)+"Your ID is:"+STR$(ID):
2080 GOSUB210:TX$="Your PASSWORD is:"+PS$:GOSUB210:TX$="Saving info...":GOSUB210
2090 NA$=NN$:Y0=1:GOSUB1350:Y0=1:GOSUB1350:NA$="":TX$="Press RETURN{ct c}":GOSUB210
2100 GOSUB100:IFIN$<>CHR$(13)THEN2100
2110 FI$="{pound}newuser":NN=1:GOSUB1200
2115 GOSUB6700:GOTO840
2120 SYS49167:TX$=CHR$(13)+CHR$(13)+"{green}TIME={light green}"+I$+"{white}:(?):{blue}C{light blue}O{cyan}M{white}M{yellow}A{pink}N{red}D{white}:{ct c}"
2121 GOSUB210:IFPEEK(681)=1THENTX$="{cm w}{up}{cm e}{down}{cm q}{down}{left*2}{cm r}{left}{up}{ct c}":GOSUB210
2130 GOSUB100:IFIN$=""ORIN$=" "ORIN$=CHR$(13)THEN2130
2140 G=0:TX$=IN$:GOSUB210:IFIN$="?"THENFI$="{pound}mainmenu"+STR$(AC):NN=1:GOSUB1200
2150 FORI=1TOLEN(CD$):IFIN$=MID$(CD$,I,1)THENG=I:IFAC<VAL(MID$(CC$,I,1))THENG=0
2160 NEXTI:IFG=0THEN2120
2161 E$="cmd:"+MID$(CD$,G,1):GOSUB12
2170 ONGGOTO2200,2250,2190,2270,2340,2440,3740,3150,4050,5170
2175 G=G-10:ONGGOTO5740,6200,6250,6500
2180 GOTO2120
2190 FI$="{pound}info":NN=1:GOSUB1200:GOTO2120
2200 TX$="FEEDBACK?{ct c}":GOSUB210:GOSUB190:IFLEFT$(I$,1)<>"y"THEN2230
2210 FF$="{pound}feedback":QQ$="BY:"+NA$+" ID#"+STR$(NA):SYS49167
2220 QD$="ON:"+STR$(MN)+"/"+STR$(DA)+" AT:"+I$:V1=8:GOSUB2480
2230 IFPEEK(765)<>1THEN2120
2240 POKE765,0:GOTO1230
2250 TX$="OFF?{ct c}":GOSUB210:GOSUB190:IFLEFT$(I$,1)="y"THENPOKE765,1:GOTO2200
2260 GOTO2120
2270 TX$="Page the SYSOP?{ct c}":GOSUB210:GOSUB190:IFLEFT$(I$,1)<>"y"THEN2120
2280 TX$="Making noises...":GOSUB210:POKE53280,1:FORI=54272TO54296
2290 POKEI,0:NEXTI:POKE54296,15:POKE54277,8:POKE54278,255:POKE54276,21:I2=255
2300 FORI=1TO40:POKE54287,I2:OU$=">":GOSUB120:FORI1=255TO0STEP-10:POKE54273,I1
2310 I2=I2-.1:NEXTI1:NEXTI:POKE54278,15:GOTO2120
2320 IFPEEK(682)=0THEN2120
2330 POKE682,0:RETURN
2340 TX$="Start at:{ct c}":GOSUB210:GOSUB111:IFI=0THEN2320
2350 S1=I
2360 OPEN1,8,15:OPEN2,8,2,"{pound}userlog":
2370 GOSUB100:IFIN$=" "THENE1=5:GOTO2400
2380 I1=INT(S1/256):I3=I1*256:I2=S1-I3
2390 PRINT#1,"p"CHR$(98)CHR$(I2)CHR$(I1)CHR$(5):GET#2,A$:INPUT#1,E1
2400 IFE1>0ORA$=""THENCLOSE1:CLOSE2:GOTO2320
2410 SYS49185:I$=A$+I$:IFI$="EMPTY"THEN2430
2420 TX$=STR$(S1)+":"+I$:GOSUB210
2430 S1=S1+1:GOTO2370
2440 TX$=CHR$(13)+"{blue}--------------------":GOSUB210:TX$="Status: ":GOSUB210
2450 TX$="{cyan}Handle:"+NA$:GOSUB210:TX$="{white}Real Name:"+RN$:GOSUB210:SYS49167
2460 TX$="{yellow}Access:"+AC$:GOSUB210:TX$="{pink}Time:"+I$:GOSUB210:SYS49191
2470 TX$="{white}Logoff:"+I$:GOSUB210:TX$="{blue}--------------------":GOSUB210:GOTO2120
2480 REM MSG MKR
2520 IFPEEK(764)<>3THENFORI=0TO50:TT$(I)=CHR$(144):NEXTI:LN=0
2530 POKE764,0:A=FRE(0):POKE763,0:TX$="{clear}{reverse on}{pink}message{white} {light blue}maker{white}!{reverse off}":GOSUB210
2540 TX$="type *h for help"+CHR$(13)+"50 lines max. go: ":GOSUB210
2550 GOSUB190:IFI$=""THENI$=CHR$(144)
2560 TT$(LN)=I$:K$="":Q$=LEFT$(TT$(LN),2):IFLEFT$(Q$,1)="*"THENK$=RIGHT$(Q$,1)
2561 IFK$="u"ANDPEEK(681)=1ANDUP=0THENUP=1:OU$=CHR$(142):GOSUB120:GOTO2550
2562 IFK$="u"ANDUP=1THENUP=0:OU$=CHR$(14):GOSUB120:GOTO2550
2563 IFK$="w"ANDPEEK(705)=1THENPOKE705,0:TX$="w/w off":GOSUB210:GOTO2550
2564 IFK$="w"ANDPEEK(705)=0THENPOKE705,1:TX$="w/w on":GOSUB210:GOTO2550
2570 IFK$=""THEN2610
2580 G=0:FORI=1TOLEN(CE$):IFK$=MID$(CE$,I,1)THENG=I
2590 NEXTI:IFG=0THEN2610
2600 ONGGOTO2630,2640,3070,2970,2960,2900,2950,3040,2840
2610 IFLN>50THENTX$="50 lines MAX!":GOSUB210:GOTO2550
2620 LN=LN+1:GOTO2550
2630 FI$="{pound}msgmkr help":GOSUB1200:GOTO2550
2640 TX$="save..(y/n){ct c}":GOSUB210:
2650 GOSUB100:IFIN$=""THEN2650
2660 TX$=IN$:GOSUB210:IFIN$="n"THEN2550
2670 IFIN$<>"y"THEN2640
2675 IFS0=1THENS0=0:OPEN1,8,15,"s0:"+FF$:CLOSE1:GOTO2700
2680 TX$="saving...":GOSUB210:OPEN8,V1,8,FF$+",s,r":CLOSE8:OPEN1,V1,15
2690 INPUT#1,E1:CLOSE1:IFE1<20THEN2710
2700 OPEN8,V1,8,FF$+",s,w":GOTO2720
2710 OPEN8,V1,8,FF$+",a":PRINT#8,CHR$(1)
2720 IFQQ$<>""ANDQD$<>""THENPRINT#8,QQ$:PRINT#8,QD$:PRINT#8,CHR$(2)
2721 TT$(LN)=CHR$(144)
2730 I=0:IFUP=1THENPRINT#8,CHR$(142)+CHR$(147)
2740 PRINT#8,TT$(I):IFI<LN-1THENI=I+1:GOTO2740
2750 CLOSE8:TX$="saved!":GOSUB210
2755 OPEN1,8,15:INPUT#1,E,E$:CLOSE1:IFE>0THENGOSUB12
2759 GOTO2890
2760 X=1:TX$="change:{ct c}":GOSUB210:GOSUB190:I3=LEN(I$):I4=LEN(TT$(Q))
2770 IFI$=""THEN2550
2780 IFMID$(TT$(Q),X,I3)=I$THEN2810
2790 X=X+1:IFX<=I4THEN2780
2800 TX$="not found!":GOSUB210:GOTO2550
2810 I1=X-1:I2=I4-X-I3+1:I1$=LEFT$(TT$(Q),I1):I2$=RIGHT$(TT$(Q),I2)
2820 TX$="to:{ct c}":GOSUB210:GOSUB190:TT$(Q)=I1$+I$+I2$:IFTT$(Q)=""THENTT$(Q)="{black}"
2830 A=FRE(0):GOTO2550
2840 TX$="abort..(y/n){ct c}":GOSUB210
2850 GOSUB100:IFIN$=""THEN2850
2860 TX$=IN$:GOSUB210:IFIN$="n"THEN2550
2870 IFIN$<>"y"THEN2840
2880 POKE763,1
2890 POKE705,0:QQ$="":QD$=""
2891 S0=0:FORI=0TO50:TT$(I)="":NEXTI:A=FRE(0):OU$=CHR$(14):GOSUB120:RETURN
2900 TX$="clear..(y/n){ct c}":GOSUB210
2910 GOSUB100:IFIN$=""THEN2910
2920 TX$=IN$:GOSUB210:IFIN$="n"THEN2550
2930 IFIN$<>"y"THEN2900
2940 GOTO2520
2950 GOSUB6450:IFER=1THEN2550
2951 OPEN1,8,15
2952 TX$=TT$(Q)+CHR$(0):GOSUB210:GET#1,E$
2953 GOSUB100:IFIN$=" "THENCLOSE1:GOTO2550
2954 Q=Q+1:IFQ<=LN-1THEN2952
2956 CLOSE1:GOTO2550
2960 GOSUB6450:IFER=1THEN2550
2961 OPEN1,8,15
2962 TX$=STR$(Q)+"):"+TT$(Q)+CHR$(0):GOSUB210:GET#1,E$
2963 GOSUB100:IFIN$=" "THENCLOSE1:GOTO2550
2964 Q=Q+1:IFQ<=LN-1THEN2962
2965 CLOSE1:GOTO2550
2970 TX$="from (0-"+STR$(LN-1)+"){ct c}":GOSUB210:GOSUB111:S1=I:IFS1>LN-1THEN2550
2990 TX$="to ("+STR$(S1)+"-"+STR$(LN-1)+"){ct c}":GOSUB210:GOSUB111:
3010 S2=I:IFS2>LN-1ORS2<S1THEN2550
3020 S3=S2-S1+1:FORI=S1TOLN:TT$(I)=TT$(I+S3):NEXTI:LN=LN-S3:IFLN<0THENLN=0
3030 A=FRE(0):GOTO2550
3040 GOSUB6450:IFER=1THEN2550
3050 S1=Q:IFLN+1>50THEN2550
3060 FORI=LNTOS1+1STEP-1:TT$(I)=TT$(I-1):NEXTI:TT$(S1)="{black}":LN=LN+1:GOTO2550
3070 GOSUB6450:IFER=1THEN2550
3090 SYS49161,TT$(Q):TX$="e)dit or r)ewrite?{ct c}":GOSUB210
3100 GOSUB100:IFIN$=""THEN3100
3110 TX$=IN$:GOSUB210:IFIN$="e"THEN2760
3120 IFIN$<>"r"THEN2550
3130 TX$="rewrite: ":GOSUB210:GOSUB190:IFI$<>""THENTT$(Q)=I$:
3140 GOTO2550
3150 IFAC>6THENTX$=CHR$(13)+"{white}SysOp:{ct c}":GOSUB210:GOSUB190:IFI$="q"THEN2120
3160 IFI$="?"THENFI$="{pound}sysopmenu":NN=1:GOSUB1200:GOTO3150
3170 IFI$="d"THENDV=8:GOTO3230
3180 IFI$="l"THENPOKE682,1:GOSUB2340:POKE682,0
3190 IFI$="e"THEN3460
3200 IFI$="r"THEN5980
3210 IFI$="v"THEN6010
3220 GOTO3150
3230 TX$=CHR$(13)+STR$(DV)+":{ct c}":GOSUB210:GOSUB190:IFI$=""THEN3150
3240 Q$=LEFT$(I$,1):IFQ$="@"ANDLEN(I$)>2THENQ$=RIGHT$(I$,LEN(I$)-2):GOTO3320
3250 IFQ$="w"ANDLEN(I$)>2THENFF$=RIGHT$(I$,LEN(I$)-2):GOTO3370
3260 IFQ$="e"ANDLEN(I$)>2THENFF$=RIGHT$(I$,LEN(I$)-2):GOTO3400
3270 IFQ$="@"THENI$="":GOTO3300
3280 IFQ$="$"THENPOKE253,0:OPEN8,DV,0,I$:SYS49176:CLOSE8:GOTO3230
3285 IFQ$="%"THEN6480
3290 IFLEN(I$)<4ANDVAL(I$)>7ANDVAL(I$)<11THENDV=VAL(I$):GOTO3230
3300 OPEN1,DV,15,I$:INPUT#1,E1,E1$,E2,E3:CLOSE1
3310 TX$=STR$(E1)+","+E1$+","+STR$(E2)+","+STR$(E3):GOSUB210:GOTO3230
3320 OPEN8,DV,8,Q$+",s,r"
3330 SYS49164:IFPEEK(253)=0THENCLOSE8:GOTO3230
3340 TX$=CHR$(14)+"{white}Cont or Skip:{ct c}":GOSUB210:GOSUB190:IFI$="c"ORI$=""THEN3330
3350 IFI$="s"THENSYS49170:GOTO3340
3360 GOTO3340
3370 QQ$="":QD$="":S0=1:V1=DV:GOSUB2480:POKE764,0:GOTO3230
3400 TX$="Loading msg...":GOSUB210:OPEN8,DV,8,FF$+",s,r":GET#8,A$:IFST>0THEN3450
3410 QQ$="":CLOSE8:OPEN8,DV,8,FF$+",s,r":LN=0:FORI=0TO50:TT$(I)="{black}":NEXTI
3420 SYS49182:IFPEEK(253)=1THEN3440
3430 SYS49161,I$:TT$(LN)=I$:LN=LN+1:GOTO3420
3440 QD$="":CLOSE8:SYS49161,I$:A=FRE(0):TT$(LN)=I$:LN=LN+1:POKE764,3:GOTO3370
3450 CLOSE8:GOTO3230
3460 TX$="User #{ct c}":GOSUB210:GOSUB111:IFI=0THEN3150
3470 C2=INT(I/256):C3=C2*256:C1=I-C3:OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
3480 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(5):GET#2,A$:INPUT#1,E1
3490 IFA$=""ORE1>0THENCLOSE1:CLOSE2:TX$="no record":GOSUB210:GOTO3150
3500 GOSUB3550
3510 TX$="Handle:"+TT$(0)+":{ct c}":GOSUB210:GOSUB190:IFLEN(I$)>17THEN3510
3520 IFI$<>""THENTT$(0)=I$
3530 GOTO3590
3540 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I):SYS49185:RETURN
3550 I=5:GOSUB3540:TT$(0)=I$:I=25:GOSUB3540:TT$(1)=I$:I=35:GOSUB3540
3560 TT$(2)=I$:I=40:GOSUB3540:TT$(3)=I$:I=60:GOSUB3540:TT$(4)=I$
3570 GOSUB3540:TT$(5)=I$:I=90:GOSUB3540:TT$(6)=I$
3580 CT=7:FORI=100TO145STEP5:GOSUB3540:TT$(CT)=I$:CT=CT+1:NEXTI:RETURN
3590 TX$="Fone #:"+TT$(1)+":{ct c}":GOSUB210:GOSUB190:IFLEN(I$)>8THEN3590
3600 IFI$<>""THENTT$(1)=I$
3610 TX$="Access:"+TT$(2)+":{ct c}":GOSUB210:GOSUB190:IFLEN(I$)>1THEN3610
3620 IFI$<>""THENTT$(2)=I$
3630 TX$="Name:"+TT$(3)+":{ct c}":GOSUB210:GOSUB190:IFLEN(I$)>17THEN3630
3640 IFI$<>""THENTT$(3)=I$
3643 TX$="Pass:"+TT$(6)+":{ct c}":GOSUB210:GOSUB190:IFLEN(I$)>7THEN3643
3646 IFI$<>""THENTT$(6)=I$
3647 FORI=7TO11:TX$="Var"+STR$(I-7)+":"+TT$(I)+":{ct c}":GOSUB210:GOSUB190
3648 IFLEN(I$)>4THEN3647
3649 IFI$<>""THENTT$(I)=I$
3650 NEXTI
3651 TX$="S)ave E)dit or A)bort:{ct c}":GOSUB210:GOSUB190:IFI$="e"THEN3510
3660 IFI$="a"THENCLOSE1:CLOSE2:GOTO3150
3670 IFI$<>"s"THEN3651
3680 I=5:GOSUB3730:PRINT#2,TT$(0):I=25:GOSUB3730:PRINT#2,TT$(1):I=35:GOSUB3730
3690 PRINT#2,TT$(2):I=40:GOSUB3730:PRINT#2,TT$(3):I=60:GOSUB3730:PRINT#2,TT$(4)
3700 I=80:GOSUB3730:PRINT#2,TT$(5):I=90:GOSUB3730:PRINT#2,TT$(6)
3710 CT=7:FORI=100TO145STEP5:GOSUB3730:PRINT#2,TT$(CT):CT=CT+1:NEXTI
3720 CLOSE1:CLOSE2:GOTO3150
3730 PRINT#1,"p"CHR$(98)CHR$(C1)CHR$(C2)CHR$(I):RETURN
3740 TX$="{white}Email:{ct c}":Q$=CHR$(13):GOSUB210
3750 GOSUB100:IFIN$="q"THEN2120
3760 IFIN$="?"THENFI$="{pound}emailmenu":NN=1:GOSUB1200:GOTO3740
3770 IFIN$="r"THENWH=0:GOTO3890
3780 IFIN$="u"THENPOKE682,1:GOSUB2340:POKE682,0:GOTO3740
3800 IFIN$<>"s"THEN3750
3810 TX$=Q$+"User ID#{ct c}":GOSUB210:GOSUB111:I7=I:IFI=0THEN3740
3830 I2=I7/256:I3=I2*256:I4=I7-I3:OPEN1,8,15:OPEN2,8,2,"{pound}userlog"
3840 PRINT#1,"p"CHR$(98)CHR$(I4)CHR$(I2)CHR$(5):GET#2,A$:INPUT#1,E1:CLOSE1
3850 CLOSE2:IFA$=""ORE1>0THENCLOSE1:CLOSE2:TX$="Not found!":GOSUB210:GOTO3740
3860 QQ$="FROM:"+NA$+" ID#"+STR$(NA):SYS49167:QD$="ON:"+STR$(MN)+"/"+STR$(DA)
3870 QD$=QD$+" AT:"+I$:V1=MD:FF$="{pound}mail"+STR$(I7):GOSUB2480:IFMF=0THENGOTO3740
3880 MF=0:GOTO3890
3890 TX$="Mail...":GOSUB210:OPEN8,MD,8,"{pound}mail"+STR$(NA)+",s,r":GET#8,A$
3900 IFST>0ANDWH=0THENTX$="No Mail Found!":GOSUB210:CLOSE8:GOTO3740
3910 B=0:IFWH=0THEN3940
3920 SYS49170:GET#8,E$:IFASC(E$)=199THEN4030
3930 B=B+1:IFB<WHTHEN3920
3940 GET#8,A$:IFA$<>"#"ANDST=0THEN3940
3950 SYS49182:I7=VAL(I$):CLOSE8:OPEN8,MD,8,"{pound}mail"+STR$(NA)
3960 IFWH>0THENFORI=1TOWH:SYS49170:NEXTI
3970 SYS49164:
3980 CLOSE8:TX$=CHR$(14)+"R)eply A)gain N)ext:{ct c}":GOSUB210
3990 GOSUB100:IFIN$="a"THENTX$="a":GOSUB210:OPEN8,MD,8,"{pound}mail"+STR$(NA):GOTO3960
4000 IFIN$="n"THENWH=WH+1:TX$="n":GOSUB210:GOTO3890
4010 IFIN$<>"r"THEN3990
4020 TX$="r":GOSUB210:MF=1:GOTO3830
4030 CLOSE8:OPEN1,MD,15,"s0:{pound}mail"+STR$(NA):CLOSE1:TX$="Mail Deleted!":GOSUB210
4040 GOTO3740
4050 CS=0:TX$="{clear}{light blue}Sig System!{white}":GOSUB210:GOSUB4060:GOTO4260
4060 TX$="{cyan}{down}Entering Sig"+STR$(CS+1):GOSUB210:VD=SD(CS):CM=0:I1=0:I2=0
4070 FI$="{pound}sigmsg"+STR$(CS):GOSUB1200:OPEN1,8,15:OPEN2,8,2,"{pound}Sig"+STR$(CS)
4075 FORI=1TONM:Z1(I)=0:Z2(I)=0:NEXTI:GOTO 4090
4080 PRINT#1,"p"CHR$(98)CHR$(J)CHR$(0)CHR$(I):SYS49185:RETURN
4090 J=1:I=1:GOSUB4080:NM=VAL(I$):I=7:GOSUB4080:MP=VAL(I$):FORJ=2TONM+1
4100 I=1:GOSUB4080:Z1(J-1)=VAL(I$):I=7:GOSUB4080:Z2(J-1)=VAL(I$)
4110 I1=I1+Z1(J-1):IFZ2(J-1)>PPTHENI2=I2+1:
4120 NEXTJ:CLOSE2:CLOSE1
4130 TX$=CHR$(13)+STR$(NM)+" {cyan}{reverse on}Bulletins and "+STR$(I1)+" Responses":GOSUB210
4140 TX$=STR$(I2)+" {light blue}{reverse on}new Bulletins/Responses":GOSUB210:RETURN
4150 OPEN1,8,15:OPEN2,8,2,"{pound}Sig"+STR$(CS):GOTO4170
4160 PRINT#1,"p"CHR$(98)CHR$(I)CHR$(0)CHR$(J):RETURN
4170 I=1:J=1:GOSUB4160:PRINT#2,NM:J=7:GOSUB4160:PRINT#2,MP:CLOSE2:CLOSE1:RETURN
4180 OPEN1,8,15:OPEN2,8,2,"{pound}Sig"+STR$(CS):I=I+1:J=1:GOSUB4160:PRINT#2,A:J=7
4190 GOSUB4160:PRINT#2,B:CLOSE2:CLOSE1:RETURN
4220 MP=1:GOTO4910
4260 IFWI=1THENCM=CM+1:GOTO4800
4265 IFKD=1THEN6620
4270 TX$=CHR$(14)+CHR$(13)+"{yellow}{reverse on}Sig{reverse off}"+STR$(CS+1)+"{white}:{ct c}":GOSUB210:G=0
4280 GOSUB100:IFIN$=""ORIN$=" "THEN4280
4285 IFIN$=CHR$(13)THEN6650
4290 TX$=IN$:GOSUB210:IFIN$="?"THENFI$="{pound}msgmenu":NN=1:GOSUB1200:GOTO4270
4300 FORI=1TOLEN(CF$):IFIN$=MID$(CF$,I,1)THENG=I:IFAC<VAL(MID$(CG$,I,1))THENG=0
4305 NEXTI:IFG>0THENE$="msg:"+MID$(CF$,G,1):GOSUB12
4310 IFG>0THENONGGOTO4330,4360,4400,4720,4790,2120,4840,4940,5120,6600
4320 GOTO4260
4330 TX$="Sigs: ":GOSUB210:FORI=0TOSG:IFSG(I)>ACTHEN4350
4340 TX$=STR$(I+1)+"):"+SG$(I):GOSUB210
4350 NEXTI:GOTO4260
4360 TX$="{white}New Sig:{ct c}":GOSUB210:GOSUB111:IFI=0THEN4260
4370 Q=I-1:IFQ=CSOR(Q>SG)THEN4260
4380 IFAC>=SG(Q)THENCS=Q:GOSUB4060
4390 GOTO4260
4400 TX$="MSG # or RETURN:{ct c}":GOSUB210:GOSUB111:IFI=0THEN4260
4410 CM=I
4420 IFCM>NMORCM<1THENCM=0:WI=0:GOTO4260
4430 I1=Z1(CM):I2=Z2(CM):OPEN8,SD(CS),8,"{pound}msg"+STR$(CS)+STR$(CM):SYS49164
4440 CLOSE8:IFPEEK(252)=1THENTX$=CHR$(13)+"hit return{ct c}":GOSUB210:GOSUB6370
4449 IFI1=0THEN4590
4450 I9=0:TX$=CHR$(14)+"{pink}R)ead, {light gray}S)kip, {white}Q)uit, or {light green}RETURN:{ct c}":GOSUB210
4460 GOSUB100:IFIN$=""ORIN$=" "THEN4460
4470 TX$=IN$:GOSUB210:IFIN$="q"THEN4590
4480 IFIN$="s"THEN4690
4490 IFIN$<>"r"ANDIN$<>CHR$(13)THEN4450
4500 OPEN8,SD(CS),8,"{pound}msg"+STR$(CS)+STR$(CM):SYS49170
4510 IFI9=0THEN4530
4520 FORI=1TOI9:SYS49170:NEXTI
4525 IFI9>=I1THENCLOSE8:GOTO4590
4530 TX$="{clear}{white}Response #"+STR$(I9+1):GOSUB210:SYS49164
4531 IFPEEK(252)=1THENTX$=CHR$(13)+"hit return{ct c}":GOSUB210:GOSUB6370
4540 TX$=CHR$(14)+"{cyan}RETURN continues or A)bort:{ct c}":GOSUB210
4550 GOSUB100:IFIN$=""ORIN$=" "THEN4550
4560 TX$=IN$:GOSUB210:IFIN$="a"THENCLOSE8:GOTO4260
4570 IFIN$<>CHR$(13)THEN4550
4580 I9=I9+1:GOTO4525
4590 IFAC<VAL(MID$(CG$,G,1))THEN4260
4600 TX$=CHR$(14)+CHR$(13)+"{light green}{yellow}{reverse on}Public Reply?{reverse off}{white}:{ct c}":GOSUB210
4610 GOSUB100:IFIN$="n"ORIN$=CHR$(13)THENTX$="n":GOSUB210:GOTO4260
4620 IFIN$="q"THENWI=0:TX$="q":GOSUB210:GOTO4260
4630 IFIN$<>"y"THEN4610
4635 TX$="y":GOSUB210
4640 QQ$="REPLY BY:"+NA$+" ID#"+STR$(NA):SYS49167:QD$="DATE:"+STR$(MN)+"/"
4650 QD$=QD$+STR$(DA)+" TIME:"+I$+CHR$(13):FF$="{pound}msg"+STR$(CS)+STR$(CM)
4660 V1=SD(CS):OU$=CHR$(13):GOSUB120:GOSUB2480:IFPEEK(763)=1THEN4260
4670 TX$="Fixing...":GOSUB210:Z1(CM)=Z1(CM)+1:Z2(CM)=HN+1:GOSUB4150
4680 I=CM:HN=HN+1:A=Z1(CM):B=Z2(CM):GOSUB4180:GOTO4260
4690 TX$="Num to Skip:{ct c}":GOSUB210:GOSUB111:IFI=0THEN4450
4700 I9=I:IFI9>Z1(CM)THEN4590
4710 GOTO4500
4720 I=1:IFI>NMTHEN4260
4730 OPEN8,SD(CS),8,"{pound}msg"+STR$(CS)+STR$(I)::SYS49179:I$=RIGHT$(I$,LEN(I$)-2)
4740 I$=" P"+I$:CLOSE8:GOSUB100:IFIN$=" "THEN4260
4750 TX$="Msg #"+STR$(I)+":"+STR$(Z1(I))+" Responses":GOSUB210:SYS49161,I$
4760 GOSUB100:IFIN$=" "THEN4260
4770 I=I+1:IFI>NMORI<1THEN4260
4780 GOTO4730
4790 CM=1:WI=0:TX$="Search...":GOSUB210
4800 IFCM>NMTHENTX$=CHR$(13)+"Search Complete.":GOSUB210:WI=0:GOTO4260
4805 G=3:IFZ2(CM)>PPTHENWI=1:GOTO4420
4810 CM=CM+1:GOTO4800
4840 V1=SD(CS):TX$="{white}Post a Message?:{ct c}":GOSUB210
4850 GOSUB100:IFIN$="n"THENTX$="n":GOSUB210:GOTO4260
4860 IFIN$<>"y"THEN4850
4865 TX$="y":GOSUB210
4870 QQ$="{clear}POSTED BY:"+NA$+" ID#"+STR$(NA)+CHR$(13)+"ON:"+STR$(MN)+"/"+STR$(DA)
4880 TX$=CHR$(13)+"Subject:{ct c}":GOSUB210:GOSUB190:S$=I$:IFI$=""THEN4260
4890 SYS49167:QD$="TIME:"+I$+" SUBJECT:"+S$+CHR$(13):IFSF(CS)>NMTHENNM=NM+1
4900 S0=1:MP=MP+1:IFMP>=SF(CS)THEN4220
4910 FF$="{pound}msg"+STR$(CS)+STR$(MP):GOSUB2480:IFPEEK(763)=1THENNM=NM-1:GOTO4260
4920 TX$="Fixing...":GOSUB210:GOSUB4150:I=MP:Z1(MP)=0:Z2(MP)=HN+1:HN=HN+1
4930 A=Z1(MP):B=Z2(MP):GOSUB4180:GOTO4260
4940 V1=SD(CS):TX$="Msg #{ct c}":GOSUB210:GOSUB111:IFI=0THEN4260
4950 M=I:T=Z1(M):IFM>NMORT=0THEN4260
4960 OPEN1,V1,15,"s0:msg":PRINT#1,"r0:msg={pound}msg"+STR$(CS)+STR$(M):CLOSE1:CT=1
4970 TX$="Storing Bulletin...":GOSUB210:OPEN8,V1,8,"msg,s,r"
4980 OPEN9,V1,9,"{pound}msg"+STR$(CS)+STR$(M)+",s,w"
4990 SYS49182:IFI$<>CHR$(1)THENPRINT#9,I$:GOTO4990
5000 PRINT#9,CHR$(1):MM=Z1(M)
5010 TX$="Save response #"+STR$(CT)+"?{ct c}":GOSUB210:GOSUB190:IFI$=""THEN5010
5020 IFI$="n"THEN5090
5030 IFI$<>"y"THEN5010
5040 SYS49182:IFI$=CHR$(1)THENPRINT#9,CHR$(1):GOTO5070
5050 IFPEEK(253)=1THENA=FRE(0):GOTO5070
5060 PRINT#9,I$:GOTO5040
5070 IFCT=MMTHEN5110
5080 CT=CT+1:GOTO5010
5090 SYS49170:Z1(M)=Z1(M)-1:IFZ1(M)=0ORCT=MMTHEN5110
5100 GOTO5080
5110 CLOSE8:CLOSE9:OPEN1,8,15,"s0:msg":CLOSE1:A=Z1(M):B=Z2(M):I=M:GOSUB4180
5111 GOTO4260
5120 V1=SD(CS):TX$="Delete Msg #{ct c}":GOSUB210:GOSUB111:IFI=0ORI>NMTHEN4260
5140 TX$="Standby...":GOSUB210:OPEN1,V1,15,"s0:{pound}msg"+STR$(CS)+STR$(I)
5150 FORX=ITONM:PRINT#1,"r0:{pound}msg"+STR$(CS)+STR$(X)+"={pound}msg"+STR$(CS)+STR$(X+1)
5160 Z1(X)=Z1(X+1):Z2(X)=Z2(X+1):NEXTX:CLOSE1:IFMP>=ITHENMP=MP-1
5161 Q=I:FORG=QTONM:A=Z1(G):B=Z2(G):I=G:GOSUB4180:NEXTG:NM=NM-1:GOSUB4150
5162 GOTO4260
5170 TX$="{pink}U/{yellow}D{white}"+STR$(CU+1)+":{ct c}":GOSUB210
5180 GOSUB100:IFIN$=""THEN5180
5190 TX$=IN$:GOSUB210:IFIN$="?"THENFI$="{pound}udmenu":NN=1:GOSUB1200:GOTO5170
5200 IFIN$="$"THEN5250
5210 IFIN$="q"THEN2120
5220 IFIN$="u"THEN5270
5230 IFIN$="d"THEN5460
5231 IFIN$="l"THEN6300
5232 IFIN$="c"THEN6330
5233 IFIN$="a"THENGOSUB6430
5240 GOTO5170
5250 TX$="{clear}Directory...":GOSUB210
5260 POKE253,64+CU:OPEN8,UV(CU),0,"$:"+CHR$(64+CU)+"*":SYS49176:CLOSE8:GOTO5170
5270 POKE512,0:TX$="{white}UPLOAD!":GOSUB210:GOSUB6391:IFER=1THEN5170
5310 FI$=I$:IFLEFT$(I$,1)<>"{pound}"THENFI$=CHR$(64+CU)+I$
5320 GOSUB5640:IFER=1THEN5170
5325 GOSUB6400
5330 TX$="{clear}{white}Loading Punter...":GOSUB210:FI$=FI$+"w":I$="{pound}p.protocol":SYS51971,I$
5340 IFPEEK(681)=1THENPRINT#5,"O.K...GO TO{sh space}SEND!"
5350 IFPEEK(681)=0THENPRINT#5,"o.k...go to send!"
5360 TI$="000000"
5370 GETA$:IFA$="a"THEN5620
5380 IFPEEK(830)=1THEN1230
5390 IFVAL(TI$)>40THEN5620
5400 GET#5,A$:IFA$="a"ORA$="A"THEN5620
5410 IFA$<>"g"ANDA$<>"o"ANDA$<>"k"THEN5370
5420 SYS49173:SYS49161:IFPEEK(512)=1THEN5620
5421 E$="ul:"+FI$:GOSUB12:P$(3)=STR$(VAL(P$(3))+1)
5430 OPEN1,UV(CU),15:OPEN2,UV(CU),2,FI$:INPUT#1,E:IFE>0THEN5620
5440 FORI=1TO1300:NEXTI:SYS61310:SYS49173:SYS49155
5450 FORX=1TO1000:NEXTX:POKE668,PEEK(667):YU=1:GOTO5620
5460 POKE512,0:TX$="{white}DOWNLOAD!":GOSUB210:GOSUB6391:IFER=1THEN5170
5490 FI$=I$:GOSUB5640:IFII$<>"{pound}"THENFI$=CHR$(64+CU)+FI$
5500 FI$=FI$+"r":OPEN1,UV(CU),15:OPEN2,UV(CU),2,FI$:INPUT#1,E
5510 IFE>1THENCLOSE1:CLOSE2:GOTO5170
5520 TX$="{clear}{white}Loading Punter...":GOSUB210:I$="{pound}p.protocol":SYS51971,I$
5530 IFPEEK(681)=1THENPRINT#5,"O.K....GO TO RECIEVE!"
5540 IFPEEK(681)=0THENPRINT#5,"o.k....go to receive!"
5550 TI$="000000"
5560 GETA$:IFA$="a"THEN5620
5570 IFPEEK(830)=1THEN1230
5575 IFVAL(TI$)>40THEN5620
5580 GET#5,A$:IFA$="a"ORA$="A"THEN5620
5590 IFA$<>"g"ANDA$<>"o"ANDA$<>"k"THEN5560
5600 POKE51227,TY:SYS49173:SYS49164:IFPEEK(512)=1THEN5620
5601 E$="dl:"+FI$:GOSUB12:P$(4)=STR$(VAL(P$(4))+1)
5610 SYS49173:POKE51224,255:SYS49158
5620 CLOSE1:CLOSE2:PRINT#5,"loading system ml":I$="{pound}v2.2 ml1":SYS51971,I$
5630 IFYU=0THEN5170
5631 OPEN8,UV(CU),0,"$:z=u":SYS49197:CLOSE8:IFPEEK(707)>20ORPEEK(708)>0THEN5170
5632 OPEN1,UV(CU),15,"s0:"+LEFT$(FI$,LEN(FI$)-4):CLOSE1:GOTO5170
5640 ER=0:TX$="{pink}P)RG{white} or {yellow}S)EQ{white}:{ct c}":GOSUB210
5650 GOSUB100:IFIN$=""THEN5650
5655 IFIN$=CHR$(13)THENER=1
5660 TX$=IN$:GOSUB210:IFIN$="p"THENFI$=FI$+",p,":TY=1:RETURN
5670 IFIN$="s"THENFI$=FI$+",s,":TY=2:RETURN
5680 GOTO5640
5690 PRINT"{clear}":OPEN8,8,8,FI$+",s,r"
5700 SYS49164:IFPEEK(253)=0THENCLOSE8:GOTO390
5710 PRINT"Hit RETURN:"
5720 GETA$:IFA$<>CHR$(13)THEN5720
5730 GOTO5700
5740 TX$="{pink}Loading Vote!":GOSUB210:OPEN1,8,15:OPEN8,8,8,"{pound}vote,s,r":INPUT#1,E1
5750 I1=0:I2=0:I1$="":IFE1>0THENCLOSE1:CLOSE8:GOTO2120
5760 INPUT#8,I1$:INPUT#8,I2:FORI=0TOI2:INPUT#8,I1$(I):INPUT#8,I2$(I):I1(I)=0
5770 NEXTI:CLOSE8:TX$="{yellow}Loading Results!":GOSUB210:OPEN8,8,8,"{pound}vote box,s,r"
5780 INPUT#1,E1:IFE1>0THENCLOSE8:CLOSE1:GOTO2120
5790 INPUT#8,A$:IFST AND 64THENCLOSE1:CLOSE8:GOTO5830
5800 INPUT#8,I:IFNA=ITHENI1=1
5810 INPUT#8,I:I1(I)=I1(I)+1:IFST AND64THENCLOSE1:CLOSE8:GOTO5830
5820 GOTO5800
5830 IFI1=1THEN5930
5840 Q$=CHR$(13)+"{light blue}--------------------------------------"
5845 TX$="{clear}{down}{white}Title:"+I1$:GOSUB210:TX$=Q$:GOSUB210
5850 FI$="{pound}question":GOSUB1200:TX$=Q$:GOSUB210
5860 FORI=0TOI2:TX$=STR$(I+1)+")"+I1$(I):GOSUB210:NEXT
5870 TX$=Q$:GOSUB210:TX$="Your Choice:{ct c}":GOSUB210
5880 GOSUB100:IFIN$=""THEN5880
5890 I=VAL(IN$):IFI=0THEN5930
5900 IFI>I2+1THEN5880
5910 I1(I-1)=I1(I-1)+1:TX$="Saving your vote...":GOSUB210:E$="vote":GOSUB12
5920 OPEN8,8,8,"{pound}vote box,a":PRINT#8,NA:PRINT#8,I-1:CLOSE8
5930 TX$="{light blue}Results!":GOSUB210:O=0:FORI=0TOI2:O=O+I1(I):NEXT
5940 TX$="{light green}Out of"+STR$(O)+" votes...":GOSUB210:FORI=0TOI2
5945 TX$=STR$(I1(I))+"-"+I2$(I)+" ("+STR$(INT((I1(I)/O)*100))+"% )":GOSUB210
5950 NEXTI:TX$="Hit RETURN{ct c}":GOSUB210
5960 GOSUB100:IFIN$<>CHR$(13)THEN5960
5970 GOTO2120
5980 TX$="Reset vote box?{ct c}":GOSUB210:GOSUB190:IFI$<>"y"THEN3150
5990 OPEN1,8,15,"s0:{pound}vote box":CLOSE1:OPEN8,8,8,"{pound}vote box,s,w":PRINT#8,"start"
6000 CLOSE8:GOTO3150
6010 TX$="{white}Loading Vote!":GOSUB210:OPEN8,8,8,"{pound}vote,s,r":INPUT#8,I1$
6020 INPUT#8,I2:FORI=0TOI2:INPUT#8,I1$(I):INPUT#8,I2$(I):NEXTI:CLOSE8
6030 TX$="{clear}{pink}A)Title:"+I1$+"{yellow}":GOSUB210:TX$="B)Response #:"+STR$(I2+1):GOSUB210
6040 TX$="{cyan}Option{white}/{light blue}Result":GOSUB210:FORI=0TOI2
6050 TX$=STR$(I+1)+"){cyan}"+I1$(I)+"{white}/{light blue}"+I2$(I):GOSUB210:NEXTI
6060 TX$="{light green}(Q/S)/Option:":GOSUB210
6070 GOSUB100:IFIN$=""THEN6070
6080 TX$=IN$:GOSUB210:IFVAL(IN$)>0ANDVAL(IN$)<=I2+1THEN6160
6090 IFIN$="a"THENTX$=":{ct c}":GOSUB210:GOSUB190:I1$=I$:GOTO6030
6100 IFIN$="q"THEN3150
6110 IFIN$="s"THEN6180
6120 IFIN$<>"b"THEN6030
6130 TX$=":":GOSUB210:GOSUB190:IFI$=""ORLEN(I$)>4THEN6030
6140 I=VAL(I$)-1:IFI<=9ANDI>0THENI2=I:GOTO6030
6150 GOTO6030
6160 I=VAL(IN$)-1:TX$="Option:{ct c}":GOSUB210:GOSUB190:I1$(I)=I$
6170 TX$="Result:{ct c}":GOSUB210:GOSUB190:I2$(I)=I$:GOTO6030
6180 OPEN1,8,15,"s0:{pound}vote":CLOSE1:OPEN8,8,8,"{pound}vote,s,w":PRINT#8,I1$:PRINT#8,I2
6190 FORI=0TOI2:PRINT#8,I1$(I):PRINT#8,I2$(I):NEXTI:CLOSE8:GOTO3150
6200 IFPEEK(659)=8THENTX$="1200 baud cannot change":GOSUB210:GOTO2120
6201 TX$="Baud is:"+STR$(BA):GOSUB210:TX$="New Baud (RETURN):{ct c}":GOSUB210
6210 GOSUB111:IFI=0THEN2120
6220 IFI<200ORI>500THEN2120
6225 TX$="Change baud and hit RETURN{ct c}":GOSUB210:POKE659,6
6230 BA=I:A=1022730/BA/2:GOSUB6249:POKE51196,I1:POKE51197,I2:A=1022730/BA
6240 GOSUB6249:POKE665,I1:POKE666,I2:A=1022730/BA/2-100:GOSUB6249
6241 POKE661,I1:POKE662,I2
6244 IFFH=1THENFH=0:RETURN
6245 GOSUB100:IFIN$<>CHR$(13)THEN6245
6248 GOTO2120
6249 I2=INT(A/256):I3=I2*256:I1=A-I3:RETURN
6250 TX$=CHR$(14)+"{light blue}File #{white}:{cyan}Q=QUIT{white}:{ct c}":GOSUB210:GOSUB190:IFI$=""THEN6250
6251 IFI$="q"ORLEN(I$)>4THEN2120
6252 IFI$="?"THENA=0:GOTO6260
6253 A=VAL(I$):IFA<1THEN6250
6260 FI$="{pound}txt"+STR$(A):NN=1:VD=UL:GOSUB1200:IFPEEK(252)=0THEN6250
6270 TX$="hit return{ct c}":GOSUB210:GOSUB6370:GOTO6250
6300 TX$="UD Sections:"
6301 FORI=0TOUD:IFAC>=UA(I)THENTX$="{white}"+STR$(I+1)+"):"+UD$(I):GOSUB210
6310 NEXTI:GOTO5170
6330 TX$="{cyan}New UD{white}:{ct c}":GOSUB210:GOSUB111:IFI=0ORI>UD+1THEN5170
6345 IFAC<UA(I-1)THEN5170
6350 CU=I-1:GOTO5170
6370 GOSUB100:IFIN$<>CHR$(13)THEN6370
6390 TX$=" ":GOSUB210:RETURN
6391 ER=0:TX$="{white}Filename:{ct c}":GOSUB210:GOSUB190:IFI$=""ORLEN(I$)>15THENER=1
6392 FORL=1TOLEN(I$):L$=MID$(I$,L,1):IFL$="*"ORL$=","ORL$="?"ORL$="$"THENER=1
6394 IFL$=":"ORL$=";"ORL$="#"ORL$="{ct c}"THENER=1
6395 NEXTL:RETURN
6400 TX$="{white}Now enter a description of the file":GOSUB210:TX$="Hit Return"
6410 GOSUB210:GOSUB6370:FF$="^"+MID$(FI$,2,LEN(FI$)-4):S0=1:V1=UV(CU):GOSUB2480
6420 RETURN
6430 GOSUB6391:IFER=0THENFI$="^"+I$:NN=1:VD=UV(CU):GOSUB1200
6440 IFPEEK(253)=0THENTX$="None Found!":GOSUB210:
6445 RETURN
6450 Q=0:ER=0:E$=RIGHT$(TT$(LN),LEN(TT$(LN))-2):IFLEN(E$)>4THENER=1:GOTO6470
6460 Q=VAL(E$):IFQ>LN-1ORQ<0THENTX$="bad #":GOSUB210:ER=1
6470 RETURN
6480 I$=RIGHT$(I$,LEN(I$)-1):OPEN8,DV,0,"$"+I$:SYS49194:CLOSE8
6490 TX$=STR$(PEEK(706))+" files counted":GOSUB210:GOTO3230
6500 TX$=CHR$(13)+"{pink}H)i Msg:"+STR$(PP):GOSUB210:TX$="{yellow}P)assword:"+PS$:GOSUB210
6510 TX$="{white}B)aud:"+P$(2):GOSUB210:TX$="{light green}Choice:{ct c}":GOSUB210:GOSUB190
6511 IFI$="h"THEN6520
6512 IFI$="p"THEN6540
6513 IFI$="b"THEN6560
6514 IFI$=""THEN2120
6515 GOTO6500
6520 TX$="{pink}New Number (1-"+STR$(HN)+"):{ct c}":GOSUB210:GOSUB111:IFI<1ORI>HNTHEN6599
6530 PP=I:GOTO6500
6540 TX$="{yellow}New Pass:{white}{ct c}":GOSUB210:POKE710,1:GOSUB190:O$=I$:IFI$=""THEN6599
6541 TX$=CHR$(13)+"{yellow}Verify:{white}{ct c}":GOSUB210:POKE710,1:GOSUB190:IFI$<>O$THEN6599
6542 PS$=I$:TX$=" ":GOSUB210:GOTO6500
6560 TX$="{white}New Baud:{ct c}":GOSUB210:GOSUB111:IFI<200ORI>500THEN6599
6570 P$(2)=RIGHT$(STR$(I),LEN(STR$(I))-1):GOTO6500
6599 TX$=CHR$(13)+"Entry unchanged":GOSUB210:GOTO6500
6600 TX$="{white}Start...":GOSUB210:KD=0
6610 GOSUB4060:IFI2>0THENG=3:KD=1:GOTO4790
6620 CS=CS+1:IFCS>SGTHENTX$="Done.":GOSUB210:CS=CS-1:KD=0:GOTO4260
6625 IFSG(CS)>ACTHENTX$="Done.":GOSUB210:CS=CS-1:KD=0:GOTO4260
6630 GOTO6610
6650 TX$=" ":GOSUB210:CM=CM+1:G=3:GOTO4420
6700 OPEN8,8,8,"{pound}newqs,s,r":GET#8,B$:I=0
6701 SYS49182:NQ$(I)=I$:GET#8,B$:IFST>0THEN6710
6702 IFI<9THENI=I+1:GOTO6701
6710 CLOSE8:FORX=0TOI:TX$=NQ$(X)+"{ct c}":GOSUB210:GOSUB190:NA$(X)=I$:NEXTX
6720 TX$="Is this all correct?{ct c}":GOSUB210
6730 GOSUB100:IFIN$="n"THENTX$="n":GOSUB210:GOTO6710
6735 IFIN$<>"y"THEN6730
6740 TX$="y":GOSUB210:OPEN8,8,8,"{pound}newinfo,s,r":GET#8,I$:IFST=0THEN6750
6745 CLOSE8:OPEN8,8,8,"{pound}newinfo,s,w":PRINT#8,"start"
6750 CLOSE8:OPEN8,8,8,"{pound}newinfo,a":PRINT#8,CHR$(1):PRINT#8,"#"+STR$(ID)+" "+NN$
6760 FORX=0TOI:PRINT#8,NQ$(X)+NA$(X):NEXTX:CLOSE8:RETURN
6800 POKE659,6:I=300:FH=1:GOSUB6230:RETURN
6810 INPUT"dial";A$:INPUT"change to 300 baud?";B$
6815 IFB$="y"THENGOSUB6800
6820 PRINT#5,"atdt"+A$:SYS49188:POKE702,16:RUN
