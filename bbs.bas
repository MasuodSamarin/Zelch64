!--------------------------------------------------
!- Friday, May 12, 2017 11:03:58 PM
!- Import of : 
!- c:\src\zelch64\bak\bbs.prg
!- Commodore 64
!--------------------------------------------------
0 POKE650,128:CLR:CLOSE5:OPEN5,2,0,CHR$(6):SYS51977:I$="0":A$="l":GOTO100
1 IFM0=0THENPRINT#5,"at"+A$:GOSUB45:GET#5,A$,A$,A$,A$,A$,A$
2 POKEMC+2,32:POKEMC,0:RETURN
3 PRINT#8,"p"+CHR$(98)+CHR$(U)+CHR$(V)+CHR$(I):RETURN
4 GOSUB3:SYS38233:RETURN
5 IFTX$=""THENRETURN
6 SYS38209,TX$:GOTO8
7 SYS38206:IFPEEK(830)=1THEN1500
8 POKE710,0:POKE731,0:RETURN
9 IFP0<101THENP0$(P0)=E$:P0=P0+1:IFPEEK(709)THENPRINT#15,E$
10 RETURN
11 SYS38200:IFPEEK(830)=1THEN1500
12 VY=PEEK(197):VZ=PEEK(653):IFVY>6ORVY<3THENRETURN
13 ON-(VY=5ANDVZ=1)-((VY=6ANDVZ=1)*2)-((VY=6ANDVZ=0)*3)GOTO16,17,18
14 IFVY<>5THENRETURN
15 VY=AC:PRINTCR$+"ACS:"+STR$(AC):PRINT"NEW LVL:";:GOSUB73:AC=I:RETURN
16 VY=PEEK(897):PRINTCR$+"TIME:";:GOSUB73:ONABS(I>0)GOTO20:RETURN
17 TX$=CR$+"SYSOP LOCAL...":GOSUB5:PRINT"F1 ABORTS!":POKE720,1:RETURN
18 VY=P(5):PRINTCR$+"SUBOP #";P(5):PRINT"NEW #";:GOSUB73:P(5)=I:RETURN
19 ONPEEK(681)+1GOTO66:TX$="{white}{left}> <{left*2}{ct c}":GOSUB5:GOTO66
20 POKE897,I:POKE896,1:RETURN
21 POKE254,ASC(OU$+CHR$(0)):SYS38203:RETURN
22 IFD1AND2^(D-8)THENF$="0:"+F$:PRINT#D,"s"+F$:RETURN
23 F$="@0:"+F$:RETURN
25 IFNN=1THENTX$="{clear}space - abort/ cntrl[s] (home) - pause":GOSUB5
26 OU$=CR$:GOSUB21:IFVD<8THENVD=8
27 OPEN2,VD,2,"0:"+FI$+",s,r":SYS38212:CLOSE2:IFPEEK(252)=0THEN29
28 TX$="hit return{ct c}":GOSUB5:GOSUB35
29 NN=0:VD=0:RETURN
30 I=0:GOSUB7:IFI$=""ORLEN(I$)>4THENRETURN
31 FORI8=1TOLEN(I$):IFMID$(I$,I8,1)="e"ANDVAL(LEFT$(I$,1))>0THENI$="0"
32 NEXTI8:I=VAL(I$):RETURN
33 I7=PEEK(720):POKE720,1:GOSUB7:POKE720,I7:RETURN
35 GOSUB11:IFIN$<>CR$THEN35
36 OU$=CR$:GOSUB21:RETURN
37 ONSGN(PEEK(MC)AND16)GOTO8:POKEMC,0:POKEMC+2,32:ONM0+1GOTO40,8,39,80
39 POKEMC,32:POKEMC+2,0:RETURN
40 PRINT#5,CR$:GOSUB45:PRINT#5,"+++";:GOSUB45:PRINT#5,"ath":RETURN
41 T$=TI$
42 ON(T$=TI$)+2GOTO42:RETURN
44 ON(LF$=I$)+2GOTO8000:LF$=I$:SYS51971:LOADI$,8
45 GOSUB41:GOSUB41:GOSUB41:RETURN
46 POKEMC,2:POKEMC+2,6:ONM0GOTO8:POKEMC,0:POKEMC+2,38:IFM0<>3THENRETURN
47 POKEMC+2,230:POKEMC,0:POKE56590,65:POKE56588,48:POKE56588,49:RETURN
49 RESTORE:FORI=659TO666:READA:POKEI,A:NEXT:BA=1200:A=56:GOTO52
50 DATA8,0,70,1,0,9,55,3,6,0,69,6,0,9,82,13,31,28,31,30,31,31,31,31,30,31,30,31
51 RESTORE:FORI=1TO8:READA:NEXT:FORI=659TO666:READA:POKEI,A:NEXT:BA=300:A=40
52 IFM0=3THENPOKE56588,A:POKE56588,A+7
53 POKE51196,168:POKE51197,6:RETURN
55 EQ=0:CLOSE2:CLOSE3:OPEN2,8,2,"0:{pound}log1":U=U1:V=U2:I=0:GOSUB3:GET#2,A$
56 INPUT#2,NA$,PH$,AC$,RN$,PS$:CLOSE2:OPEN2,8,2,"0:{pound}log2":I=0:GOSUB3
57 INPUT#2,CA$,LC$:FORI=0TO9:INPUT#2,P(I):NEXT:CLOSE2:GOTO63
58 CLOSE2:CLOSE3:OPEN2,8,2,"0:{pound}log1":A$="{pound}"+NA$+CR$+PH$+CR$+AC$+CR$+RN$
59 U=U1:V=U2:A$=A$+CR$+PS$:I=0:GOSUB3:PRINT#2,A$:CLOSE2:IFY0=0THENRETURN
60 IFY0=0THENP(4)=INT(CC/256):P(3)=CC-(INT(CC/256)*256)
61 Y0=0:A$=CA$+CR$+LC$+CR$:FORI=0TO9:A$=A$+STR$(P(I))+CR$:NEXT
62 U=U1:V=U2:OPEN2,8,2,"0:{pound}log2":I=0:GOSUB3:PRINT#2,A$:CLOSE2:RETURN
63 CC=P(3)+(256*P(4)):LC=VAL(LC$):RETURN
64 TX$="{ct n}"+CR$:GOSUB5:TX$="":IF(P(6)AND16)=0THENRETURN
65 TX$=STR$(PEEK(897)):TX$="{white}<"+RIGHT$(TX$,LEN(TX$)-1)+">":RETURN
66 GOSUB11:IFIN$=""THEN66
67 G=0:FORI8=1TOLEN(C$):IFIN$=MID$(C$,I8,1)THENG=I8
68 NEXTI8:ONG+1GOTO66:TX$=IN$:GOSUB5:RETURN
70 I=0:GOSUB7:IFI$=""ORLEN(I$)>6THENRETURN
71 FORI8=1TOLEN(I$):IFMID$(I$,I8,1)="e"ANDVAL(LEFT$(I$,1))>0THENI$="0"
72 NEXTI8:I=VAL(I$):RETURN
73 I7=PEEK(720):POKE720,1:GOSUB30:IFI$=""THENI=VY
74 POKE720,I7:RETURN
75 B=0:A=YR*10000000:FORI=1TOMN-1:B=B+DA(I):NEXTI:B=B+DA:A=A+(B*10000):SYS38215
76 B=VAL(LEFT$(I$,2)):W$=RIGHT$(I$,2):IFB=12THENB=0
77 B=B*60:B=B+VAL(RIGHT$(I$,4)):IFW$="pm"THENB=B+720
78 A=A+B:AA$=STR$(A):RETURN
80 POKEMC+2,230:POKEMC,32:POKE56590,65:POKE56588,14:POKE53265,PEEK(53265)OR16
81 RETURN
100 GOSUB51:DIMTT$(101),Z1(50),Z2(50),P0$(100),DA(12),NA$(2,26),AC(2,26)
101 DIMDV(2,26),MM(2,26),V(26),I1$(10),I2$(10):RESTORE:CR$=CHR$(13):POKE53280,0
102 MN=PEEK(703):POKE694,26:POKE695,24:POKE696,3:IFPEEK(788)>0THENSYS51980
110 DA=PEEK(704):FORI=1TO16:READA:NEXTI:FORI=1TO12:READA:DA(I)=A:NEXTI:MC=56577
120 YR=PEEK(712):IFPEEK(702)=16THEN200
130 PRINT"{clear}{down*5}{yellow}{ct n}DATA DISK(S) IN!":PRINT"PRESS {reverse on}RETURN{reverse off}";
140 GETA$:IFA$<>CHR$(13)THEN140
200 CLOSE8:OPEN8,8,15,"i0":IFM0=3THENGOSUB80
210 POKE702,0:OPEN2,8,2,"0:{pound}vars,s,r":INPUT#2,BN$:IFSTTHENCLOSE2:GOTO130
220 INPUT#2,MD,UL,UC,DC,M0,D0,D1,M1,M2:FORI=0TO6:INPUT#2,C$(I),A$(I):NEXTI
230 FORI=0TO2:INPUT#2,N(I):FORX=0TON(I):INPUT#2,NA$(I,X),AC(I,X),DV(I,X),MM(I,X)
240 NEXTX:NEXTI:FORI=0TO9:INPUT#2,CA(I),TM(I):NEXTI:FORI=1TO8:INPUT#2,A$
250 POKE684+I,ASC(A$):NEXTI:CLOSE2:POKE701,M0:OPEN2,8,2,"0:{pound}log3":U=1:V=0
280 I=0:GOSUB4:I1$=I$:SYS38233:I2$=I$:INPUT#2,LZ:CLOSE2:A$="x1e0m0v0":GOSUB1
285 CLOSE8:FORI=1TOD0:OPEN7+I,7+I,15,"i0":NEXT:IFPEEK(709)THENOPEN15,4,7
500 PRINT"{clear}{down*13}{white}";BN$:GOSUB37:IFM0=3THENPOKEMC,0:POKEMC+2,0
510 POKE53280,0:POKE53281,0:POKE896,0:POKE720,0:POKE682,0:PRINT"{yellow}BBS MODE:"
520 POKE714,0:PRINT"{reverse on}F1{reverse off} {white}LOCAL MENU"+CR$+"{yellow}{reverse on}F3{reverse off}{white} LOGON"+CR$+"{yellow}LAST CALLER:{white}";
530 PRINTI1$:PRINT"{yellow}OFF AT:{white}";I2$:PRINT"{yellow}CALLS TODAY:{white}";STR$(PEEK(733))
550 SYS38215:PRINT"{home}{down}{right*2}{yellow}DATE:{white}";MN;"/";DA;"   {yellow}TIME:{white}";I$
560 A=VAL(LEFT$(I$,2)):IF(PEEK(MC)AND8)=0ANDM0>0THENI=0:X=0:GOSUB46:GOTO650
570 IFRIGHT$(I$,2)="am"ANDPEEK(713)<>15THENIFA=1ORA=12THENPOKE713,15:GOTO640
580 IFPEEK(713)=15ANDRIGHT$(I$,2)="am"ANDA<12ANDA>1THENPOKE713,0
590 GETA$:NA=1:U1=1:U2=0:IFA$="{f1}"THEN800
600 IFA$="{f3}"THENPOKE714,1:POKE830,0:POKE720,1:I=0:GOTO1000
610 GET#5,B$
615 IFB$="5"ORB$=CHR$(181)THENPOKE830,0:POKE714,0:GOSUB49:GOTO1000
620 IFB$="1"ORB$=CHR$(177)THENPOKE830,0:POKE714,0:GOSUB51:GOTO1000
630 GOTO550
640 PRINT"{clear}{down*3}{reverse on}{green}monster mode":I$="{pound}prg.monster":GOTO44
650 IF(PEEK(MC)AND16)=0THENX=X+1:IFX>40THEN671
660 I=I+1:IFI<700THEN650
670 GOSUB37:GOTO550
671 I=0:X=0:IFM0<>3THEN680
673 IF(PEEK(MC)AND1)=XTHENI=I+1:PRINTI,X:IFI<5THEN673
675 IFI<5THENX=ABS(X-1):I=0:GOTO673
677 IF(PEEK(MC)AND16)=16THEN650
679 IFX=1THEN690
680 B$="1":GOTO615
690 B$="5":GOTO615
800 PRINT"{clear}{pink}Z{yellow}E{white}L{cyan}C{light blue}H {white}SYSOP MENU:":PRINT"1) BBS MODE"+CR$+"2) MONSTER MODE"
810 PRINT"3) ZAP TERM"+CR$+"4) FEEDBACK"+CR$+"5) USER LOG"+CR$+"{down}?:{reverse on} {reverse off}{left}";
820 POKE714,1:I=9:GOSUB20:POKE830,0:C$="12345":GOSUB66:POKE714,0
821 A=8:FI$="{pound}calls":I$="{pound}prg.term":IFG<4THENONGGOTO500,640,44
824 IFG=4THENFI$="{pound}feedback":A=MD
900 PRINT"{clear}":POKE720,1:OPEN2,A,2,"0:"+FI$+",s,r"
910 SYS38212:IFPEEK(253)=0THENCLOSE2:POKE720,0:PRINT:PRINT"{ct n}DELETE?";:GOTO950
920 PRINT"{ct n}";:GETA$:ON(A$=CR$)+2GOTO910:GOTO920
950 GETA$:IFA$="y"THENPRINT"y":PRINT#A,"s0:"+FI$:CLOSEA:OPENA,A,15,"i0":GOTO500
960 ON(A$<>"n")+2GOTO950:PRINT"n":GOTO500
1000 CLOSE2:CLOSE4:FORI=0TO50:GET#5,A$:NEXT:I=5:GOSUB20:POKE830,0
1005 POKE681,0:POKE682,1:PRINT"{clear}{light gray}";:TX$=CR$+CR$+CR$+"connection!!!":GOSUB5
1006 SYS38215:E$=I$+" call":GOSUB9:IFPEEK(145)=223THENPOKE681,1:GOTO1145
1010 TX$=CR$+CR$+CR$+"hit return:{ct c}":GOSUB5:C$=CR$+CHR$(10):GOSUB66:NA$=""
1020 IFPEEK(714)=1THEN1055
1030 TX$=CR$+CR$+"commodore graphics"+CR$+"are the above words in uppercase?{ct c}"
1035 GOSUB5:C$="nN"+CHR$(110)+"yY"+CHR$(121):GOSUB66:IFG<4THEN1060
1055 POKE681,1:POKE682,0:GOTO1080
1060 TX$="NEED LINEFEEDS?{ct c}":GOSUB5:C$="yn":GOSUB66:IFG=2THENPOKE682,0
1080 I=5:GOSUB20:NN=1:FI$="{pound}opening msg":A=0:IFPEEK(681)THENFI$=FI$+"/g"
1085 POKE762,79:POKE699,100:GOSUB25:CLOSE2:OPEN2,8,2,"0:{pound}log1"
1090 A=A+1:IFA=6THENTX$="{f1}GOODBYE!":GOSUB5:GOTO1500
1100 NE=0:TX$=CR$+"{f1}{ct n}(?)USERLIST"+CR$+"{f1}ID NUMBER :{ct c}":GOSUB5:GOSUB30
1105 IFI$="?"THENFI$="{pound}userlist":CLOSE2:A=A-1:GOTO1085
1110 IFI=0ANDI$<>"new"THENTX$="{f1}TYPE 'NEW' IF{sh space}YOU{sh space}HAVE NO ID #":GOSUB5:GOTO1090
1115 NA=I:IFI$="new"THENCLOSE2:I=10:GOSUB20:I$="{pound}prg.new":GOTO44
1120 U2=INT(NA/256):U1=NA-(U2*256):I=0:U=U1:V=U2:GOSUB3:GET#2,A$:INPUT#8,E
1125 IFA$<>"{pound}"ORE>0THEN1140
1130 FORI=1TO5:SYS38233:NEXT:PS$=I$:TX$=CR$+"{f1}PASSWORD  :{ct c}":GOSUB5
1135 POKE710,1:GOSUB7:IFI$=PS$THENCLOSE2:GOTO1145
1140 TX$="{red}INVALID INFO!":GOSUB5:I=0:GOTO1110
1145 TX$="{clear}{f1}LOADING INFO...":GOSUB5:GOSUB55:AC=VAL(AC$):E$=NA$:GOSUB9
1150 I=TM(AC):GOSUB20:I$="{pound}prg.open":GOTO44
1500 CLOSE2:CLOSE3:FORI=9TOD0+8:CLOSEI:NEXT:OU$="{black}":IFPEEK(830)=0THEN1530
1505 IF(PEEK(MC)AND16)=16THENE$="hung up":GOSUB9:PRINT"{clear}{reverse on}{white}>>":GOTO1535
1510 TX$=CR$+"{f1}time up!":GOSUB5
1530 FI$="{pound}closing msg":GOSUB25:TX$="{red}{ct n}{reverse on}BYE...":GOSUB5:IFBA<600THENGOSUB51
1535 GOSUB75:GOSUB21:CA$=STR$(VAL(CA$)+1):LC$=AA$:IFNA$<>""THENGOSUB60
1540 POKE1,PEEK(1)OR2:GOSUB37:LC$=STR$(MN)+"/"+STR$(DA):SYS38215:E$=I$+" off"
1550 GOSUB9:OPEN2,8,2,"{pound}calls,s,a":INPUT#8,E:IFE<20THENPRINT#2,CHR$(1):GOTO1570
1560 CLOSE2:OPEN2,8,2,"0:{pound}calls,s,w"
1570 PRINT#2,LC$+CR$+P0$(0);:FORI=1TOP0-1:A$=RIGHT$(P0$(I),1):IFA$=CR$THENA$="+"
1572 IFLEFT$(P0$(I),3)=LEFT$(P0$(I-1),3)THENPRINT#2,","+A$;:GOTO1580
1575 PRINT#2,CR$+P0$(I);
1580 NEXTI:CLOSE2:OPEN2,8,2,"0:{pound}log3":IFNA$=""THENNA$="NOBODY"
1590 U=1:V=0:I=0:GOSUB3:PRINT#2,NA$+CR$+LC$+" "+I$+CR$+STR$(LZ+1):CLOSE2
1600 PRINT"{clear}{white}{reverse on}{ct n}RESET>>":CLOSE8:GOSUB51:PRINT#5,CR$:GOSUB37:POKE702,16:CLOSE5:RUN
2000 GOSUB64:IFP(6)AND16THENTX$=TX$+" {f1}TIME LEFT":GOSUB5
2010 TX$="{white}(?) {f1}COMMAND{white}:{ct c}":GOSUB5:C$="?"+C$(0)
2030 GOSUB19:IFG=1THENFI$="{pound}mainmenu"+STR$(AC):NN=1:GOSUB25:GOTO2000
2040 G=G-1:IFAC<VAL(MID$(A$(0),G,1))ORG<1THEN2000
2070 E$="cmd:"+IN$:GOSUB9:ONGGOTO2150,2200,2100,2250,2300,2400,2450,2500,2600
2080 G=G-9:ONGGOTO2800,2980,3000,3500,2990,2995,3800
2100 FI$="{pound}info":NN=1:GOSUB25:GOTO2000
2150 TX$=CR$+"{f1}FEEDBACK (Y/N)?{ct c}":GOSUB5:C$="yn":GOSUB66:IFG=2THEN2180
2160 FF$="{pound}feedback":GOSUB75:QQ$=STR$(A)+CR$+"{f1}BY:"+NA$+" ID#"+STR$(NA)
2170 SYS38215:QD$="{f1}ON:"+STR$(MN)+"/"+STR$(DA)+" AT:"+I$:V1=MD:GOSUB5000
2180 IFXX=0THEN2000
2190 GOTO1500
2200 TX$=CR$+"{f1}LOG OFF (Y/N)?{ct c}":GOSUB5:C$="yn":GOSUB66:IFG=1THENXX=1:GOTO2150
2210 GOTO2000
2250 IFPEEK(1118)=33THENTX$=CR$+"{f1}THE{sh space}PAGE{sh space}LIGHT{sh space}IS{sh space}ON!":GOSUB5:GOTO2000
2255 TX$="{f1}PAGING SYSOP...{ct c}":GOSUB5:FORX=1TO25:FORI=1TO50:NEXTI
2260 TX$="{f1}.{ct g}{ct c}":GOSUB5:NEXTX:POKE1118,33:GOTO2000
2300 TX$=CR$+"{f1}USERLIST (?):{ct c}":GOSUB5:POKE731,1:GOSUB30:IFI$="?"THEN2340
2302 IFI>0ORI$=""THEN2345
2304 OPEN2,8,2,"0:{pound}userlist,s,r":A$=I$:A=0:SYS38230:SYS38209,I$
2306 SYS38230:IFI$=""THEN2390
2308 B$=RIGHT$(LEFT$(I$,LEN(I$)-1),LEN(I$)-LEN(STR$(A))-4)
2310 IFMID$(B$,1,LEN(A$))=A$THENTX$=I$:GOSUB5
2320 A=A+1:GOSUB11:IFIN$=" "THEN2390
2330 GOTO2306
2340 TX$="{f1}ENTER ID#"+CR$+"{f1}SEARCH LETTERS"+CR$+"{f1}OR (RETURN){down}":GOSUB5:GOTO2300
2345 OPEN2,8,2,"0:{pound}userlist,s,r":SYS38230:SYS38209,I$
2350 SYS38230:IFI$=""THEN2390
2355 X=VAL(RIGHT$(I$,LEN(I$)-1)):IFX<ITHEN2350
2360 SYS38209,I$:GOSUB11:IFIN$=" "THEN2390
2370 SYS38230:IFI$=""THEN2390
2380 GOTO2360
2390 CLOSE2:IFUR=1THENUR=0:RETURN
2399 GOTO2000
2400 A$=CR$+"{f1}--------------------"+CR$:TX$=A$+"{f1}STATUS ":GOSUB5:SYS38215
2410 TX$="{f1}HANDLE:"+NA$+CR$+"{f1}REAL NAME:"+RN$+CR$+"{f1}ACCESS:"+AC$+CR$+"{f1}TIME:"+I$
2415 GOSUB5:TX$="{f1}U/D CREDITS:"+STR$(CC):GOSUB5
2420 TX$="{f1}TIME LEFT:"+STR$(PEEK(897))+CR$+A$:GOSUB5:GOTO2000
2450 TX$="{clear}{down*2}{f1}EMAIL SECTION!":GOSUB5:I$="{pound}prg.email":GOTO44
2455 NN=-1:GOTO2465
2460 GOSUB64:TX$=TX$+"{f1}EMAIL:{ct c}":GOSUB5:NN=0
2462 C$=C$(4)+"?":GOSUB19:WH=0:E$="eml:"+IN$:GOSUB9:ONGGOTO8500,2470,8010,2000
2465 FI$="{pound}emailmenu":NN=NN+1:GOSUB25:GOTO2460
2470 UR=1:GOSUB2300:GOTO2460
2500 GOSUB64:TX$=TX$+"{f1}SYSOP:{ct c}":GOSUB5:C$=C$(3)+"?":GOSUB19
2501 DV=8:E$="sys:"+IN$:GOSUB9:ONGGOTO2520:IFG=2THENUR=1:GOSUB2300:GOTO2500
2503 IFG<5THENI$="{pound}prg.user":GOTO44
2505 IFG=5THENTX$="{f1}RELOADING...":GOSUB5:GOSUB55:GOTO2500
2506 IFG<9THENI$="{pound}prg.sysop":GOTO44
2509 IFG=9THEN2000
2510 FI$="{pound}sysopmenu":NN=1:GOSUB25:GOTO2500
2520 TX$=CR$+STR$(DV)+":{ct c}":GOSUB5:GOSUB7:IFI$=""THEN2500
2522 A$=LEFT$(I$,1):IFA$="@"ANDLEN(I$)>2THENA$=RIGHT$(I$,LEN(I$)-2):GOTO2540
2524 IFA$="w"ANDLEN(I$)>2THENFF$=RIGHT$(I$,LEN(I$)-2):GOTO2550
2526 IFA$="e"ANDLEN(I$)>2THENFF$=RIGHT$(I$,LEN(I$)-2):GOTO2555
2528 IFA$="@"THENI$="":GOTO2536
2530 IFA$="$"THENPOKE253,0:OPEN2,DV,0,I$:SYS38224:CLOSE2:GOTO2520
2532 IFA$="%"THEN2590
2534 IFLEN(I$)<4ANDVAL(I$)>7ANDVAL(I$)<12THENDV=VAL(I$):GOTO2520
2535 PRINT#DV,I$
2536 INPUT#DV,E1,E1$,E2,E3:TX$=STR$(E1)+","+E1$+","+STR$(E2)+","+STR$(E3)
2538 GOSUB5:GOTO2520
2540 OPEN2,DV,2,"0:"+A$+",s,r"
2542 SYS38212:IFPEEK(253)=0THENCLOSE2:GOTO2520
2544 TX$="{white}{ct n}S)KIP NEXT OR (RETURN){ct c}":GOSUB5:C$="s"+CR$:GOSUB66:IFG=2THEN2542
2546 SYS38218:GOTO2544
2550 QQ$="":QD$="":S0=1:V1=DV:GOSUB5000:GOTO2520
2555 TX$="{f1}SEARCHING FOR "+FF$:GOSUB5:OPEN2,DV,2,"0:"+FF$+",s,r":GET#2,A$:UP=0
2560 IFST>0THENTX$="FILE{sh space}NOT{sh space}FOUND":GOSUB5:GOTO2587
2565 TX$="LOADING":GOSUB5
2570 QQ$="":CLOSE2:OPEN2,DV,2,"0:"+FF$+",s,r":LN=0:FORI=0TO100:TT$(I)="{black}":NEXTI
2575 SYS38230:IFPEEK(253)=1THEN2586
2580 IFI$=CHR$(142)+CHR$(147)THENUP=1:SYS38209,I$:GOTO2575
2585 SYS38209,I$:TT$(LN)=I$:LN=LN+1:GOTO2575
2586 CLOSE2:SYS38209,I$:A=FRE(0):TT$(LN)=I$:LN=LN+1:S3=1:GOTO2550
2587 CLOSE2:GOTO2520
2590 I$=RIGHT$(I$,LEN(I$)-1):OPEN2,DV,0,"$"+I$:SYS38242:CLOSE2
2595 TX$=STR$(PEEK(706))+" files counted":GOSUB5:GOTO2520
2600 TX$="{clear}{f1}{down}MESSAGE BASE!{white}":GOSUB5:I$="{pound}prg.read":GOTO44
2605 GOSUB2610:GOTO2650
2610 TX$=CR$+"{f1}{down}ENTERING SIG "+CHR$(97+CS):GOSUB5:DV=DV(0,CS):CM=0:I1=0:I2=0
2611 VD=DV:FI$="{pound}sigmsg "+CHR$(65+CS):GOSUB25:OPEN2,8,2,"0:{pound}Sig "+CHR$(97+CS)
2612 J0=0:FORI=1TONM:Z1(I)=0:Z2(I)=0:NEXT:U=1:V=0:I=0:GOSUB3
2613 INPUT#2,NM,MP:FORU=2TONM+1:X=U-1:I=0:GOSUB3:INPUT#2,Z1(X),Z2(X)
2614 I1=I1+Z1(X):IFZ2(X)>LCTHENJ0=J0+1
2615 NEXTU:CLOSE2:TX$=CR$+"{f1}"+STR$(NM)+" MESSAGE(S) /"+STR$(I1)+" REPLY(S){f1}"
2616 TX$=TX$+CR$+STR$(J0)+" NEW MESSAGE(S) / REPLY(S)":GOSUB5:RETURN
2620 OPEN2,8,2,"0:{pound}Sig "+CHR$(97+CS):U=1:V=0:I=0:GOSUB3
2625 PRINT#2,STR$(NM)+CR$+STR$(MP):U=M+1:GOSUB3:PRINT#2,STR$(X)+CR$+STR$(Y)
2630 CLOSE2:RETURN
2650 IFWI=1THENCM=CM+1:GOTO2686
2651 IFKD=1THEN2770
2655 GOSUB64:TX$=TX$+"{f1}SIG {f1}"+CHR$(97+CS)+"{white}:{ct c}":GOSUB5:C$="?"+CR$+C$(2):GOSUB19
2656 G=G-2:E$="msg:"+IN$:GOSUB9:IFG<1THEN2668
2657 IFASC(IN$)>192THEN2690
2658 IFASC(IN$)>96THEN2689
2659 IFAC<VAL(MID$(A$(2),G,1))THEN2650
2660 ONGGOTO8600,8010,8700,2685,2000,8500,2695,2750
2668 IFG=0THEN2780
2669 FI$="{pound}msgmenu":NN=1:GOSUB25:GOTO2655
2685 CM=1:WI=0:TX$="{f1}READ NEW...":GOSUB5
2686 IFCM>NMTHENWI=0:GOTO2650
2687 G=3:IFZ2(CM)>LCTHENWI=1:GOTO8050
2688 CM=CM+1:GOTO2686
2689 IN$=CHR$(ASC(IN$)+96)
2690 I=ASC(IN$)-193:IFI>N(0)ORI=CSTHEN2650
2691 IFAC(0,I)>ACTHEN2650
2693 CS=I:GOSUB2610:GOTO2650
2695 IFP(5)<>CS+1ANDP(5)<>100THEN2650
2696 I$="{pound}prg.medit":GOTO44
2750 TX$="{f1}ALL{sh space}NEW...":GOSUB5:KD=0
2755 IFJ0>0THENG=3:KD=1:GOTO2685
2760 GOTO2770
2765 GOSUB2610:IFJ0>0THENG=3:KD=1:GOTO2685
2770 CS=CS+1:IFCS>N(0)THEN2778
2773 IFAC(0,CS)<=ACTHEN2765
2778 TX$="DONE!":GOSUB5:CS=CS-1:KD=0:GOTO2650
2780 CM=CM+1:GOTO8050
2800 TX$="{clear}{down}{f1}UPLOAD/DOWNLOAD SYSTEM!":GOSUB5:I$="{pound}prg.udsec":GOTO44
2805 FI$="{pound}udmenu":GOSUB25:GOTO2810
2808 GOSUB45:GOSUB41
2810 CLOSE2:YU=0:GOSUB64:TX$=TX$+"{f1}U{white}/{f1}D{white} "+CHR$(CU+193)+":{ct c}":GOSUB5
2811 C$="?"+CR$+C$(6):GOSUB19:E$="u/d:"+IN$:GOSUB9:G=G-2:IFG<1THEN2818
2812 IFASC(IN$)>192THEN8300
2813 IFASC(IN$)>96THEN8299
2816 IFAC>=VAL(MID$(A$(6),G,1))THENONGGOTO8100,2000,9000,9500,8200
2818 IFG<0THENFI$="{pound}udmenu":NN=1:GOSUB25
2819 GOTO2810
2830 TX$="{clear}{down}{f1}GO{sh space}TO{sh space}SEND{sh space}FILES MODE!":GOSUB5:FI$=FI$+"w":QY=1:GOTO2849
2832 QY=0:SYS49173:SYS49161:E$="ul:"+FI$:GOSUB9:IFPEEK(512)=1THEN2860
2836 OPEN2,DV(2,CU),2,"0:"+FI$:INPUT#DV(2,CU),E:IFE>0THEN2860
2838 FORI=1TO1300:NEXTI:SYS61310:SYS49173:SYS49155:FORX=1TO150:NEXTX
2840 POKE668,PEEK(667):SYS49173:YU=1:GOTO2860
2849 TI$="000000":TX$="{f1}(X) ABORTS / (L) SETS AUTO LOGOFF":GOSUB5
2851 ER=0:GOSUB11:IFIN$="x"ORVAL(TI$)>40THENER=1:GOTO2860
2852 IFIN$="l"THENXX=1:TX$="{f1}AUTO LOGOFF{sh space}SET!":GOSUB5
2856 GET#5,A$:IFA$<>"g"ANDA$<>"o"ANDA$<>"k"THEN2851
2858 ONQY+1GOTO2940,2832
2860 CLOSE2:ONFHGOTO9000:TX$="{clear}":GOSUB5:QY=0:OPEN2,DV(2,CU),0,"$0:"+FI$
2862 SYS38239:CLOSE2:A=PEEK(251):B=PEEK(707)+256*PEEK(708):IFYU=0THEN2880
2863 IFB<MM(2,CU)ORA=0THENPRINT#DV(2,CU),"s0:"+FI$:PRINT#DV(2,CU),"v0"
2880 ONERGOTO2891:IFYU=0THENCC=CC-(A*DC)
2890 IFYU=1THENYU=0:CC=CC+(A*UC)
2891 ONXXGOTO1500:GOTO2808
2930 TX$="{clear}{down}{f1}GO TO RECEIVE FILES MODE!":GOSUB5:QY=0:GOTO2849
2940 POKE51227,TY:SYS49173:SYS49164:IFPEEK(512)=1THEN2860
2950 E$="dl:"+FI$:GOSUB9:SYS49173:POKE51224,255:SYS49158
2955 GOTO2860
2980 I$="{pound}prg.vote":GOTO44
2990 I$="{pound}prg.var":GOTO44
2995 I$="{pound}prg.menu":GOTO44
3000 IFPEEK(659)=8THENTX$="{f1}CANNOT{sh space}CHANGE{sh space}1200 BAUD":GOSUB5:GOTO2000
3010 TX$="{f1}BAUD{sh space}IS"+STR$(BA):GOSUB5:TX$="{f1}NEW BAUD :{ct c}":GOSUB5
3020 GOSUB30:IFI<200ORI>500THEN2000
3030 TX$="{f1}CHANGE BAUD NOW! (RETURN){ct c}":GOSUB5:POKE659,6
3040 BA=I:A=1022730/BA/2:GOSUB3499:POKE51196,X:POKE51197,Y:A=1022730/BA
3050 GOSUB3499:POKE665,X:POKE666,Y:A=1022730/BA/2-100:GOSUB3499:POKE661,X
3060 POKE662,Y:POKE659,6:I1=PEEK(211):I2=PEEK(214):PRINT"{home}{down*2}";:POKE211,27
3065 PRINT"{white}";BA:POKE211,I1:POKE214,I2
3070 IFFH=1THENFH=0:RETURN
3090 GOSUB35:GOTO2000
3499 Y=INT(A/256):X=A-(Y*256):RETURN
3500 I$="{pound}prg.text":GOTO44
3800 TX$="{f1}SURE YOU{sh space}WANT TO{sh space}TOGGLE "+CR$+"C= GFX/ASCII (Y/N)?{ct c}":GOSUB5
3801 C$="yn":GOSUB66:IFG=2THEN2000
3804 TX$=CR$+"{f1}SWITCH TO ":IFPEEK(681)THENTX$=TX$+"ASCII AND{sh space}HIT{sh space}RETURN{ct c}"
3805 IFPEEK(681)=0THENTX$=TX$+"C= COLOR/GFX AND{sh space}HIT RETURN{ct c}":
3810 GOSUB5:A=ABS(PEEK(681)-1):POKE681,A:POKE1124,A+32:GOSUB35:GOTO2000
5000 OU$="{ct n}":IFUP=1ORP(6)AND8THENOU$=CHR$(142):UP=1
5001 POKE705,0:IFP(6)AND4THENPOKE705,1
5002 LO=0:POKE762,CX:IFP(6)AND2THENLO=1:POKE762,CX-3
5003 ML=100:IFP(6)AND1THENML=50
5010 IFS3=0THENFORI=0TO100:TT$(I)="{black}":NEXTI:LN=0
5020 GOSUB21:A=FRE(0):IFS1=1THEN5040
5030 TX$="{clear}{f1}message maker!":GOSUB5
5040 TX$="{f1}type /h for help":GOSUB5:S1=0:S2=0:S3=0
5050 TX$="{f1}"+RIGHT$(STR$(ML),LEN(STR$(ML))-1)+" lines max. go:{white}":GOSUB5
5060 IFLO=1THENI5=LN:GOSUB5360
5070 GOSUB7:IFI$=""THENI$="{black}"
5080 TT$(LN)=I$:K$="":Q$=LEFT$(TT$(LN),2):IFLEFT$(Q$,1)="/"THENK$=RIGHT$(Q$,1)
5090 IFK$=""THEN5130
5100 G=0:FORI=1TOLEN(C$(1)):IFK$=MID$(C$(1),I,1)THENG=I
5110 NEXTI:IFG=0THENTX$="illegal command":GOSUB5:GOTO5060
5120 ONGGOTO5145,5150,5300,5370,5380,5390,5394,5400,5410,5430,5440,5450,5470,5290,5490
5130 IFLN>ML-1THENTX$="out of lines. type /h for help":GOSUB5:GOTO5060
5140 LN=LN+1:GOTO5060
5145 FI$="{pound}msgmkr help":GOSUB25:GOTO5060
5150 TX$="save..(y/n){ct c}":GOSUB5:C$="yn":GOSUB66:IFG=2THEN5060
5180 TX$="saving...":GOSUB5:OPEN2,V1,0,"$0:z=u":SYS38239:CLOSE2
5181 A=PEEK(251)+256*PEEK(252):IFA<20THENTX$="disk full!":GOSUB5:GOTO5415
5185 IFS0=1THEN5210
5190 OPEN2,V1,2,"0:"+FF$+",s,a":INPUT#V1,E
5200 IFE<20THENPRINT#2,CHR$(1):GOTO5220
5210 CLOSE2:F$=FF$:D=V1:GOSUB22:OPEN2,D,2,F$+",s,w"
5220 IFQQ$<>""ANDQD$<>""THENPRINT#2,QQ$:PRINT#2,QD$+"{white}":PRINT#2,CHR$(2)
5230 TT$(LN)=CHR$(144):IFUP=1THENPRINT#2,CHR$(142)+"{clear}"
5240 FORI=0TOLN-1:PRINT#2,TT$(I):NEXTI:CLOSE2:TX$="saved":GOSUB5:GOTO5420
5290 GOSUB5350:IFER=1ORN=1THEN5060
5291 TX$=TT$(Q)+CR$+"rewrite line"+STR$(Q):GOSUB5:IFLO=1THENI5=Q:GOSUB5360
5292 GOSUB7:IFI$=""THENTX$="(no change)":GOSUB5:GOTO5060
5295 TT$(Q)=I$:GOTO5060
5300 GOSUB5350:IFER=1ORN=1THEN5060
5301 X=1:TX$=">"+TT$(Q)+CR$+"search for:{ct c}":GOSUB5:GOSUB7:I1$=TT$(Q)
5305 A=LEN(I$):B=LEN(I1$):IFI$=""THEN5060
5310 IFMID$(I1$,X,A)=I$THEN5340
5320 X=X+1:IFX<=BTHEN5310
5330 TX$="not found!":GOSUB5:GOTO5060
5340 C=X-1:I2=B-X-A+1:A$=LEFT$(I1$,C):B$=RIGHT$(I1$,I2)
5345 TX$="change to :{ct c}":GOSUB5:GOSUB7:TT$(Q)=A$+I$+B$:IFTT$(Q)=""THENTT$(Q)="{black}"
5346 IFLEN(TT$(Q))>CX+21THENTX$="illegal length":GOSUB5:TT$(Q)=I1$
5347 GOTO5060
5350 N=0:Q=-1:O=Q:ER=0:A$=TT$(LN):E$=RIGHT$(A$,LEN(A$)-2):IFLEN(E$)>5THENER=1
5351 P=LEN(E$):FORI=1TOLEN(E$):B$=MID$(E$,I,1):IFB$=","THENP=I
5352 IFB$=" "ORB$="e"THENER=1
5353 NEXTI:IFER=1THENTX$="illegal format":GOSUB5:GOTO5357
5354 Q=VAL(LEFT$(E$,P)):IFP<>LEN(E$)THENO=VAL(RIGHT$(E$,LEN(E$)-P))
5355 IFE$=""THENN=1
5356 IFQ>LN-1ORQ<0THENER=1:IFCO=0THENTX$="bad line #":GOSUB5
5357 CO=0:IFO>0THENIFO<PORO>LN-1THENO=P
5359 RETURN
5360 TX$=STR$(I5):IFI5<10THEN5365
5362 TX$=RIGHT$(TX$,LEN(TX$)-1)
5365 TX$=TX$+">{ct c}":GOSUB5:RETURN
5370 CO=1:GOSUB5350:IFN=1ORER=1ANDQ>=0THEN5060
5371 IFER=1ANDQ<0THENQ=LN+Q:O=LN-1:ER=0:IFQ<0THENER=1
5372 IFER=1THEN5060
5373 IFO=-1THENO=Q
5374 I$="":IFO<>QTHENI$="s"
5376 A=O-Q+1:FORI=QTOLN-A+1:TT$(I)=TT$(I+A):NEXTI:LN=LN-A:IFLN<0THENLN=0
5378 A=FRE(0):TX$="line"+I$+" deleted":GOSUB5:GOTO5060
5380 GOSUB5350:OU$="{white}":GOSUB21:IFER=1THEN5060
5381 IFN=1ORO<0THENO=LN-1
5382 I5=Q:GOSUB5360:TX$=TT$(Q)+CHR$(0):GOSUB5:GOSUB11:IFIN$=" "THEN5060
5386 Q=Q+1:IFQ<=OTHEN5382
5388 GOTO5060
5390 TX$="clear..(y/n){ct c}":GOSUB5:C$="yn":GOSUB66:ONGGOTO5000,5060
5394 GOSUB5350:OU$="{white}":GOSUB21:IFER=1THEN5060
5395 IFN=1ORO<0THENO=LN-1
5396 TX$=TT$(Q):GOSUB5:GOSUB11:IFIN$=" "THEN5060
5397 Q=Q+1:IFQ<=OTHEN5396
5398 GOTO5060
5400 GOSUB5350:IFER=1ORLN>ML-1ORN=1THEN5060
5404 FORI=LNTOQ+1STEP-1:TT$(I)=TT$(I-1):NEXTI:TT$(Q)="{black}":LN=LN+1
5405 A=FRE(0):TX$="done":GOSUB5:GOTO5060
5410 TX$="abort..(y/n){ct c}":GOSUB5:C$="yn":GOSUB66:IFG=2THEN5060
5415 S2=1
5420 QQ$="":QD$="":UP=0:S0=0:S1=0:S3=0:LO=0:POKE762,CX
5425 FORI=0TO100:TT$(I)="":NEXTI:A=FRE(0):OU$="{ct n}":GOSUB21:RETURN
5430 IFPEEK(681)=1ANDUP=0THENUP=1:TX$=CHR$(142)+"gfx on":GOSUB5:GOTO5060
5435 UP=0:TX$="{ct n}gfx off":GOSUB5:GOTO5060
5440 IFPEEK(705)=1THENPOKE705,0:TX$="w/w off":GOSUB5:GOTO5060
5445 POKE705,1:TX$="w/w on":GOSUB5:GOTO5060
5450 GOSUB5350:IFER=1THEN5060
5451 IFN=1ORO<0THENO=LN-1
5454 FORD=QTOO:IFTT$(D)="{black}"THEN5469
5455 Y=0:I$=TT$(D):FORI=1TOLEN(I$):IFMID$(I$,I,1)<>" "ANDY=0THENY=I
5460 NEXTI:I$=RIGHT$(I$,LEN(I$)-(Y-1)):I=LEN(I$):FORX=0TO((((CX+1)/2)-(I/2))-1)
5465 I$=" "+I$:NEXTX:TT$(D)=I$
5469 NEXTD:GOTO5405
5470 IFLO=0THENLO=1:TX$="l/n on":GOSUB5:POKE762,CX-3:GOTO5060
5480 TX$="l/n off":GOSUB5:POKE762,CX:LO=0:GOTO5060
5490 TX$="at line:"+STR$(LN)+" l/n:":A=LO:GOSUB5495:TX$=TX$+" w/w:":A=PEEK(705)
5491 GOSUB5495:TX$=TX$+" gfx:":A=UP:GOSUB5495:GOSUB5:GOTO5060
5495 IFA=0THENTX$=TX$+"off":RETURN
5496 TX$=TX$+"on":RETURN
7999 PRINT"{ct z}{ct x}{ct c}"
